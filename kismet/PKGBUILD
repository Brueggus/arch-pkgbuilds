# Maintainer:
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Juergen Hoetzel <jason@archlinux.org>

pkgname=kismet
pkgver=2019_05_R2
_realver="${pkgver//_/-}"
pkgrel=2
pkgdesc="802.11 layer2 wireless network detector, sniffer, and intrusion detection system"
arch=('x86_64')
url="https://www.kismetwireless.net/"
license=('GPL')
depends=('libpcap' 'pcre' 'bluez-libs' 'libcap' 'libmicrohttpd' 'protobuf-c')
optdepends=('gpsd: log coordinates of detected networks'
            'wireshark-cli: provide OUI files used to determine device manufacturer'
            'wireshark-cli: mergecap, to merge multiple capture files'
            'sox: provide the default kismet sound playback binary'
            'festival: text-to-speech support'
            'flite:  alternative/lightweight text-to-speech support'
            'ruby: ruby interface')
backup=('etc/kismet.conf' 'etc/kismet_drone.conf')
install=kismet.install
source=("https://www.kismetwireless.net/code/${pkgname}-${_realver}.tar.xz")
sha256sums=('6821e5378f7d75394079c910b1ad49aeadc32507acc5fc85325a7794e0e85c1f')
validpgpkeys=('354689DF3C9DED803381A661D7B28822738BBDB1') # Michael Kershaw

build() {
    cd ${pkgname}-${_realver}
    export LDFLAGS='-Wl,--as-needed'
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
		--disable-python-tools
    make all plugins
}

package() {
    cd ${pkgname}-${_realver}
    make DESTDIR="$pkgdir" install
    # Makepkg strip bug #43600
    chmod u+w "${pkgdir}"/usr/bin/kismet*
}

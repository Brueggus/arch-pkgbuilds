# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: ELmoussaoui Bilal <bil.elmoussaoui@gmail.com>

pkgbase=python-pyotp
pkgname=('python-pyotp' 'python2-pyotp')
_pkgname=pyotp
_gitcommit=b6f7c814c38654bf7061b760ec3049c250ea2fbf
pkgver=2.2.7
pkgrel=1
pkgdesc='PyOTP is a Python library for generating and verifying one-time passwords'
url='https://pyotp.readthedocs.io/'
arch=('any')
license=('MIT')
makedepends=('git' 'python-setuptools' 'python-sphinx'
                   'python2-setuptools' 'python2-sphinx')
options=('!makeflags')
source=("git+https://github.com/pyotp/pyotp#commit=${_gitcommit}?signed")
sha512sums=('SKIP')
validpgpkeys=('29BCBADB4ECAAAC2382699388AFAFCD242818A52') # Andrey Kislyuk <kislyuk@gmail.com>

pkgver() {
  cd ${_pkgname}
  git describe --always --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd ${_pkgname}
  python setup.py build
  python2 setup.py build
  make -C docs text man
}

check() {
  cd ${_pkgname}
  python setup.py test
  python2 setup.py test
}

package_python-pyotp() {
  depends=('python')
  cd ${_pkgname}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst ./docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 ./docs/_build/man/${_pkgname}.1 -t "${pkgdir}/usr/share/man/man1"
  ln -s /usr/share/man/man1/${_pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-pyotp() {
  depends=('python2')
  cd ${_pkgname}
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst ./docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 ./docs/_build/man/${_pkgname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

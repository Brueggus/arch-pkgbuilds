# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niels Abspoel <aboe76 (at) Gmail.com>

_gemname=pg
pkgname=ruby-pg
pkgver=0.20.0
pkgrel=1
pkgdesc='Pg is the Ruby interface to the Postgresql RDBMS'
url='https://bitbucket.org/ged/ruby-pg'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ruby' 'ruby-rspec-core' 'postgresql-libs')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=(${_gemname}-${pkgver}.gem)
sha512sums=('8c7ef2e3b0cb43f42b4f96c5995cd9ff8b01a0d0fc1cfd24d1053e27fa6d16f4eb7008fcb5f86445ad000429898df0215f7ee89b4da9500ddc4df04f5f2a10e2')

package() {
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" pg-${pkgver}.gem
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

# vim: ts=2 sw=2 et:

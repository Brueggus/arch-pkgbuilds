# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Mike Roberts <noodlesgc@gmail.com>

pkgname=libcli
pkgver=1.9.7
pkgrel=3
pkgdesc='Library for including a Cisco-like command-line interface into other software'
url='https://github.com/dparrish/libcli'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libcli.so')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dparrish/libcli/archive/v${pkgver}.tar.gz)
sha512sums=('b2dacfd40bc068ddcc0dc3d60576ef2f7d7e2af80b93c6e7ae899d654afca6f0f8d2df2b33c9c8e760bdbea9aa2eaf757029d46032dcf307341f78f1a8f4f66f')

prepare() {
  sed -i 's/-Werror//' ${pkgname}-${pkgver}/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:

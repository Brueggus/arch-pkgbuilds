# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Mike Roberts <noodlesgc@gmail.com>

pkgname=libcli
pkgver=1.10.0
pkgrel=1
pkgdesc='Library for including a Cisco-like command-line interface into other software'
url='https://github.com/dparrish/libcli'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libcli.so')
source=(https://github.com/dparrish/libcli/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0d8aaad55982581aef637a30f8824557d8460f95503d9731df15a176af7a5b928945bacff0d0cbbc1f4c196ac0c7bc207197cca1873f8f57cd511962f1bbed19')

prepare() {
  sed -i 's/-Werror//' ${pkgname}-${pkgver}/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:

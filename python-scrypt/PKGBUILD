# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-scrypt
pkgver=0.8.13
pkgrel=1
pkgdesc='Bindings for the scrypt key derivation function library'
arch=('x86_64')
url='https://pypi.python.org/pypi/scrypt/'
license=('BSD')
depends=('python' 'glibc' 'openssl')
makedepends=('python-setuptools')
options=('!emptydirs')
source=(https://pypi.io/packages/source/s/scrypt/scrypt-${pkgver}.tar.gz)
sha512sums=('a65e24b0a7dbe02e16c0b38b52c66549f36370e267aacf348a107cb980221806ca783cee0caa95ab6af978dd2b0d8efdb6b06798bb43742d4623bd8f6535e9a5')

build() {
  cd scrypt-${pkgver}
  python setup.py build
}

check() {
  cd scrypt-${pkgver}
  python setup.py test
}

package() {
  cd scrypt-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

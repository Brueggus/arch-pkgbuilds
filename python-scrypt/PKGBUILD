# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-scrypt
pkgver=0.8.15
pkgrel=1
pkgdesc='Bindings for the scrypt key derivation function library'
url='https://github.com/holgern/py-scrypt'
arch=('x86_64')
license=('BSD')
depends=('python' 'glibc' 'openssl')
makedepends=('python-setuptools')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d194a2474305c5115300a69b53374edef7bec89fb93cce85d99fb0bd9ff56b2407e741b73331e83560500d7d91ae2cf88c38f1cae2c79441214c8dc32292b573')
b2sums=('a0bd5bf120058afccd63188c0effddbc72d983735b810ec1de45a8acff9247256c763883ca4e95b7e2592511691e8f7870e7405dbc3f70a0a01fee8438dc44ec')

build() {
  cd py-scrypt-${pkgver}
  python setup.py build
}

check() {
  cd py-scrypt-${pkgver}
  python setup.py test
}

package() {
  cd py-scrypt-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/scrypt/tests
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Phil Pirozhkov <pirjsuka@gmail.com>

pkgname=mtd-utils
pkgver=2.0.2
pkgrel=1
pkgdesc='Utilities for dealing with MTD devices'
arch=('x86_64')
url='http://www.linux-mtd.infradead.org/'
license=('GPL2')
depends=('lzo' 'util-linux')
source=(ftp://ftp.infradead.org/pub/mtd-utils/$pkgname-$pkgver.tar.bz2{,.asc})
sha512sums=('5e8e8159a0aef719193e4248b3ed22aa37bacc26e8fa0cceb7501b0bf05881f5fa1c87599a6fd78a677fe168df3fbae14fa6f3ccaae7c25dbe3c892db243d9a4'
            'SKIP')
validpgpkeys=('13063F723C9E584AEACD5B9BBCE5DC3C741A02D1')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Christian Karl <christian dot karl at protonmail dot com>

pkgname=vimiv
pkgver=0.9
pkgrel=1
pkgdesc='Image viewer with vim-like keybindings'
url='https://github.com/karlch/vimiv'
arch=('x86_64')
license=('MIT')
depends=('python' 'python-gobject' 'gtk3' 'python-pillow' 'glibc')
makedepends=('python-setuptools' 'libgexiv2')
optdepends=('libgexiv2: autorotation and EXIF support')
backup=('etc/vimiv/vimivrc'
        'etc/vimiv/keys.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/karlch/vimiv/archive/v${pkgver}.tar.gz)
sha256sums=('8437bec3f9ea5ffc0afeb14a2274a57b9ad6ff729d0788f944ca44a7d9081e57')
sha512sums=('c6e94e739659ee09efed8bfdd0f9d321f16304df23b86399da21a000554d4225ed0c163324bebb3129dc5d9c734b66cb275f7b2c380fec545bc62cc457b82aad')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

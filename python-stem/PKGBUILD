# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Spider.007 <archlinux AT spider007 DOT net>

pkgname=python-stem
_pypiname=${pkgname#*-}
pkgver=1.7.1
pkgrel=3
pkgdesc='Python controller library for Tor'
url='https://stem.torproject.org/'
arch=('any')
license=('LGPL3')
depends=('python' 'python-cryptography' 'python-pynacl' 'procps-ng')
checkdepends=('tor' 'python-mock')
optdepends=('tor: tor-server to talk to')
replaces=('stem')
provides=('stem')
source=(https://files.pythonhosted.org/packages/source/${_pypiname::1}/${_pypiname}/${_pypiname}-${pkgver}.tar.gz{,.asc}
        stem-python-3.8.patch)
sha256sums=('c9eaf3116cb60c15995cbd3dec3a5cbc50e9bb6e062c4d6d42201e566f498ca2'
            'SKIP'
            '294e645ceba300c34693df2e8269c844686f3a1bb7b9bb1e3ffc6882a07f2505')
validpgpkeys=('68278CC5DD2D1E85C4E45AD90445B7AB9ABBEEC6') # Damian Johnson (www.atagar.com) <atagar1@gmail.com>

prepare() {
  cd ${_pypiname}-${pkgver}
  # remove flaky integration tests
  sed -i test/settings.cfg \
    -e '/|test.integ.client.connection.TestConnection/d' \
    -e '/|test.integ.process.TestProcess/d' \
    -e '/|test.integ.installation.TestInstallation/d'
  rm test/integ/{client/connection,{installation,process}}.py

  # fix tests with python 3.8
  patch -p1 -i ../stem-python-3.8.patch
}

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pypiname}-${pkgver}
  ./run_tests.py --all
}

package() {
  cd ${_pypiname}-${pkgver}
  python setup.py install --optimize=1 --root="${pkgdir}" --skip-build
}

# vim: ts=2 sw=2 et:

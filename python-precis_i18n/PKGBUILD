# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0
pkgrel=1
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/byllyfish/precis_i18n/archive/v${pkgver}.tar.gz
        ${pkgname}-fix-unicode-11.patch::https://github.com/byllyfish/precis_i18n/commit/f68b66a80fa36ff1004f5a88696edb424a75dd76.patch)
sha512sums=('b43622458fefb051e6f5ae6e347abfa65195052c63290b2be3ffb7c05eae0300caee53713d7216ee36c4ef0508cc4828e4d7ed3c65df873a684b803cc19bd410'
            'd8c5a96fcc21a8dddb73de7258bf3eb97fb1ff7fc9437bc97f5a1709b3d0d449f2ffe23fcf34e5ffd0c8bea5291599bede7c5c5b573c31dd4342ec7f36193187')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 < ../${pkgname}-fix-unicode-11.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0.1
pkgrel=1
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://github.com/byllyfish/precis_i18n/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a62e76588ae1ccba6de51488ceae0e2a454e77bdc946e7c4cea1cb41017253b6e239aab120bdbf1a57b51b827ec7554fb7d6fc10a3a4203f09f6f91f1b4192f4')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

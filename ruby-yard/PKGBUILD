# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_gemname=yard
pkgname=ruby-${_gemname}
pkgver=0.9.24
pkgrel=1
pkgdesc='YARD is a Ruby Documentation tool. The Y stands for "Yay!"'
url='https://yardoc.org/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/lsegal/yard/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('cf7f5973e03562c7081b7c213c6dffc01afdc1207f79f52fd51f7e6fd917cdc13e6b5b6e89c93dbe3c05338e3a1418e7c6e8d617e96613b8156b95868b60ce11')
b2sums=('33d0c3c0931b1234814651eaee674cbb18722b15674575860b57c2c7992d160ffe8606290a2c887e61dcf095230a8d98abaa2ac41c92a761272d64ddeacced9e')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_gemname=yard
pkgname=ruby-yard
pkgver=0.9.13
pkgrel=1
pkgdesc='YARD is a Ruby Documentation tool. The Y stands for "Yay!"'
url='https://yardoc.org/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/lsegal/yard/archive/v${pkgver}.tar.gz)
sha256sums=('3e1a93a188156b73a91be93709fb19fb62c177afa8e3527ecba69aa5b611c131')
sha512sums=('8ae886e1c981f017f740e12f07f90470ac9bf420a51f99692b642c212e26231d0a19e1b64084e3a70e09cad4e3a7175278fca66bd3b2420eac4ba197e20876ee')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

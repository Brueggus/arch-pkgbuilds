# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.25.3
pkgrel=1
pkgdesc='Thin wrapper for Terraform that provides extra tools for working with multiple Terraform modules'
url='https://github.com/gruntwork-io/terragrunt'
arch=('x86_64')
makedepends=('git' 'go')
depends=('glibc' 'terraform')
license=('MIT')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('fe0df274084d75899f911934ac253fa4c7ae12a089481f49c14cb9def08984a3')
b2sums=('e89da9fb2b1b5514ba76dec126c36523d7d2937433683c8903341caebaaa4ebcb18afc5a2046e4b922cd15ef8464af952d83e0ee42dd44d4989b94f6bf7b74ab')

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X main.VERSION=${pkgver}" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

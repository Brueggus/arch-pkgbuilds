# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgbase=python-wtforms
pkgname=('python-wtforms' 'python2-wtforms')
pkgver=2.2
pkgrel=1
pkgdesc='Flexible forms validation and rendering library for python web development'
url='http://wtforms.simplecodes.com/'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-sphinx'
             'python2-setuptools' 'python2-sphinx')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wtforms/wtforms/archive/${pkgver}.tar.gz)
sha512sums=('06b26ebe80304ff096400f4b8d22b18219e75f5b7ab9837e4d7ce6977f8f29068b80e3270145c8d43a401a8e4f0154c1b61738b69c84da8a5e01d3de0242203f')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    make -C docs html SPHINXBUILD=sphinx-build
  )
  (cd ${_pyname}-${pkgver}-py2
    make -C docs html SPHINXBUILD=sphinx-build2
  )
}

package_python-wtforms() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_python2-wtforms() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

# vim: ts=2 sw=2 et:

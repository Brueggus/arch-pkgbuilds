# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-libpsl
pkgver=0.21.0
pkgrel=1
pkgdesc='Public Suffix List library (32-bit)'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
depends=('libpsl' 'lib32-glibc' 'lib32-libidn2' 'lib32-libunistring')
makedepends=('libxslt' 'python' 'publicsuffix-list')
provides=('libpsl.so')
source=(https://github.com/rockdaboot/libpsl/releases/download/libpsl-${pkgver}/libpsl-${pkgver}.tar.gz
        libpsl-0.21.0-build-fix.patch::https://github.com/rockdaboot/libpsl/commit/9347024221f4a9d63f9dcafcda13362a7c8d92fe.patch)
sha512sums=('165c4f0b0640a813d512bd916e1532e32e43c8c81a5efd048f3a5b07b1b3c9129b4c4b5008b8b11a7c1b3914caea17564321389cd350bf1d687d53a97f2afa4d'
            '692d6dd6f64653f6e355df0b6cfe519a5c202d966deab066123e814591692a28a805fe794dc57dd6f9ab357b4e0dd8dfdf842239ec39f303fc33d98019e7f24e')

prepare() {
  cd libpsl-${pkgver}
  rm -frv list
  patch -Np1 < ../libpsl-0.21.0-build-fix.patch
  autoreconf -fiv
}

build() {
  cd libpsl-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-static \
    --disable-dependency-tracking \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt
  LC_CTYPE=en_US.UTF-8 make
}

check() {
  cd libpsl-${pkgver}
  make check
}

package() {
  cd libpsl-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libpsl "${pkgdir}"/usr/share/licenses/lib32-libpsl
}

# vim: ts=2 sw=2 et:

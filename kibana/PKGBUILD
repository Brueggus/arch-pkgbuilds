# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Spider.007 <archlinux AT spider007 DOT net>

pkgname=kibana
pkgver=5.6.3
pkgrel=1
pkgdesc='Browser based analytics and search dashboard for Elasticsearch'
url='https://www.elastic.co/products/kibana'
arch=('any')
license=('Apache')
depends=('nodejs')
optdepends=('elasticsearch>=2.4')
backup=('etc/kibana/kibana.yml')
options=('!strip' 'emptydirs')
install=kibana.install
source=(https://artifacts.elastic.co/downloads/${pkgname}/${pkgname}-${pkgver}-linux-x86_64.tar.gz
        kibana.service)
sha256sums=('6f3df48dabf51917252b1d99bea03a66e981812b27097645cc3cbb53281a1c71'
            '34fe64b2f7d27b359b3e2c395f1a30ce48ec2b7116cc974b58345e8928bd843a')
sha512sums=('fe957f0a2def9fb40be32ac77986d5eb7cc3373f828eed8eae074770ef965cf01c36028f2f9790f927e217ee59a283e63273eb768b9d2375aa0cdf15e4963c9a'
            '68b5104ab57942c4a443fddb34ed8ef06659a8af9ab0db76193aad6ac4c4f9857d8fb07368cfab042c8aac9dd96222500d4da83067787e77a3964df094bba28b')

prepare() {
  cd ${pkgbase}-${pkgver}-linux-x86_64
  # set default quiet mode for systemd, cli option forces specified values
  sed -r 's|#(logging.quiet:) false|\1 true|' -i config/kibana.yml
}

package() {
  cd ${pkgbase}-${pkgver}-linux-x86_64

  install -dm 755 "${pkgdir}/usr/share/kibana"
  cp -a * "${pkgdir}/usr/share/kibana"

  install -dm 750 "${pkgdir}/etc/kibana"
  install -Dm 640 config/kibana.yml -t "${pkgdir}/etc/kibana"
  install -Dm 644 "${srcdir}/kibana.service" -t "${pkgdir}/usr/lib/systemd/system"

  chown -R 206:206 "${pkgdir}"/usr/share/kibana/{optimize,data} "${pkgdir}/etc/kibana"
  rm -r "${pkgdir}/usr/share/kibana/node"
}

# vim: ts=2 sw=2 et:

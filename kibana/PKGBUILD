# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Spider.007 <archlinux AT spider007 DOT net>

pkgname=kibana
pkgver=5.6.0
pkgrel=1
pkgdesc='Browser based analytics and search dashboard for Elasticsearch'
url='https://www.elastic.co/products/kibana'
arch=('any')
license=('Apache')
depends=('nodejs')
optdepends=('elasticsearch>=2.4')
backup=('etc/kibana/kibana.yml')
options=('!strip' 'emptydirs')
install=kibana.install
source=(https://artifacts.elastic.co/downloads/${pkgname}/${pkgname}-${pkgver}-linux-x86_64.tar.gz
        kibana.service)
sha256sums=('1feb6de22351a7f4109396d98fd788a7cd625ef23acbe3e85adf1801fef162f6'
            '85d62ab2d8e2667746303f98b055fcc6eae6bde8d378563c1b56057998eb3c98')
sha512sums=('6a5b720a40152206000b2fde12943815c8741afd760018c6e3d9e5b2d2abd48d733c482fd1ba6c0380ebd26292a53da01bb9cef6ce6c4f7ce64fd69739e08d90'
            '573942289964f554541222d2e8cd7851f5a94cabf3d1a26b734fb5f3efbbe5214f0c110eef2eeadd6185ba8765f6e5ab53fc8f40dea7fad5f51fee70cf20a787')

package() {
  cd ${pkgbase}-${pkgver}-linux-x86_64

  install -dm 755 "${pkgdir}/usr/share/kibana"
  cp -a * "${pkgdir}/usr/share/kibana"

  install -dm 750 "${pkgdir}/etc/kibana"
  install -Dm 640 config/kibana.yml -t "${pkgdir}/etc/kibana"
  install -Dm 644 "${srcdir}/kibana.service" -t "${pkgdir}/usr/lib/systemd/system"

  chown -R 206:206 "${pkgdir}"/usr/share/kibana/{optimize,data} "${pkgdir}/etc/kibana"
  rm -r "${pkgdir}/usr/share/kibana/node"
}

# vim: ts=2 sw=2 et:

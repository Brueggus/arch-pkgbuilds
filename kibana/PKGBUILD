# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Spider.007 <archlinux AT spider007 DOT net>

pkgname=kibana
pkgver=5.5.0
pkgrel=2
pkgdesc='Browser based analytics and search dashboard for Elasticsearch'
url='https://www.elastic.co/products/kibana'
arch=('any')
license=('Apache')
depends=('nodejs')
optdepends=('elasticsearch>=2.4')
backup=('etc/kibana/kibana.yml')
options=('!strip' 'emptydirs')
install=kibana.install
source=(https://artifacts.elastic.co/downloads/${pkgname}/${pkgname}-${pkgver}-linux-x86_64.tar.gz
        kibana.service)
sha256sums=('8aa30bf3a4a453bdbcfad4e96fabf518b4bff7532a67b8001d3e293fcd625cd2'
            '85d62ab2d8e2667746303f98b055fcc6eae6bde8d378563c1b56057998eb3c98')
sha512sums=('c4c9ad706050b3224b05b155b0c25f1c04fc3c53800f9b29e7162016599e6429ff0d08f07660ef865dc79dbba39f2cc16d0ed31b4cdc0d1eac920951411e4131'
            '573942289964f554541222d2e8cd7851f5a94cabf3d1a26b734fb5f3efbbe5214f0c110eef2eeadd6185ba8765f6e5ab53fc8f40dea7fad5f51fee70cf20a787')

package() {
  cd ${pkgbase}-${pkgver}-linux-x86_64

  install -dm 755 "${pkgdir}/usr/share/kibana"
  cp -a * "${pkgdir}/usr/share/kibana"

  install -dm 750 "${pkgdir}/etc/kibana"
  install -Dm 640 config/kibana.yml -t "${pkgdir}/etc/kibana"
  install -Dm 644 "${srcdir}/kibana.service" -t "${pkgdir}/usr/lib/systemd/system"
  
  chown -R 206:206 "${pkgdir}"/usr/share/kibana/{optimize,data} "${pkgdir}/etc/kibana"
  rm -r "${pkgdir}/usr/share/kibana/node"
}

# vim: ts=2 sw=2 et:

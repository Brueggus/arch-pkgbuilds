# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ã‰tienne Deparis <etienne@depar.is>
# Contributor: Christoph J. Thompson <thompsonc@protonmail.ch>

pkgname=nitrokey-app
pkgver=1.3.2
pkgrel=3
pkgdesc='Nitrokey management application'
url='https://www.nitrokey.com'
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'hicolor-icon-theme' 'libnitrokey' 'libnitrokey.so')
makedepends=('cmake' 'qt5-tools' 'bash-completion' 'cppcodec')
source=(https://github.com/Nitrokey/nitrokey-app/releases/download/v${pkgver}/nitrokey-app-v${pkgver}.tar.gz{,.sig})
sha512sums=('e962a088011c8b6bb3f17c354dbef93c02880157bb33404fea796141630a84af5a9a5d2f1e700c0c144da41970ff9ddf91669841d3a684f234cf2156f64fb6ae'
            'SKIP')
validpgpkeys=('868184069239FF65DE0BCD7DD9BAE35991DE5B22') # Szczepan Zalega <szczepan.zalega@gmail.com>

prepare() {
  cd ${pkgname}-v${pkgver}
  mkdir -p build
  rm -r data/icons/ubuntu-mono-{dark,light} 3rdparty/cppcodec
  ln -s /usr/include/cppcodec 3rdparty/cppcodec
}

build() {
  cd ${pkgname}-v${pkgver}/build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${pkgname}-v${pkgver}
  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>

pkgbase=wireshark
pkgname=('wireshark-cli' 'wireshark-common' 'wireshark-gtk' 'wireshark-qt')
pkgver=2.6.5
pkgrel=1
pkgdesc='Network traffic and protocol analyzer/sniffer'
url='https://www.wireshark.org/'
arch=('x86_64')
license=('GPL2')
makedepends=('c-ares' 'libmaxminddb' 'qt5-tools' 'qt5-multimedia' 'gtk3' 'krb5' 'libpcap' 'bash' 'libssh'
             'gnutls' 'portaudio' 'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
options=('!emptydirs')
source=(https://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.xz
        wireshark.sysusers
        do_not_use_svn_version.patch)
sha512sums=('fed2d08bb0ba06496c22a5d45946faf2ff4ca19c59faf758c668c583193faeec3c4379b64750478469fede91562891e9b6991591a09a3da8d2193cb64718d02f'
            '3956c1226e64f0ce4df463f80b55b15eed06ecd9b8703b3e8309d4236a6e1ca84e43007336f3987bc862d8a5e7cfcaaf6653125d2a34999a0f1357c52e7c4990'
            '847eac73c789b02f57f0ec378d086aea5a103cf0c6525c227d168ad67db08703686d4df37ed2ac95f47125cfcd5cafd472e2988f7f743428b291dc2aebe0f435')

prepare() {
  cd ${pkgbase}-${pkgver}
  # Avoid ugly "svn rev unknown from unknown" version string
  patch -p1 < "${srcdir}/do_not_use_svn_version.patch"
}

build() {
  cd ${pkgbase}-${pkgver}
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-qt=5 \
      --with-gtk=3 \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5 \
      --with-c-ares \
      --with-maxmind
  make all
}

package_wireshark-cli() {
  pkgdesc+=' - CLI version'
  depends=('c-ares' 'libmaxminddb' 'krb5' 'libgcrypt' 'libcap' 'libpcap' 'gnutls' 'glib2' 'lua52' 'libssh' 'libxml2' 'libnghttp2')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" uninstall-local

  # wireshark uid group is 150
  install -Dm644 "${srcdir}/wireshark.sysusers" "${pkgdir}/usr/lib/sysusers.d/wireshark.conf"
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark" "${pkgdir}/usr/bin/wireshark-gtk"

  # headers
  install -Dm 644 *.h -t "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes epan/wmem wiretap wsutil; do
    install -Dm 644 ${d}/*.h -t "${pkgdir}/usr/include/${pkgbase}/${d}"
  done
}

package_wireshark-common() {
  pkgdesc+=' - common files'
  depends=('hicolor-icon-theme' 'shared-mime-info' 'xdg-utils')

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install-data-local
  rm "${pkgdir}"/usr/share/applications/wireshark{,-gtk}.desktop
}

package_wireshark-gtk() {
  pkgdesc+=' - GTK frontend'
  depends=('desktop-file-utils' 'gtk3' 'portaudio' 'wireshark-cli' 'wireshark-common')
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}
  install -Dm 755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk"
  install -Dm 644 wireshark-gtk.desktop -t "${pkgdir}/usr/share/applications"
}

package_wireshark-qt() {
  pkgdesc+=' - Qt frontend'
  depends=('desktop-file-utils' 'qt5-multimedia' 'wireshark-cli' 'wireshark-common')

  cd ${pkgbase}-${pkgver}
  install -Dm 755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  install -Dm 644 wireshark.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:

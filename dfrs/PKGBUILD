# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dfrs
pkgver=0.0.2
pkgrel=1
pkgdesc='Display file system space usage using graphs and colors'
url='https://github.com/anthraxx/dfrs'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
sha512sums=('1ed745a243790e76bf6098e4cb7e791b31f314462b774f55abdf4849fc08294717a3ed05f39c4bea058e8c535cddd447e9bf86f450b95bc495dd226dff31ea03'
            'SKIP')
b2sums=('717c26c3678690272df63778d727809ecb6aaceefaf76e1be9b35f2956f29c76fb953c853e109abcad91a5055ee221205dc0486f6e4d08ca4569032134bf8933'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

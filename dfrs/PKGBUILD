# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dfrs
pkgver=0.0.3
pkgrel=1
pkgdesc='Display file system space usage using graphs and colors'
url='https://github.com/anthraxx/dfrs'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'scdoc')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
options=('!makeflags')
sha512sums=('b838d77109a3067371b5a19117becd103f9ff0b01afebe2dcb1cab15eccfb7d0162deab0b0420e4e902910e28a3dbde1e379af247e6007736bc524599a72e0bf'
            'SKIP')
b2sums=('dcaee03cc0ee62fce87058859b4d40d9a2ea0d90b49e096d641d8a4396f206b6faa7e1f0568f6de48ee0f78ffe4413aa846268966e281f03235baaded0214775'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dfrs
pkgver=0.0.4
pkgrel=1
pkgdesc='Display file system space usage using graphs and colors'
url='https://github.com/anthraxx/dfrs'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'scdoc')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
options=('!makeflags')
sha512sums=('a3a628742e61f4283620584d4a71d805ced1c9c76349be67d195e0886429c1a3e865d432d933663d154f1184023b3205ca89075df918e4a9a23c6f40a0315dd7'
            'SKIP')
b2sums=('53a97c5085d3b88a685796f7fb487fd75f07d370965d4b2e1be5c0f66d51fe8dffca9afd40edbc5e2e2ce5eeff82d5e8ca082d4c93e79cbc9d49132d5d78939c'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

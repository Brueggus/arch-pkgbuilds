# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sass
pkgname=ruby-sass
pkgver=3.5.3
pkgrel=1
pkgdesc='Sass makes CSS fun again'
url='http://sass-lang.com/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-sass-listen' 'ruby-yard' 'ruby-maruku')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sass/sass/archive/${pkgver}.tar.gz)
sha256sums=('159f6c9211c256befcaa52ce0f15e28573e3f5c064d14f2f5f1148964a7e6cd5')
sha512sums=('ab03dce49ce26b41f632e540a9ff95f112c6b449be2687317a8178411a521d3cf9ec0a5e84d6924cbfa50cfaa21530dbf421a7228acd97ef568a10343e1053be')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

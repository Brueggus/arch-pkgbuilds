# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pwndbg
pkgver=2017.02.01
pkgrel=1
pkgdesc='Makes debugging with GDB suck less'
url='https://github.com/pwndbg/pwndbg'
arch=('any')
license=('MIT')
depends=('python-capstone' 'python-unicorn' 'python-pycparser' 'python-psutil' 'python-ptrace'
         'python-pyelftools' 'python-six' 'python-future')
optdepends=('checksec: checksec command support'
            'ropper: ropper command support'
            'ropgadget: ropgadget command support'
            'radare2: radare2 command support')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/pwndbg/pwndbg/archive/${pkgver}.tar.gz)
sha512sums=('c605e35dd009ba04621a76c1bccd18def694ecbda91f4dde7db096454c26615e27b41071c44cbefdb3e0ad3588f4b6341711384e37a039c35866e01186a1ade9')

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/0L/d' -i ida_script.py
}

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr/share/pwndbg"
  cp -r *.py pwndbg __pycache__ "${pkgdir}/usr/share/pwndbg"
  install -Dm 644 README.md FEATURES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pwndbg
pkgver=2019.12.09
pkgrel=1
pkgdesc='Makes debugging with GDB suck less'
url='https://github.com/pwndbg/pwndbg'
arch=('any')
license=('MIT')
depends=('gdb' 'python-capstone' 'python-unicorn' 'python-pycparser' 'python-psutil' 'python-ptrace'
         'python-pyelftools' 'python-six' 'python-future' 'python-pygments')
optdepends=('checksec: checksec command support'
            'ropper: ropper command support'
            'ropgadget: ropgadget command support'
            'radare2: radare2 command support')
source=(https://github.com/pwndbg/pwndbg/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('99ebb74ad262a074fe141b052608eed17270b8f72cb598bc802072e4269ef7b3cf86cef7d0131ffa8194321ec000788b668c21202925673d35343de7ed6e75c1')
b2sums=('0f67f0be764e19c1ac7aacfea7012ee74cdb0867b39d5b01ee5902700b0de8337673b6d1f373497830fd7cd6da0f9238ff12dbf7e6d02e00e18b82cdca46c6c3')

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/0L/d' -i ida_script.py
}

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr/share/pwndbg"
  cp -r *.py pwndbg __pycache__ "${pkgdir}/usr/share/pwndbg"
  install -Dm 644 README.md FEATURES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

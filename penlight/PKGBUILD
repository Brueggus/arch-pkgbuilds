# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight')
pkgver=1.6.0
pkgrel=1
pkgdesc='Lua libraries focusing on input data handling'
url='https://github.com/stevedonovan/penlight'
arch=('any')
license=('custom')
depends=('lua' 'lua-filesystem')
source=(https://github.com/stevedonovan/penlight/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a552d0a314f7aa94c9579746996a7aad4ed59f3187f33b4735d3e323e27354b0')
sha512sums=('4f9988e9e5d66aad1c7137a5b8a36e4eaa2dba5408c90ee0253ce3db69980b2e0be8c9ba38f1e88f269da96d09a908115e0e9c2bd0667393f950fdfc1e232ff0')

check() {
  cd Penlight-${pkgver}
  export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;$(lua -e 'print(package.path)')"
  lua run.lua
}

package_lua-penlight() {
  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/5.3/pl"
  install -Dm 644 CONTRIBUTING.md CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

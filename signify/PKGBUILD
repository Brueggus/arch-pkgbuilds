# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=signify
pkgver=26
pkgrel=1
pkgdesc='OpenBSD tool to signs and verify signatures on files'
url='https://github.com/aperezdc/signify'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'libbsd>=0.8')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/aperezdc/${pkgname}/archive/v${pkgver}.tar.gz
        LICENSE)
sha512sums=('84edacc480c5551bdc01db6e135539968676f23343beac3b09f4444bee562b1a6cb6653121ad60421920960ce6f75d02201f9741f9b5821f31e8478ab0f65a35'
            'a587ef19d5481a81945475a2c1dde0fbf8022e0a28db06389c73df1b7118fb94399f5e4523b91df3d846707dcaffff482ae6e58902b6620bf7b2019a8e15efec')

prepare() {
  cd ${pkgname}-${pkgver}
  # using O_NOFOLLOW doesn't lock anything down here, swapping files
  # without symlinks is still equally open to TOCTOU and it doesn't even
  # matter here. lets prefere supporting symlinked target files.
  sed -E 's/\| ?O_NOFOLLOW//g' -i ./*.c
}

build () {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr \
    LTO=1 \
    EXTRA_CFLAGS="${CFLAGS} ${CPPFLAGS}" \
    EXTRA_LDFLAGS="${LDFLAGS}" \
    GIT_TAG=''
}

package () {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr \
    DESTDIR="${pkgdir}" \
    GIT_TAG='' install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${srcdir}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

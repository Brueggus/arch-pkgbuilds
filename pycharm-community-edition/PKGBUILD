# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2019.2.2
_build=192.6603.34
pkgrel=1
pkgdesc='Python IDE for Professional Developers'
arch=(x86_64)
url=https://www.jetbrains.com/pycharm/
license=(APACHE)
depends=(
  giflib
  glib2
  java-runtime=11
  python
  sh
  ttf-font
)
optdepends=(
  'ipython2: IPython integration for Python 2'
  'ipython: IPython integration for Python 3'
  'python2: Python 2 support'
)
makedepends=(
  ant
  git
  java-environment=8
  java8-openjfx
  python2-setuptools
  python-setuptools
)
source=(
  git+https://github.com/JetBrains/intellij-community.git#commit=34475245b1c4c3371dfdf9c99a4684d6849b6ac5
  pycharm.desktop
  pycharm.sh
)
sha256sums=('SKIP'
            '28e0d3200c721e61831d8246eba8f72485d3c8379d10bfa75ccb5331b6749480'
            'eaae397aebb9df4681f5813c82afcc2292e3ea2978d02ce68be3ee89c824e996')

prepare() {
  cd intellij-community

  echo ${_build} > build.txt
  sed '/def targetOs =/c def targetOs = "linux"' -i build/dependencies/setupJbre.gradle
  sed '/String targetOS/c   String targetOS = OS_LINUX' -i platform/build-scripts/groovy/org/jetbrains/intellij/build/BuildOptions.groovy
  sed "s/SNAPSHOT/PC-${_build}/; s/-Xmx612m -XX:MaxPermSize=152m/-Xmx2048m -XX:MaxPermSize=512m/" -i python/build.xml
  sed '/intellij\.markdown/d' -i python/{build/plugin-list.txt,intellij.pycharm.community.main.iml}
}

build() {
  cd intellij-community/python

  unset _JAVA_OPTIONS
  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk
  export PATH="$JAVA_HOME/bin:$PATH"

  ant -Dintellij.build.target.os=linux build
  tar -xf ../out/pycharm-ce/artifacts/pycharmPC-${_build}-no-jbr.tar.gz -C "${srcdir}"

  cd ../../pycharm-community-${pkgver}

  python2 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  python3 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {
  cd pycharm-community-${pkgver}

  # workaround FS#40934
  sed -i 's/lcd/on/' bin/*.vmoptions

  rm -rf bin/fsnotifier{,-arm} lib/libpty/linux/x86

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,pycharm}
  cp -dr --no-preserve='ownership' bin helpers lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -Dm 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../pycharm.sh "${pkgdir}"/usr/bin/pycharm
}

# vim: ts=2 sw=2 et:

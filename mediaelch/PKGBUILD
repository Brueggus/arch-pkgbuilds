# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.6.4
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='http://www.mediaelch.de/'
arch=('x86_64')
license=('LGPL3')
depends=('libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6754fbda31ceb9a7492879c945e766a59ed94611cfb8815b17838907cdb77318')
b2sums=('5252546b3239d308a3e0c644e14808e2a7a462d1caad3394be8b18b93a137465934102ccb8e5bfc1355f4dfc2f9d013a0606dd6537e50a91cbe85a3043bef584')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|quazip5/|g' -i src/*/*.cpp src/*/*.h
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

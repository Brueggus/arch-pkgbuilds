# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.8.4
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1bda81d37d5e5222c26f1c3902054db5d0b0f47cd02293db277321a7428b0366')
b2sums=('45973ec557c615387fedd1e2d299547f2a8b17fc7be190c775a4047868dd9455d6b2e4034ae46a02e8f246f7c3e170bf777baef102dc13750bb601b1bfe01509')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|QuaZip-Qt5-1.1/quazip/|g' -i src/*/*.cpp src/*/*.h
  sed 's|lquazip5|lquazip1-qt5|g' -i MediaElch.pro
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip1-qt5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

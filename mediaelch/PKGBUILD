# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.6.2
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='http://www.mediaelch.de/'
arch=('x86_64')
license=('LGPL3')
depends=('libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bb2df14b7f9cef2d3004a505d6d5eeb57fae6e4234c557e76d924f0b2b5e3573')
sha512sums=('e56358e3005c4e2c7b3c8179a79219f616610222d502e32c74465b941b37a1349bae6a266af62b27d60152f160bef8aee67770dce940d62da5ea82ff21afdcd8')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|quazip5/|g' -i src/*/*.cpp src/*/*.h
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

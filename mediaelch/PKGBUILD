# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.8.0
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('034ca43c350b16933c2b83bae368f920824e765524bda56ab2324946d4245ef7')
b2sums=('68cb5de5e7d5d92f5771889be898fbb3db2e94ba62760811419de3e70e23dea1743fd86792a98f3b3113df3dc5db3c2832f8418fde3e97df48e1888eb31de479')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|QuaZip-Qt5-1.1/quazip/|g' -i src/*/*.cpp src/*/*.h
  sed 's|lquazip5|lquazip1-qt5|g' -i MediaElch.pro
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip1-qt5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.6.0
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='http://www.mediaelch.de/'
arch=('x86_64')
license=('LGPL3')
depends=('libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Komet/MediaElch/archive/v${pkgver}.tar.gz)
sha256sums=('b296770b3f8ad63a1ad9fb56436f4c4dfcfe334493543f9aefe66dd75120eb20')
sha512sums=('842803ea34065a6fa792c3fc6a098de6977801eff1df9b4e293834cb02991bd71d962f88b975a84028802be4cfd2810ca4c397f5f40d57fe4bd5516d1d668f58')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|quazip5/|g' -i src/*/*.cpp src/*/*.h
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

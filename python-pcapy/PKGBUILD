# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

_pyname=pcapy
pkgbase=python-pcapy
pkgname=('python-pcapy' 'python2-pcapy')
pkgver=0.11.3
pkgrel=1
pkgdesc='Extension module that interfaces with the libpcap packet capture library'
url='https://github.com/CoreSecurity/pcapy'
arch=('x86_64')
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'libpcap')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/CoreSecurity/pcapy/archive/${pkgver}.tar.gz)
sha512sums=('1fb385e9de757346876cc5a4daf5399380e5e41632c4d9b02385833b55396342d082e5e86a284f7d1bf65eda886cc9aa1d324e77fabad7f8ca6731837f03f08a')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  msg2 'Building python2...'
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 'Checking python...'
  (cd ${_pyname}-${pkgver}/tests
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python pcapytests.py
  )
  msg2 'Checking python2...'
  (cd ${_pyname}-${pkgver}-py2/tests
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 pcapytests.py
  )
}

package_python-pcapy() {
  depends=('python' 'libpcap')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/pcapy"
}

package_python2-pcapy() {
  depends=('python2' 'libpcap')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/pcapy"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anthony25 <Anthony Ruhier>

_pkgname=Flask-Migrate
pkgbase=python-flask-migrate
pkgname=('python-flask-migrate' 'python2-flask-migrate')
pkgver=2.4.0
pkgrel=1
pkgdesc='SQLAlchemy database migrations for Flask applications using Alembic..'
url='https://github.com/miguelgrinberg/Flask-Migrate/'
arch=('any')
license=('MIT')
makedepends=('python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy' 'python-setuptools' 'python-sphinx'
             'python2-alembic' 'python2-flask' 'python2-flask-script' 'python2-flask-sqlalchemy' 'python2-setuptools' 'python2-sphinx')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-Migrate/archive/v${pkgver}.tar.gz)
sha256sums=('507ad1d83be82efc923c951921843050044098908ead0b76ee52fffef552471e')
sha512sums=('aec265c506483af5117c4866f96e9fc95c779e7a96461bd8a816638836b6e8b66bd75e4bb7e1ce06729411b0be9b7cb6f6971be7b5649619cd9087a09f70a5ca')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  msg2 "Building python..."
  (cd ${_pkgname}-${pkgver}
    python setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build
  )
  msg2 "Building python2..."
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  msg2 "Checking python..."
  (cd ${_pkgname}-${pkgver}
    python setup.py test
  )
  msg2 "Checking python2..."
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-flask-migrate() {
  depends=('python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy' 'python-setuptools')
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-migrate() {
  depends=('python2-alembic' 'python2-flask' 'python2-flask-script' 'python2-flask-sqlalchemy' 'python2-setuptools')
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

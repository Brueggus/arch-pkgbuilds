# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anthony25 <Anthony Ruhier>

_pkgname=Flask-Migrate
pkgname=python-flask-migrate
pkgver=2.5.2
pkgrel=3
pkgdesc='SQLAlchemy database migrations for Flask applications using Alembic..'
url='https://github.com/miguelgrinberg/Flask-Migrate/'
arch=('any')
license=('MIT')
depends=('python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy' 'python-setuptools')
makedepends=('python-sphinx')
source=(https://github.com/miguelgrinberg/Flask-Migrate/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ddf53166d92f29c5b6aca4abf6371d2e596dcd22a03ecaa8ba6a2393adf222eb')
sha512sums=('f7355bfa41972c93b8975e37dc8ab01935ff71fcced7198cf9db015f9f88df07b82b1841e938611fe3272a3f7df2fb878568ba0d17fdb87e81b46dec03bc2565')

build() {
  msg2 "Building python..."
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -j1 -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  msg2 "Checking python..."
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

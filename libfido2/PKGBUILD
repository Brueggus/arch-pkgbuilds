# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de

pkgname=libfido2
# TODO: add hidapi dependency for 1.4.0 release
pkgver=1.3.0
pkgrel=4
pkgdesc='Library functionality for FIDO 2.0, including communication with a device over USB'
url='https://developers.yubico.com/libfido2/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl' 'libcbor' 'libcbor.so' 'hidapi' 'systemd-libs'  'libudev.so')
makedepends=('cmake')
provides=('libfido2.so')
source=(https://developers.yubico.com/libfido2/Releases/libfido2-${pkgver}.tar.gz{,.sig})
sha256sums=('0b2e3671c4c5d42fd5604a08e45f89f49592b97cf66d7d3bfbc7e6a4d5a0fec7'
            'SKIP')
b2sums=('18a0036127a9f643ef7ca917dab909bfa344caecdfdef3f1f0881bd7c84b73ea8273a57a2daccc95cc448d7777c389a8f573f663895abd78c48b3340a45f7764'
        'SKIP')
validpgpkeys=('EE90AE0D19774C8386628FAAB428949EF7914718') # pedro martelletto <pedro@yubico.com>

build() {
  mkdir -p ${pkgname}-${pkgver}/build
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=None
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.15.10
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'libsass')
makedepends=('rust' 'cargo')
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('5b50036c26a6a9ed2fca51c285102884fd3fba57d9f1d1239d0a47d89aa02ca8948ac96fef2c1d126309644812f6785525da1daa58daa69cbbae52dc7ac261a1')
b2sums=('447e52a3f042a98c901d0666ae4a0fd190b861b204064a0c969e88fa6f265cfc87ab3eb9e0eacde6ee9649b23a8e31fc6f00112d61a484a5907cb46eea8fbf0d')

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --release --locked --features 'syntax-highlight sass'
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.15.9
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'libsass')
makedepends=('rust' 'cargo')
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('f2147933c61d30764b0825f8e2dab3ff6fcda2bc2d1b41c4384e0e1cf005e88ec0a8e163b3ebb5e133562ec4f0321ccf0946739d25842006ecb2a0b134fb45fa')
b2sums=('4c3c82b0dc1553fd553e1c4e2e35e0292777e7281436dd30049480641b07b48a745a84a0842cd6274b799da324d13914a50002b8701934f6c4507892dc8ba47d')

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --release --locked --features 'syntax-highlight sass'
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: James Reed <supplantr at archlinux dot info>

pkgname=tcplay
pkgver=3.1
pkgrel=1
pkgdesc='Free and simple TrueCrypt implementation based on dm-crypt'
url='https://github.com/bwalex/tc-play'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'device-mapper' 'libutil-linux' 'libgcrypt' 'libuuid.so')
makedepends=('cmake' 'udev')
provides=('libtcplay.so')
source=(https://github.com/bwalex/tc-play/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('edca7cf5ea4ad824adb86be83ab742781a87e6b16c53a045bdd1b781a8da75e6')
b2sums=('03195a3fffae843ae9336bc7b2fdd8834c7f31bedabb9014b041ac49768cebd9bd37c7e2000c6dbab1878ab2ec5d9c2dc8ba372b7e0e538b5613c047d527caa7')

build() {
  mkdir -p tc-play-${pkgver}/build
  cd tc-play-${pkgver}/build
  # cmake is stupid
  export CFLAGS+=" ${CPPFLAGS}"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_SUFFIX= \
    -DSBIN_DIR=bin ..
  make VERBOSE=1
}

package() {
  cd tc-play-${pkgver}
  make -C build DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGELOG README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-mail
pkgbase=python-flask-mail
pkgname=('python-flask-mail' 'python2-flask-mail')
pkgver=0.9.1
pkgrel=3
pkgdesc='Flask extension providing simple email sending capabilities'
url='https://pythonhosted.org/Flask-Mail/'
arch=('any')
license=('BSD')
makedepends=('python-flask' 'python-blinker' 'python-sphinx' 'python-setuptools'
             'python2-flask' 'python2-blinker' 'python2-sphinx' 'python2-setuptools')
checkdepends=('python-nose' 'python-blinker' 'python-speaklater' 'python-mock'
              'python2-nose' 'python2-blinker' 'python2-speaklater' 'python2-mock')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/mattupstate/flask-mail/archive/${pkgver}.tar.gz
        ${pkgbase}-fix-headers-for-attachments.patch::https://github.com/mattupstate/flask-mail/commit/e195fca6de1077cabb711426e6378f51dc39d598.patch)
sha512sums=('1cf45ee734b35bf05862a4e88070a743a7d3cc877e4dcdc55f5e0a9642631752c4bf1e23065212157e71e42c5fdb010de8a68b4ab9f8444f9fa096d62a7aad3d'
            '7e85212c213307ede608f57e00f3a7109408216d999ed6823caad758231aaac5493dbab9a625d46f4a42278641f8d56aa3733fc9f87bb4b912b87a62b16e6814')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 < ../${pkgbase}-fix-headers-for-attachments.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
  python2 setup.py build
  sphinx-build2 -b text docs docs/_build2/text
  sphinx-build2 -b man docs docs/_build2/man
}

check() {
  cd ${_pkgname}-${pkgver}
  nosetests
  nosetests2
}

package_python-flask-mail() {
  depends=('python' 'python-flask' 'python-blinker')
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-mail.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-mail() {
  depends=('python2' 'python2-flask' 'python2-blinker')
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build2/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build2/man/flask-mail.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=aliyun-cli
pkgver=3.0.70
_aliyun_openapi_meta_commit=81f6d6f81d8c8ac36016f5c97538e49daa310496
pkgrel=1
pkgdesc='Alibaba Cloud CLI'
arch=('x86_64')
url='https://github.com/aliyun/aliyun-cli'
license=('APACHE')
depends=('glibc' 'jq')
makedepends=('git' 'go' 'go-bindata')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        "git+https://github.com/aliyun/aliyun-openapi-meta#commit=${_aliyun_openapi_meta_commit}")
sha512sums=('6f70b492571c7b28bd27d10dcad4d540a0be13a2b783077299edba5f6d026cf63d7aab648a5077e43805dadc9bb67b41c72466d54f47f15c873a2c5b953d6c40'
            'SKIP')
b2sums=('86aaddf0017d37e350d31f5448d0e3248c9d5429b0b65631fc286d3faf935b2eff218329642cc24d18f5127cf572cc891044d2b9a329d2a677a3de9da1d45d0c'
        'SKIP')

prepare() {
  export GOPATH="${srcdir}/go"

  mkdir -p "${GOPATH}/src/github.com/aliyun"
  ln -rTsf "${pkgname}-${pkgver}" "${GOPATH}/src/github.com/aliyun/${pkgname}"
  ln -rTsf aliyun-openapi-meta "${GOPATH}/src/github.com/aliyun/aliyun-openapi-meta"
}

build() {
  export GOPATH="${srcdir}/go"
  export PATH="${GOPATH}/bin:${PATH}"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=vendor -modcacherw -ldflags=-linkmode=external"

  go-bindata \
      -o resource/metas.go \
      -pkg resource \
      ../aliyun-openapi-meta/...

  go build \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X 'github.com/aliyun/aliyun-cli/cli.Version=${pkgver}'" \
    -o ./out/aliyun ./main/main.go
}

check() {
  export GOPATH="${srcdir}/go"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  # Horrible but needed for the ./cli/ tests
  touch "${HOME}/.bashrc"

  # for now can't test the `./oss/...` folder, because it needs an env file that is not so easy to have in dev
  go test \
    ./cli/... ./command/... ./config/... ./i18n/... ./main/... ./openapi/... ./resource/...
}

package() {
  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"
  install -Dm 755 out/aliyun "${pkgdir}/usr/bin/aliyun"
  install -Dm 644 README*.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 bin/README.md -t "${pkgdir}/usr/share/doc/${pkgname}/bin"
}

# vim: ts=2 sw=2 et:

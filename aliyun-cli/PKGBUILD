# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=aliyun-cli
pkgver=3.0.71
_aliyun_openapi_meta_commit=d45282a09bd3a5f18617f3bf08d496ac65a529ac
pkgrel=1
pkgdesc='Alibaba Cloud CLI'
arch=('x86_64')
url='https://github.com/aliyun/aliyun-cli'
license=('APACHE')
depends=('glibc' 'jq')
makedepends=('git' 'go' 'go-bindata')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        "git+https://github.com/aliyun/aliyun-openapi-meta#commit=${_aliyun_openapi_meta_commit}")
sha512sums=('5d28f8d74de3a819141f886327b998cb1e09718b8beed8ded624aca3c8676d9e320aacbb5fda1c5e363594c8c0ee3544034583799b8f8944b6d734cafb6db45a'
            'SKIP')
b2sums=('772ce29c75353257aa72a3d69eecea1aff55d7fb04431f993742cc05c9362e6c8933743c144e2f681af911583424bddb0dc7ab5b603f93f209a74122deb31a86'
        'SKIP')

prepare() {
  export GOPATH="${srcdir}/go"

  mkdir -p "${GOPATH}/src/github.com/aliyun"
  ln -rTsf "${pkgname}-${pkgver}" "${GOPATH}/src/github.com/aliyun/${pkgname}"
  ln -rTsf aliyun-openapi-meta "${GOPATH}/src/github.com/aliyun/aliyun-openapi-meta"
}

build() {
  export GOPATH="${srcdir}/go"
  export PATH="${GOPATH}/bin:${PATH}"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=vendor -modcacherw -ldflags=-linkmode=external"

  go-bindata \
      -o resource/metas.go \
      -pkg resource \
      ../aliyun-openapi-meta/...

  go build \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X 'github.com/aliyun/aliyun-cli/cli.Version=${pkgver}'" \
    -o ./out/aliyun ./main/main.go
}

check() {
  export GOPATH="${srcdir}/go"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  # Horrible but needed for the ./cli/ tests
  touch "${HOME}/.bashrc"

  # for now can't test the `./oss/...` folder, because it needs an env file that is not so easy to have in dev
  go test \
    ./cli/... ./command/... ./config/... ./i18n/... ./main/... ./openapi/... ./resource/...
}

package() {
  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"
  install -Dm 755 out/aliyun "${pkgdir}/usr/bin/aliyun"
  install -Dm 644 README*.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 bin/README.md -t "${pkgdir}/usr/share/doc/${pkgname}/bin"
}

# vim: ts=2 sw=2 et:

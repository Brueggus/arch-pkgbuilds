# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=aliyun-cli
pkgver=3.0.42
_jteeuwen_go_bindata_commit=6025e8de665b31fa74ab1a66f2cddd8c0abf887e
_aliyun_openapi_meta_commit=8ee9c7a0cd24e45f5f729c98395b944b4cc89e4a
pkgrel=1
pkgdesc='Alibaba Cloud CLI'
arch=('x86_64')
url='https://github.com/aliyun/aliyun-cli'
license=('APACHE')
depends=('glibc' 'jq')
makedepends=('git' 'go' 'go-bindata')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        "git+https://github.com/aliyun/aliyun-openapi-meta#commit=${_aliyun_openapi_meta_commit}"
        "jteeuwen-go-bindata::git+https://github.com/jteeuwen/go-bindata#commit=${_jteeuwen_go_bindata_commit}")
sha512sums=('b446d55254db7628004440da8b7fe8c43e382f6fd7712490c656e0e05048eea3377155f867d9fb9d16b06e585d2e0eddf94e6c5ebc1e3bd43b32473f2e84f641'
            'SKIP'
            'SKIP')
b2sums=('60eeace414f8ebab48f839599d8ba864f570014d32fa50ca9544209479c47e339bb6c50cb1c2d3cc41ca7b6f1e895f1ea3768718504740cf178ea0615d105d25'
        'SKIP'
        'SKIP')

prepare() {
  export GOPATH="${srcdir}/go"

  mkdir -p "${GOPATH}/src/github.com/"{aliyun,jteeuwen}
  ln -rTsf "${pkgname}-${pkgver}" "${GOPATH}/src/github.com/aliyun/${pkgname}"
  ln -rTsf aliyun-openapi-meta "${GOPATH}/src/github.com/aliyun/aliyun-openapi-meta"

  # for now we can't use the go-bindata package as this breaks the resulting binary.
  # The issue has been reported upstream: https://github.com/aliyun/aliyun-cli/issues/262
  ln -rTsf jteeuwen-go-bindata "${GOPATH}/src/github.com/jteeuwen/go-bindata"
}

build() {
  export GOPATH="${srcdir}/go"
  export PATH="${GOPATH}/bin:${PATH}"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=vendor"

  #pushd "${srcdir}/jteeuwen-go-bindata/go-bindata"
  #go build
  #popd

  go-bindata \
      -o resource/metas.go \
      -pkg resource \
      ../aliyun-openapi-meta/...

  go build \
    -ldflags "-X 'github.com/aliyun/aliyun-cli/cli.Version=${pkgver}'" \
    -o ./out/aliyun ./main/main.go
}

check() {
  export GOPATH="${srcdir}/go"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  # Horrible but needed for the ./cli/ tests
  touch "${HOME}/.bashrc"

  # for now can't test the `./oss/...` folder, because it needs an env file that is not so easy to have in dev
  go test \
    ./cli/... ./command/... ./config/... ./i18n/... ./main/... ./openapi/... ./resource/...
}

package() {
  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"
  install -Dm 755 out/aliyun "${pkgdir}/usr/bin/aliyun"
  install -Dm 644 README*.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 bin/README.md -t "${pkgdir}/usr/share/doc/${pkgname}/bin"
}

# vim: ts=2 sw=2 et:

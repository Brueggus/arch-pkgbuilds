# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=aliyun-cli
pkgver=3.0.46
_aliyun_openapi_meta_commit=5f5f2b9649e360a1ae3426207a514dbd0bd8a2df
pkgrel=1
pkgdesc='Alibaba Cloud CLI'
arch=('x86_64')
url='https://github.com/aliyun/aliyun-cli'
license=('APACHE')
depends=('glibc' 'jq')
makedepends=('git' 'go' 'go-bindata')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        "git+https://github.com/aliyun/aliyun-openapi-meta#commit=${_aliyun_openapi_meta_commit}")
sha512sums=('55722630d8ec6ae300458a98eba29a3df40acef72c51c427252722294ef273c40e4a58942b07ac9db8410f74f096b712fb86858a9b66ed5cf3f9f777ffc9afd2'
            'SKIP')
b2sums=('42aacc3af9e57d08ab8defd6cd7a8e9582c7b8f59e13648c631ce380c8c35d6a00665e69af3791089b7e8dd92c3f00b4c3ccf091130642ccb864b17daab895b2'
        'SKIP')

prepare() {
  export GOPATH="${srcdir}/go"

  mkdir -p "${GOPATH}/src/github.com/aliyun"
  ln -rTsf "${pkgname}-${pkgver}" "${GOPATH}/src/github.com/aliyun/${pkgname}"
  ln -rTsf aliyun-openapi-meta "${GOPATH}/src/github.com/aliyun/aliyun-openapi-meta"
}

build() {
  export GOPATH="${srcdir}/go"
  export PATH="${GOPATH}/bin:${PATH}"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=vendor"

  go-bindata \
      -o resource/metas.go \
      -pkg resource \
      ../aliyun-openapi-meta/...

  go build \
    -ldflags "-X 'github.com/aliyun/aliyun-cli/cli.Version=${pkgver}'" \
    -o ./out/aliyun ./main/main.go
}

check() {
  export GOPATH="${srcdir}/go"

  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"

  # Horrible but needed for the ./cli/ tests
  touch "${HOME}/.bashrc"

  # for now can't test the `./oss/...` folder, because it needs an env file that is not so easy to have in dev
  go test \
    ./cli/... ./command/... ./config/... ./i18n/... ./main/... ./openapi/... ./resource/...
}

package() {
  cd "${GOPATH}/src/github.com/aliyun/${pkgname}"
  install -Dm 755 out/aliyun "${pkgdir}/usr/bin/aliyun"
  install -Dm 644 README*.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 bin/README.md -t "${pkgdir}/usr/share/doc/${pkgname}/bin"
}

# vim: ts=2 sw=2 et:

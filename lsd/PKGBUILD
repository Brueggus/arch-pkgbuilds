# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>

pkgname=lsd
pkgver=0.11.1
pkgrel=2
pkgdesc='Modern ls with a lot of pretty colors and awesome icons'
url='https://github.com/Peltoche/lsd'
arch=('x86_64')
license=('Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/Peltoche/lsd/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('751098ff9296559b75d2c849194418c368c722a22021015d5adbb6c0a4b56dabff4a7597b821cb57ca928ffb635015f08e20e052f8926016f48788a6dbf20fd2')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>

pkgname=lsd
pkgver=0.14.0
pkgrel=1
pkgdesc='Modern ls with a lot of pretty colors and awesome icons'
url='https://github.com/Peltoche/lsd'
arch=('x86_64')
license=('Apache')
depends=('gcc-libs' 'awesome-terminal-fonts')
makedepends=('rust' 'cargo')
source=(https://github.com/Peltoche/lsd/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ac30347c0a1826c37f5f2629a3bd12a4c1cec42428ea15d0d86d56841eaf6998')
sha512sums=('f1c17b63769f7c390a36e2ff99caf6d1da326fefc041a992986f756614a70db5aae4c87a0a33e416a6096d8ce65f0c0cbed50d168a8d0ca6dc2e97f0065593a0')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-filebytes
pkgname=('python-filebytes' 'python2-filebytes')
_pyname=${pkgbase/python-/}
pkgver=0.9.17
pkgrel=1
pkgdesc='Library to read and edit executable binary files'
url='https://github.com/sashs/filebytes'
arch=('any')
license=('GPL2')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/sashs/filebytes/archive/v${pkgver}.tar.gz)
sha512sums=('93f9c5eb40e15563904f4b4cc9e77ac81274de66f234cded0c6d95201c1f8f008d73eff0d2ecf29410ce36838d6c39987bfcc58cc35e7d1b932404d57f9d6a32')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
  sed -r 's|env python$|env python2|' -i ${_pyname}-${pkgver}-py2/samples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-filebytes() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python2-filebytes() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

# vim: ts=2 sw=2 et:

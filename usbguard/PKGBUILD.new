# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=usbguard
pkgver=0.7.4
pkgrel=4
pkgdesc='Software framework for implementing USB device authorization policies'
url='https://github.com/dkopecek/usbguard'
arch=('x86_64')
license=('GPL2')
depends=('libqb' 'libsodium' 'libcap-ng' 'protobuf' 'polkit' 'qt5-base' 'qt5-svg' 'qt5-tools' 'hicolor-icon-theme' 'dbus-glib')
makedepends=('git' 'libxslt' 'asciidoc' 'bash-completion')
source=(${url}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=(
  '732cc99f9b03632eb558941781c01f869bf96aad7f6976998094b3824d9b7ae2'
  'e6f15ffcfec97681b7da45120104f068cfd8a3d7906749b610cfad622c4d77f0'
  '3a6ddb78a7f5c01a6a83c0a3337dd79a184ecb8a88d3adce1087e8beb4ca9c3f')
validpgpkeys=('430C1928960157CC45FA1BEBAA06120530AE0466')
backup=(
  etc/usbguard/usbguard-daemon.conf
  etc/usbguard/rules.conf)

prepare() {
  cd ${pkgname}-${pkgver}
  rm -f ./usbguard-daemon.conf
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sys=/etc \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --enable-systemd \
    --with-gui-qt=qt5 \
    --with-bundled-catch \
    --with-bundled-pegtl
  make
  touch rules.conf
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make SYSTEMD_UNIT_DIR="/usr/lib/systemd/system" DESTDIR="$pkgdir" install
  mkdir -p "${pkgdir}/etc/usbguard"
  install -p -m 600 ./usbguard-daemon.conf "${pkgdir}/etc/usbguard/usbguard-daemon.conf"
  install -p -m 600 ./rules.conf "${pkgdir}/etc/usbguard/rules.conf"
}

# vim: ts=2 sw=2 et:

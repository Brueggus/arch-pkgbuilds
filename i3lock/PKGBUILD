# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3lock
pkgver=2.11.1
pkgrel=1
pkgdesc='Improved screenlocker based upon XCB and PAM'
url='https://i3wm.org/i3lock/'
arch=('x86_64')
license=('MIT')
groups=('i3')
depends=('xcb-util-image' 'xcb-util-xrm' 'libev' 'cairo' 'libxkbcommon-x11' 'pam')
options=('docs')
backup=('etc/pam.d/i3lock')
source=(https://i3wm.org/i3lock/$pkgname-$pkgver.tar.bz2{,.asc})
sha512sums=('8b779aa0a560884adc1a508e1f07f712bdf043f72c377c24d5e7e186a6b97f34a5bda7aa67de41f5e15a1a541f4a2c2d28bc284057926a5833d70b67437d073c'
            'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

prepare() {
  cd ${pkgname}-${pkgver}
  # Fix ticket FS#31544, sed line taken from gentoo
  sed -i -e 's:login:system-auth:' pam/i3lock
  autoreconf -fiv
}


build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 i3lock.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

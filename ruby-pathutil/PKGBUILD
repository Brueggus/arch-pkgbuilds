# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=pathutil
pkgname=ruby-pathutil
pkgver=0.16.0
pkgrel=1
pkgdesc='Faster pure Ruby implementation of Pathname with extra bits'
url='https://github.com/envygeeks/pathutil'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-forwardable-extended')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/envygeeks/pathutil/archive/v${pkgver}.tar.gz)
sha256sums=('4f6197f29bc0cd688c245eb6d59664aa8782768fa9a7b896b0652d30281df586')
sha512sums=('459c33d858973aeefe00020283ec4a30bff7ad1c55866dd60123850ca2ca097eda9941c35ce485bfa13eec922c9053e465a2feac59a0f07ba20ae9f9051e359f')

prepare() {
  cd ${_gemname}-${pkgver}
  mv Gem.gemspec ${_gemname}.gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

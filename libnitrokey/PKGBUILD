# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ã‰tienne Deparis <etienne@depar.is>

pkgname=libnitrokey
pkgver=3.5
pkgrel=1
pkgdesc='Communicate with Nitrokey stick devices in a clean and easy manner'
url='https://www.nitrokey.com'
arch=('x86_64')
license=('LGPL3')
depends=('hidapi' 'libusb')
makedepends=('cmake' 'qt5-tools' 'catch2')
provides=('libnitrokey.so')
source=(https://github.com/Nitrokey/libnitrokey/releases/download/v${pkgver}/libnitrokey-v${pkgver}.tar.gz{,.sig})
sha512sums=('5e412b47f7df4b3b06bb003ac6c6d16da89dbb06cfc8c5e1e6e43c6cf84aeecb2cc6093b2bfba6fabbed0deaf0df1d17c8f49f64b464b959814d9b754c2279d7'
            'SKIP')
validpgpkeys=('868184069239FF65DE0BCD7DD9BAE35991DE5B22') # Szczepan Zalega <szczepan.zalega@gmail.com>

prepare() {
  cd ${pkgname}-v${pkgver}
  mkdir -p build
  sed '/REQUIRE(std::regex_match(s, pattern));/d' -i unittest/test_offline.cc
}

build() {
  cd ${pkgname}-v${pkgver}/build
  cmake .. \
    -DBUILD_SHARED_LIBS=ON \
    -DCOMPILE_OFFLINE_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check() {
  cd ${pkgname}-v${pkgver}/build
  ctest -VV
}

package() {
  cd ${pkgname}-v${pkgver}
  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

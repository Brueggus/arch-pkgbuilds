# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgbase=python-qrcode
pkgname=(python-qrcode python2-qrcode)
pkgver=6.1
pkgrel=1
pkgdesc='Python library to generate QR codes'
url='https://github.com/lincolnloop/python-qrcode'
arch=('any')
license=('custom')
makedepends=('python-pillow' 'python2-pillow' 'python-six' 'python2-six' 'python-setuptools' 'python2-setuptools')
checkdepends=('python2-mock')
source=(https://github.com/lincolnloop/python-qrcode/archive/v${pkgver}/${pkgbase}-v${pkgver}.tar.gz)
sha512sums=('3af3b67959be8dd30b372145ff7b8aa1e20153c403b17c688d5a7189e3b25da872c6eee916e6d47e4f6545c87c0c0eb9af52e115353a0e8c0f73e4948ddc6829')

prepare() {
  cp -a ${pkgbase}-${pkgver}{,-py2}
  find "${pkgbase}-${pkgver}-py2" -type f -exec sed -e 's#/usr/bin/python#/usr/bin/python2#g' \
                                              -e 's#/usr/bin/env python#/usr/bin/env python2#g' -i {} +
}

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  python setup.py build

  cd "${srcdir}"/${pkgbase}-${pkgver}-py2
  python2 setup.py build
}

check() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  python -m unittest discover qrcode

  cd "${srcdir}"/${pkgbase}-${pkgver}-py2
  python2 -m unittest discover qrcode
}

package_python-qrcode() {
  depends=('python-pillow' 'python-six' 'python-setuptools')

  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-qrcode() {
  depends=('python2-pillow' 'python2-six' 'python2-setuptools')

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}

  rm -r "${pkgdir}"/usr/bin
  rm -r "${pkgdir}"/usr/share/man
}

# vim:ts=2:sw=2:et:

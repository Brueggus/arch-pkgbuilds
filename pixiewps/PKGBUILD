# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mitch Bigelow <ipha00@gmail.com>

pkgname=pixiewps
pkgver=1.2.2.19.gf723d50
pkgrel=1
_gitcommit=f723d5017f304b727981560396044d7d11ec1080
pkgdesc='Offline bruteforce of the WPS pin exploiting the low or non-existing entropy of some APs'
url='https://github.com/wiire/pixiewps'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('glibc')
makedepends=('git')
optdepends=('reaver: authentication session key support'
            'bully: authentication session key support')
source=(${pkgname}::"git+https://github.com/wiire/pixiewps#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --always|sed 's/^v//;s/-/./g'
}

build() {
  cd ${pkgname}
  make -C src
}

package() {
  cd ${pkgname}
  make -C src DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pixiewps.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

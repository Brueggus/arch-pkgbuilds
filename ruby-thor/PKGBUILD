# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=thor
pkgname=ruby-thor
pkgver=0.20.3
pkgrel=1
pkgdesc='Toolkit for building powerful command-line interfaces'
url='https://github.com/erikhuda/thor'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/erikhuda/thor/archive/v${pkgver}.tar.gz)
sha256sums=('bd6af5304dc4f13095474394337e46c3bebef6f137521037e227a204452d8b70')
sha512sums=('368cedd9584fcdecf8f5feb9b727aa283b37747e8200f8629aaa762f8e3a59dafe6ee1e7c60ef2b43b2a16b319842b10d0f3face12ba8dded4b6211640db7f44')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

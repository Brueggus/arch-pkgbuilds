# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=99.9.9
pkgrel=1
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-trollop' 'ruby-manpages')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/busyloop/lolcat/archive/v${pkgver}.tar.gz)
sha256sums=('10ac193276abb46e0c8b7af02048f9e622f09b3f37d640df7d1e870e6cd349b2')
sha512sums=('0e32bd3ff892aae56ea1f9b58ff2c4907004f7c6cb3af1e28f468e6726a5d9ccc28464df76300c5817facaa9f30d1e4f5e0ed2934ff58e396ced271ab380d657')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem" \
    "${pkgdir}/usr/share/man/man6/lolcat.6"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgdir}/${_gemdir}/gems/lolcat-${pkgver}/man/lolcat.6" -t "${pkgdir}/usr/share/man/man6"
}

# vim: ts=2 sw=2 et:

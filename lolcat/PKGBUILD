# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=99.9.21
pkgrel=1
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-optimist' 'ruby-manpages')
options=('!emptydirs')
source=(https://github.com/busyloop/lolcat/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e6c31afb72e2290b7a5a1c3b2f5fccf0b226fb12f8283e5c5720750008d2efe8')
sha512sums=('aa3a1ba9907023d9e004426617bff6f995d29850bbf70dfd3abed6735e31f6fade0218893dc39bdde8c6d0b3b9ea321a440b388adac8adafd13a13577b17572b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem

  install -Dm 644 "${pkgdir}/${_gemdir}/gems/lolcat-${pkgver}/man/lolcat.6" \
    -t "${pkgdir}/usr/share/man/man6"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

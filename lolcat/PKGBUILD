# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=99.9.99
pkgrel=1
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-optimist' 'ruby-manpages')
options=('!emptydirs')
source=(https://github.com/busyloop/lolcat/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1a2631733963d28ee89ddb09c7b842db398e10ff923e6c8dba1868c3d7782d68')
sha512sums=('a2ebbc2f9f153009c0731240d27785a7fcf23f22244c32810da934017d6b4d051c86ab052209397897416d3155d40602dce1ba5be7387e38bb0b1182de65de97')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem

  install -Dm 644 "${pkgdir}/${_gemdir}/gems/lolcat-${pkgver}/man/lolcat.6" \
    -t "${pkgdir}/usr/share/man/man6"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

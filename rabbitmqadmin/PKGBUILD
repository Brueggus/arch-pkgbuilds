# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.7.8
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}.tar.gz)
sha256sums=('bbe6994464d229bb847904052fbd04cb9ed8488b843633b1054c2fa1e19d2b84')
sha512sums=('64dd2d8028a4c6974d526b9b83922d46da8ff6dbe62f149dd7b927fea984f5e77a748ba7d925f25a39e7e7f122f44764a5ba8d81434256c374421aada9b5cc7c')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

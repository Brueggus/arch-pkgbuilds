# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.6.11
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/rabbitmq_v${pkgver//./_}.tar.gz)
sha256sums=('5cc76d3926fef1bcc67535af78c5e5fc244235a830459210e8f91a479812df75')
sha512sums=('c6087f6f67ba51ef66d9df17f5d3aab8f1cd1140739207c53f1af7fe86603759bc08cf95308e4fb3487e265f168a57e6a6fb61995e98c2f6aae0c0c1dc8b0e0b')

prepare() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

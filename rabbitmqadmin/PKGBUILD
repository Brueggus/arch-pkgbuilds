# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.6.9
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/rabbitmq_v${pkgver//./_}.tar.gz)
sha512sums=('abf460dc711ebb0d02a810d345bd9d529d76ed34c9d6dff97eec599be8a39a90fcae0acadd6d4ccdb67bc45005cfc6305cb61682ea734ebea340eb02c1c5d83a')

prepare() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-rabbitmq_v${pkgver//./_}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

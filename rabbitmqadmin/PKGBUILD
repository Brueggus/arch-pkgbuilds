# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.7.5
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}.tar.gz)
sha256sums=('328cd08634e01397fe9c0d782cf09957ce9221c2ddcadc587f80f3e14438d3a4')
sha512sums=('3404d75f1db92be99e0b610923777d875a1e6d81f79111680b8f557ee683cd414231c37e24e53dafaefaa9a4ed0a636f89a9a5afd53b03efa0b456aed630df99')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: openfbt
# Contributor: Werecat
# Contributor: Xyne

pkgname=cjdns
pkgver=20.4
pkgrel=1
pkgdesc='Routing engine designed for security, scalability, speed and ease of use'
url='https://github.com/cjdelisle/cjdns'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'sh')
optdepends=('nodejs: optional utilities support')
makedepends=('nodejs' 'python2')
install=cjdns.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/cjdelisle/${pkgname}/archive/cjdns-v${pkgver}.tar.gz
        1b7f09b9492fd9ccbacc4a16f6cc9a623653aba3.patch)
sha512sums=('5a6bd36b2edd07fa883efa4b14dd8a7ba0189bf43404e27ab6ae3b0c2f1e63ce738c52b34416f51f93c9752f6654ea95eac2c71897d2a1e19f4a6d1ca3bad46d'
            'c7bb9d1550661221f5fb08f31384a5d06b9c2c8531c1f5bbc14624a99adc21f19873c01a553b20198790e8bf72ea763de4e486e13b02df5356f397549692ba9c')

prepare() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  patch --forward --strip=1 --input="${srcdir}/1b7f09b9492fd9ccbacc4a16f6cc9a623653aba3.patch"
}

build() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  # Pretend to be cross compiling so -march=native is not set.
  CROSS="true" CJDNS_RELEASE_VERSION="${pkgver}" \
    node ./node_build/make.js
}

package() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  install -Dm 755 cjdroute -t "${pkgdir}/usr/bin"
  install -Dm 644 contrib/systemd/{cjdns,cjdns-resume}.service \
    -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 doc/man/cjdroute.conf.5 -t "${pkgdir}/usr/share/man/man5"
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md \
    doc/admin-api.md \
    doc/configure.md \
    doc/djc_layer_model.md \
    doc/nat-gateway.md \
    doc/network-services.md \
    doc/non-root-user.md \
    doc/security_specification.md \
    doc/shorewall_and_vpn_gateway_howto.md \
    doc/tunnel.md
  cp -a tools "${pkgdir}/usr/lib/${pkgname}"
  cp -a node_modules "${pkgdir}/usr/lib/${pkgname}/node_modules"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: openfbt
# Contributor: Werecat
# Contributor: Xyne

pkgname=cjdns
pkgver=20
pkgrel=1
pkgdesc='Routing engine designed for security, scalability, speed and ease of use'
url='https://github.com/cjdelisle/cjdns'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('glibc' 'sh')
optdepends=('nodejs: optional utilities support')
makedepends=('nodejs' 'python2')
install=cjdns.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/cjdelisle/${pkgname}/archive/cjdns-v${pkgver}.tar.gz)
sha256sums=('08a1029d47f3b666eec9d901b2e2fe6e8f971348c10465427db95e4153ecd8b7')
sha512sums=('7da6537ed417d33ebd9ffa27a9e08758c48ea99930a85497853f0ee43cc87501434ab76bee0e69dc935b3338959d86bcb4bef527f1b93887b0a3662b4ab55879')

build() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  CJDNS_RELEASE_VERSION="${pkgver}" \
    node ./node_build/make.js
}

package() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  install -Dm 755 cjdroute "${pkgdir}/usr/bin/cjdroute"
  install -Dm 644 contrib/systemd/${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 644 contrib/systemd/${pkgname}-resume.service "${pkgdir}/usr/lib/systemd/system/${pkgname}-resume.service"
  install -Dm 644 doc/man/cjdroute.conf.5 "${pkgdir}/usr/share/man/man5/cjdroute.conf.5"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    doc/admin-api.md \
    doc/configure.md \
    doc/djc_layer_model.md \
    doc/nat-gateway.md \
    doc/network-services.md \
    doc/non-root-user.md \
    doc/security_specification.md \
    doc/shorewall_and_vpn_gateway_howto.md \
    doc/tunnel.md
  cp -a tools "${pkgdir}/usr/lib/${pkgname}"
}

# vim: ts=2 sw=2 et:

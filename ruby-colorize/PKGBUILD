# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Phillips <dbphillipsnz gmail>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

_gemname=colorize
pkgname=ruby-colorize
pkgver=0.8.1
pkgrel=3
pkgdesc='Ruby string class extension to set color, background and text effect using ANSI escape sequences'
url='https://github.com/fazibear/colorize'
arch=('any')
license=('GPL2')
depends=('ruby')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('0819dfaa94150c85cbe633c77006f2f4bdd6c4f9bf9dfa954b8c4038b8a680c99a2b4b160da05bf426402d287a0db4c4eb1cc66a949405188b632f39da381c72')

package() {
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/"{README.md,CHANGELOG} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

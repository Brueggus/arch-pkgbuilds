# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sqlite3
pkgname=ruby-${_gemname}
pkgver=1.3.13
pkgrel=3
pkgdesc='Ruby bindings for the SQLite3 embedded database'
url='http://github.com/luislavena/sqlite3-ruby'
arch=('x86_64')
license=('BSD')
depends=('ruby' 'sqlite')
options=('!emptydirs')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-hoe' 'ruby-mini_portile' 'ruby-minitest')
source=(https://github.com/sparklemotion/sqlite3-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('4ba98fec37d6828aaa68fc9779c20f124121f3965254b1bf1bc2fa3b1e7caa08')
sha512sums=('7099ee6420af22d064cecd8c35e2275f958162bf088d03b00504cc6efd2cf5a720302b7c803a55e9e162432cf5d767ea3f6528b254e474ac0f7ff8a932cc8787')

prepare() {
  cd ${_gemname}-ruby-${pkgver}
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd ${_gemname}-ruby-${pkgver}
  rake compile
  rake gem
}

check() {
  cd ${_gemname}-ruby-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-ruby-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc CHANGELOG.rdoc API_CHANGES.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:

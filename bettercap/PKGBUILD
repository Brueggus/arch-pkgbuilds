# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.4
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libnetfilter_queue' 'iproute2' 'glibc' 'libnfnetlink')
makedepends=('go-pie' 'git' 'glide')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/bettercap/bettercap/archive/v${pkgver}.tar.gz)
sha512sums=('1947d3ae66d94cd812087e9100953e6e208101b7f691267f1522794c59c6a65837bd4184a7856847cc0a79a0177cfb06b844526d7ec8bc06ec012ffd439a8d5a')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/bettercap
  ln -rTsf ${pkgname}-${pkgver} src/github.com/bettercap/bettercap
}

build() {
  cd src/github.com/bettercap/bettercap
  export GOPATH="${srcdir}"
  glide install
  go build -o bettercap
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.10
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libnetfilter_queue' 'iproute2' 'glibc' 'libnfnetlink')
makedepends=('go-pie' 'git' 'dep')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/bettercap/bettercap/archive/v${pkgver}.tar.gz)
sha512sums=('ff431277e6478509ee9424affc924044835942fd3ba7cfb85967b48d91b69829b2a0db5008898794fd3f20e1968ae896048d75bdc70d52cc46dde1df6d3997d2')

prepare(){
  export GOPATH="${srcdir}/go"
  mkdir -p go/src/github.com/bettercap
  ln -rTsf ${pkgname}-${pkgver} go/src/github.com/bettercap/bettercap
}

build() {
  cd go/src/github.com/bettercap/bettercap
  export GOPATH="${srcdir}/go"
  dep ensure
  go build -o bettercap
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.1
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap')
makedepends=('go-pie' 'git')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/bettercap/bettercap/archive/v${pkgver}.tar.gz)
sha512sums=('a83be8b46f2175ee5bab896de031af6f2baad7b89eb536b5155c9dfdc297f990b686f93be78ab24d3cfd85acff6e5f0c5a49ca60c505faf37cc8fbffdd44a7da')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/bettercap
  ln -rTsf ${pkgname}-${pkgver} src/github.com/bettercap/bettercap
}

build() {
  cd src/github.com/bettercap/bettercap
  export GOPATH="${srcdir}"
  go get -v
  go build -o bettercap
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ../bin/bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

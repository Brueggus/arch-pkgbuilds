# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.22
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libnetfilter_queue' 'iproute2' 'glibc' 'libnfnetlink')
makedepends=('go-pie' 'git' 'dep')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/bettercap/bettercap/archive/v${pkgver}.tar.gz)
sha512sums=('86e520a8121afa4175740bbab859f51ea420c14db3ae9a3e690cbec7c5a0b715a4e42a6d3907a323c378e435e4f7a4fc259801d1c4fdae60cb2333bbd4ba1357')

prepare(){
  export GOPATH="${srcdir}/go"
  mkdir -p go/src/github.com/bettercap
  ln -rTsf ${pkgname}-${pkgver} go/src/github.com/bettercap/bettercap
  sed -E "s|(go build)|\\1 -ldflags '-linkmode external -extldflags \"${LDFLAGS}\"'|" -i ${pkgname}-${pkgver}/Makefile
}

build() {
  cd go/src/github.com/bettercap/bettercap
  export GOPATH="${srcdir}/go"
  dep ensure
  make build
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

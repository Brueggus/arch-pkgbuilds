# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>

pkgname=yrd
pkgver=0.5.0
pkgrel=1
pkgdesc='cjdns swiss army knife'
url='https://github.com/kpcyrd/yrd'
license=('GPL3')
arch=('any')
depends=('python-setuptools' 'python-argh' 'python-requests' 'cjdns')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/yrd/archive/v${pkgver}.tar.gz
        yrd.tmpfiles.conf)
sha512sums=('e227759fe18ae325324a640a3ca8fb9699981f58aad8eecf997d0f135a577b886e0018d7e7481caa4028a80d4bc3e22e78dfc7de79f2e94e9dadab5ee6f34e9f'
            '191fdf5687886357c01b72b2b9037045ca24d0ee888dc2bf6b2748a769eeb9a481eb01a9807f8f32b783d0970229f18d1264e937c2dd44b282ac764c49450cca')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build

  install -Dm 644 doc/yrd.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 "${srcdir}/${pkgname}.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/yrd.conf"
  install -Dm 644 init/yrd.service -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:

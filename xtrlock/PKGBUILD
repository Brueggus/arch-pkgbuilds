# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.12
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('x86_64')
license=('GPL3')
depends=('libx11')
source=(http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz)
sha256sums=('13b634dc6c23a35386e683163d2b8be76de2229e1cd7fb82517cb8e388e278ba')
sha512sums=('8392a8c254726e99d2324415bfdac6e577f725942d4e7fd39c915637f87814f1737aefa903a1502632272cabbb10182070dc15ec5d3e5268c9c1b53218d00f36')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD" \
    LDLIBS="-lX11 -lcrypt" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:

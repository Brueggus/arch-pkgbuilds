# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=etherape
pkgver=0.9.17
pkgrel=1
pkgdesc='Graphical network monitor for various OSI layers and protocols'
url='http://etherape.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'glib2' 'libgnomecanvas' 'libpcap' 'popt' 'libglade')
makedepends=('rarian' 'gnome-doc-utils' 'yelp-tools')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('65f039cd98826fee9a5988235dbdface00e2f17e5bd83eb28339d5bdb9d2b5f8ffe84e071c36418617d68add32e39866eb393f23b008b35fe36662453a02c57a')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=8.5
pkgrel=1
pkgdesc='Very fast network logon cracker which support many different services'
url='https://www.thc.org/thc-hydra/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'libmariadbclient' 'libidn' 'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2' 'pkg-config')
source=(${pkgname}-${pkgver}.tar.gz::https://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz)
sha256sums=('557ab284f877aa6c8a26f7491c0373a84836593e5e0ca141b327035e7937dd6d')
sha512sums=('0b462074a5556a53eb3135da7d4c3cafac5448645fa6020f9a86caea04f0e35ebead32e63db80ba8e680a3b14ff7f6a125553653e776c8e54e9caf923c0da0ec')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u -k')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --fhs
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

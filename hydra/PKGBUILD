# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=8.4
pkgrel=2
pkgdesc='Very fast network logon cracker which support many different services'
url='https://www.thc.org/thc-hydra/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'libmariadbclient' 'libidn' 'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2' 'pkg-config')
source=(${pkgname}-${pkgver}.tar.gz::https://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz)
sha256sums=('482846580888f54e000bb125e263a65c32fa28ba86c9d4012371cf55c3bdc140')
sha512sums=('62c53f1725eb5b965d16d293cd90ab5e74779eba8fe2e2ab9094f51851fa24acfd74574f509a7914af8254e43197d803cca6b41eace3557d9599d79d695f6a48')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u -k')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --fhs
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=3.4.4
pkgrel=1
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='http://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=('ruby-bundler' 'libxslt' 'libyaml' 'curl' 'libxml2')
options=('!strip' '!emptydirs')
install=wpscan.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wpscanteam/wpscan/archive/v${pkgver}.tar.gz)
sha256sums=('ffdaf1166f7d459c0939506aaf95beb85f8f0969175894ef931abb029570fd89')
sha512sums=('4186c4f864a3296cbf4fc13cd01b1438ce17437a5a082850af9a25981d6acfb27c20029bd6189aa78d64f834dedc5cf580e179d42fcaab139f60f9c07b221bdd')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/bundler/ s|~>|>=|g' $pkgname.gemspec
  bundle config build.nokogiri --use-system-libraries
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)" --path vendor/bundle --without development test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/wpscan/bin/wpscan "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  find "${pkgdir}" \( -name gem_make.out -or -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:

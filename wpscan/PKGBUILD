# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=3.8.14
pkgrel=1
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='https://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=('ruby-bundler' 'libxslt' 'libyaml' 'curl' 'libxml2')
optdepends=('metasploit')
options=('!strip' '!emptydirs')
install=wpscan.install
source=(https://github.com/wpscanteam/wpscan/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('57f9a875b7a0ea96608d2824600ba941632cda99512d3833bf8f004c6e42e1ee04304ea2c70133d531604a7c73d2d0c6f626422ae601dbb0664ef62bc8c662cf')
b2sums=('82c69ce4e8a56392dabf0da37006e2961474fa352763e87cb92e31e31a70d757c6b33aa1b8836d4640822695117a662ebfcc15eab677b29b734d218e8fd9feed')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/bundler/ s|~>|>=|g' $pkgname.gemspec
  bundle config build.nokogiri --use-system-libraries
  bundle config set --local path vendor/bundle
  bundle config set --local without development test
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)"
  # reproducible builds: don't leak jobs count
  sed '/BUNDLE_JOBS/d' -i .bundle/config
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/wpscan/bin/wpscan "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}"/opt/wpscan/vendor/bundle/ruby/*
  rm -rf cache gems/*/{ext,lib/*/*.so} \
    extensions/*/*/*/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:

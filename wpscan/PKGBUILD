# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=3.8.10
pkgrel=1
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='https://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=('ruby-bundler' 'libxslt' 'libyaml' 'curl' 'libxml2')
optdepends=('metasploit')
options=('!strip' '!emptydirs')
install=wpscan.install
source=(https://github.com/wpscanteam/wpscan/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b33fbeb899785c0b6e87b1a5956a50666b9eef5ab543427eada88b24074e3e79f4a608b3981b8cc904c80d97046ffeeb558b5c6de5ff4e4d10e582ca51489698')
b2sums=('b7bb829c6f2c4fb82a875dd538a4b978a151492f5aa5d256ce3b217ae4a1ffba4dab8ac0e8dd23a53811128bf1b2a9799a949c8966460f9b2230405390791022')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/bundler/ s|~>|>=|g' $pkgname.gemspec
  bundle config build.nokogiri --use-system-libraries
  bundle config set --local path vendor/bundle
  bundle config set --local without development test
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)"
  # reproducible builds: don't leak jobs count
  sed '/BUNDLE_JOBS/d' -i .bundle/config
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/wpscan/bin/wpscan "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}"/opt/wpscan/vendor/bundle/ruby/*
  rm -rf cache gems/*/{ext,lib/*/*.so} \
    extensions/*/*/*/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:

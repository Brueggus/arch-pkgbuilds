# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=1.1.0
pkgrel=1
pkgdesc='Full featured and easy to use XMPP (Jabber) client'
url='https://www.gajim.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-dbus' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-pyasn1' 'python-pyopenssl' 'python-precis_i18n' 'python-cssutils'
         'python-distro' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('alsa-utils: play notification sounds'
            'python-dbus: for gajim-remote and zeroconf support'
            'avahi: serverless chatting with autodetected clients in a local network'
            'farstream: start audio and video chat'
            'gst-plugins-good: for video/voice support'
            'gst-plugins-bad: for video/voice support'
            'gst-plugins-ugly: for video/voice support'
            'gst-libav: for video/voice support'
            'gst-python: for video/voice support'
            'gspell: for spell checking support'
            'notification-daemon: for desktop notifications'
            'geoclue2: share current location'
            'gnome-keyring: store passwords encrypted in GNOME Keyring'
            'kded: store passwords encrypted in KSecretService'
            'gspell: spellchecking of composed messages'
            'gupnp-igd: request your router to forward port for file transfer'
            'libxss: measure idle time, in order to set auto status'
            'python-crypto: encrypting chat messages'
            'python-docutils: generate XHTML output from RST code'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-pillow: support of WebP avatars')
source=(https://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2)
sha512sums=('47b18de111643eba382255e7c2d195b41f30bf15957cd27939ebd44eade7af1b9ebe50c30ca10f33ca36e879d309fc414369fd8ba2c725c336f38a3a0f88ba7c')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:

# $Id: PKGBUILD 248483 2017-08-04 19:01:00Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farawayer <farwayer@gmail.com>

pkgname=ruby-public_suffix
pkgver=3.0.0
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=('any')
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/public_suffix-$pkgver.gem")
noextract=("public_suffix-$pkgver.gem")
sha512sums=('9190c85fa48f2d69af3131cacdfeb4e4ebe2ff82f833d286ff3e87298cd3189d7c5b6facdcde502bea73beae8a950c1d6ef851ca0848824a59ac53f222db4806')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" public_suffix-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/public_suffix-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/public_suffix-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

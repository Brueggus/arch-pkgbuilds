# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: farawayer <farwayer@gmail.com>

_gemname=public_suffix
pkgname=ruby-public_suffix
pkgver=4.0.5
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=('any')
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/weppos/publicsuffix-ruby/archive/v$pkgver.tar.gz)
sha512sums=('ba2a781a9f83e9923158a4b539564e33cd9afeb5890f18d696a7de77fff76d7caeadb72954711dce3a46830ac16158796f43c3f3cc90a08d00320dae931de14f')

prepare() {
  cd publicsuffix-ruby-$pkgver
  sed -r 's|~>|>=|g' -i $_gemname.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -- {test,spec,features}/*|find -type f test spec features|' -i $_gemname.gemspec
  sed 's|git ls-files|find -type f|' -i $_gemname.gemspec
}

build() {
  cd publicsuffix-ruby-$pkgver
  gem build $_gemname.gemspec
}

package() {
  cd publicsuffix-ruby-$pkgver
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" $_gemname-$pkgver.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/$_gemname-$pkgver.gem"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jacques Fontaine <waitnsea@free.fr>
# Contributor: Ryan Young <ryan31415926535@gmail.com>

pkgname=linssid
pkgver=3.1
pkgrel=1
pkgdesc='Graphical wireless scanner for Linux'
url='https://sourceforge.net/projects/linssid/'
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'qwt' 'libxkbcommon-x11' 'iw' 'wireless_tools' 'xdg-utils' 'boost-libs'
         'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('boost')
optdepends=('sudo: privilege escalation to use wireless tools')
source=(${pkgname}-${pkgver}.tar.gz::https://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('b6a1eb6e1e65b1f85184c95710a9ed2b3aada7867c25b5aab4255717ccc26c4f')
sha512sums=('1864e2b108e53a5d933509643f26c14b31e4918a684c52cf32f7d0346d469aa691268ba57a8b4179c16cdcd09bf9e6274d06a34d2dd5cfd0e0d1cbc6245d7d04')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|libqwt-qt5.so|libqwt.so|g' -i linssid-app/linssid-app.pro
  sed -r 's|/usr/sbin|/usr/bin|g' -i linssid-app/linssid-app.pro
}

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

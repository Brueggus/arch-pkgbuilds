# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jacques Fontaine <waitnsea@free.fr>
# Contributor: Ryan Young <ryan31415926535@gmail.com>

pkgname=linssid
pkgver=3.3
pkgrel=1
pkgdesc='Graphical wireless scanner for Linux'
url='https://sourceforge.net/projects/linssid/'
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'qwt' 'libxkbcommon-x11' 'iw' 'wireless_tools' 'xdg-utils' 'boost-libs'
         'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('boost')
optdepends=('sudo: privilege escalation to use wireless tools')
source=(${pkgname}-${pkgver}.tar.gz::https://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('b7023129cb8ea2d37459d911fa9e42074f7a11394b68df52658f4a90cce74058')
sha512sums=('6e5c2ed4f8404d10114fcd22401bec900c71b5888224490de36745cfbcba1e51ea15bbac50b7c6d045813855950ebf3413872266afe1d90193c6873c7021e845')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|libqwt-qt5.so|libqwt.so|g' -i linssid-app/linssid-app.pro
  sed -r 's|/usr/sbin|/usr/bin|g' -i linssid-app/linssid-app.pro
}

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

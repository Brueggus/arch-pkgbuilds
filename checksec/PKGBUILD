# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=2.1.0
pkgrel=2
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=('any')
license=('BSD')
depends=('bash' 'binutils' 'coreutils' 'findutils' 'procps-ng' 'awk' 'openssl'
         'file' 'grep')
source=(https://github.com/slimm609/checksec.sh/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        checksec-2.1.0-fix-zsh-comp.patch::https://github.com/slimm609/checksec.sh/commit/bd30c445e94043a190906c389ac2593eff7168d2.patch
        checksec-2.1.0-fix-proc-libs.patch::https://github.com/slimm609/checksec.sh/commit/a94f723df76ee431af944b6273868cf4c758eb6d.patch
        checksec-2.1.0-fortify-file-fix.patch::https://github.com/slimm609/checksec.sh/commit/143044dac548268ba914c8e73ec5ba845a52772a.patch)
sha256sums=('e50a92a2bb22d40d1aa297cd011209ab5ad0b620236dfb9a7afe4c29744260a9'
            '5f14895401e8be2f080deb8ffeb23abe9c3fd4b679fe5225b70ed4d8bf640115'
            '3b6ea3749735ba970dc2034807dd76b159332a385c6169d62ac8ef27b4a0da59'
            '30658370ffa6fb579ae11e89f500c6f6f87459b52671985404eaef5abffbcc56')
sha512sums=('66090777de15c492090da3494127037ffe20a169b426b054b3a8f642e9c980b2a99d5c8f8411715d74b1faed41023fb17770d3f07eec5b9efda0a43b64c07a74'
            'd5fd2a1ace2eb0002e136118ee1bd90006e2c90519435af019c7b7d0d35fbccea0dfe9e99ba6884d43c8bc122d4d9ff2593298b3b60b2896ca7f5a4a96edc5ef'
            '853bed827d271f75d869e1c7655081c0037f1772aa9952df406ee8a8bda144d287a7d43ad7589d7ce404bbb64f8105666d15de857f12c937b73195535d0b28fa'
            '0b5182b149a631cbc2d1729df03f9e75b02525841892035a7acf4c5912473311f4558d7cf7b0b433af01451b74c28e6182afc4ad2097e859d66bbb65520a1500')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
  patch -Np1 < "${srcdir}/checksec-2.1.0-fix-zsh-comp.patch"
  patch -Np1 < "${srcdir}/checksec-2.1.0-fix-proc-libs.patch"
  patch -Np1 < "${srcdir}/checksec-2.1.0-fortify-file-fix.patch"
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

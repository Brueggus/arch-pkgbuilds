# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=1.9.0
pkgrel=1
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=('any')
license=('BSD')
depends=('bash' 'binutils' 'procps-ng')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/slimm609/checksec.sh/archive/${pkgver}.tar.gz)
sha512sums=('8bf68dd0b86a6eceb6f5eb016a15ed1bf7d086777c3ec1c62848ec23e62dceed4579404d76824f17b8e1305e679ba6204f0dc4c224f7a11067651f33c2d96515')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

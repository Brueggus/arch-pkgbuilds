# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=1.11.1
pkgrel=1
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=('any')
license=('BSD')
depends=('bash' 'binutils' 'procps-ng')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/slimm609/checksec.sh/archive/${pkgver}.tar.gz)
sha512sums=('dd742d6c9bc567b10f8d50f5edd2e1f3439b469ced46da05f53e14c0fe5c90310114564e674afc8c7f55170e07beb6359263e1fd153e4b29f3edb4e88cfbf8c0')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

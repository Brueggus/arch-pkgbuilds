# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async-dns
pkgname=ruby-${_gemname}
pkgver=1.1.0
pkgrel=1
pkgdesc='Asynchronous DNS resolver and server'
url='https://github.com/socketry/async-dns'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-async-io')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/socketry/async-dns/archive/v${pkgver}.tar.gz)
sha256sums=('f177a36515b5538b65d7f535f6ddcdf1d9292c2bb216a024fda0826e0f8415d6')
sha512sums=('0cd9b105d55ab5a420abecff23209f3d25a7d3a04054e0ea9cc57d23fc4be900e0d62db27e8c73decb5eed195cf84925cba70baf1240ac8af40ebfe5c74596a8')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/README.md "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:

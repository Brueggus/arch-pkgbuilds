# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async-dns
pkgname=ruby-${_gemname}
pkgver=1.1.1
pkgrel=1
pkgdesc='Asynchronous DNS resolver and server'
url='https://github.com/socketry/async-dns'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-async-io')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/socketry/async-dns/archive/v${pkgver}.tar.gz)
sha256sums=('53626a92908c7f26a2f1f236be5e02d792262cb4cd8d76363fcdf2b6760f5d1f')
sha512sums=('9b36667d8ac7167e73d3e29513a38578067caa8b76df2fd70222c59606bac54c4ef91376bbf6941f2294cc007693f2710f77aad61d92ba7ce6cf6141c48dacee')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/README.md "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:

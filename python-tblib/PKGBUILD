# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tblib
pkgname=('python-tblib' 'python2-tblib')
_pyname=tblib
pkgver=1.5.0
pkgrel=1
pkgdesc='Python traceback fiddling library'
url='https://github.com/ionelmc/python-tblib'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(https://github.com/ionelmc/python-tblib/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('583527ec61e2abb949b702d46dd7059354975c18a2ecc45130e4938905df258c')
sha512sums=('33b6160401d3593e3b836cd9468ddf7246c84f77eb01118788ad4dc3f3af32c1796d09fa9347c956da1fcc4c48245b794247a28c900072f35564eb8abfdc3275')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

check() {
  (cd ${pkgbase}-${pkgver}
    python setup.py test
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-tblib() {
  depends=('python-six')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-tblib() {
  depends=('python2-six')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

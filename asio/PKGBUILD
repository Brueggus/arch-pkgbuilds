# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.12.1
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=('x86_64')
license=('custom:boost')
makedepends=('boost')
source=("https://sourceforge.net/projects/asio/files/asio/${pkgver}%20%28Stable%29/asio-${pkgver}.tar.bz2")
sha256sums=('a9091b4de847539fa5b2259bf76a5355339c7eaaa5e33d7d4ae74d614c21965a')

prepare() {
  cd ${pkgname}-${pkgver}/

  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}/

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}/

  make check
}

package() {
  cd ${pkgname}-${pkgver}/

  make DESTDIR=${pkgdir} install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

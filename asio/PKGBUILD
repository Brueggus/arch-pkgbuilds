# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.12.0
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=('x86_64')
license=('custom:boost')
makedepends=('boost')
source=(https://github.com/chriskohlhoff/asio/archive/asio-${pkgver//./-}.tar.gz)
sha256sums=('fa8c3a16dc2163f5b3451f2a14ce95277c971f46700497d4e94af6059c00dc06')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver//./-}/asio
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver//./-}/asio
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgname}-${pkgver//./-}/asio
  make check
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver//./-}/asio
  make DESTDIR=${pkgdir} install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

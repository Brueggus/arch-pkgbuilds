# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=(ettercap ettercap-gtk)
pkgver=0.8.3
pkgrel=2
pkgdesc='Network sniffer/interceptor/logger for ethernet LANs'
url='https://ettercap.github.com/ettercap/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'openssl' 'zlib' 'libpcap' 'pcre' 'libltdl' 'libnet' 'ethtool' 'curl' 'geoip'
         'ncurses' 'libncursesw.so' 'libformw.so' 'libpanelw.so' 'libmenuw.so' 'libcurl.so')
makedepends=('ghostscript' 'cmake' 'gtk3' 'harfbuzz' 'check')
options=('!emptydirs')
backup=(etc/ettercap/etter.{conf,dns,mdns,nbns})
source=(https://github.com/Ettercap/ettercap/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz
        cmake-harfbuzz.patch)
sha512sums=('1929c986d3a17ebc693ffe8531e01c66379c0ee6ea71305ea49b6a9eece84b6da1923135311db458bdb6035feb593e525786e6cf4c465ced5a7683384d4a4ae7'
            '248dc70641370f8161a796d090d52f2881ede5dbc834f1aa183583cd3bc64a60f28ad197556289b33f217766eb12de55ab1a96209ff70beb4d67c352debde526')
b2sums=('f49098d61f60877d3f979d7861f36dad6ec3fbfca7ed89d8f9826867145ea36daec65a1076c893f81391218688448515ef020a9cdf9a16ffddc830bacec8eb1c'
        '59e9982abe88684dac46045ab91330a908421386f30ca4df6cc37d2d73413018b37185325e759c3eb88d98e6860a9d3574ffd257c9c12a2b69b57320a0aa10d5')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -Np1 < "${srcdir}/cmake-harfbuzz.patch"
}

build() {
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXLAGS+=" ${CPPFLAGS}"

  cd ${pkgbase}-${pkgver}
  cmake -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D INSTALL_PREFIX=/usr \
    -D ENABLE_GTK=OFF \
    -D ENABLE_TESTS=ON \
    -D ENABLE_IPV6=ON
  make -C build VERBOSE=1

  cmake -B build-gtk \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D INSTALL_PREFIX=/usr \
    -D ENABLE_GTK=ON \
    -D ENABLE_TESTS=ON \
    -D ENABLE_IPV6=ON
  make -C build-gtk VERBOSE=1
}

check() {
  cd ${pkgbase}-${pkgver}
  make -C build test
  make -C build-gtk test
}

package_ettercap() {
  pkgdesc+=" - console"
  provides=('libettercap.so')

  cd ${pkgbase}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 CHANGELOG README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_ettercap-gtk() {
  pkgdesc+=" - GTK frontend"
  depends+=('gtk3' 'harfbuzz')
  optdepends=('polkit: run ettercap directly from menu')
  provides=('ettercap' 'libettercap.so')
  conflicts=('ettercap')

  cd ${pkgbase}-${pkgver}
  make -C build-gtk DESTDIR="${pkgdir}" install
  install -Dm 644 CHANGELOG README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

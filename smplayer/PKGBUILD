# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>

pkgname=smplayer
pkgver=17.7.0
pkgrel=1
pkgdesc='Media player with built-in codecs that can play virtually all video and audio formats'
url='http://smplayer.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt5-script' 'mpv' 'hicolor-icon-theme')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'youtube-dl: youtube videos and streaming'
            'mplayer: alternative multimedia engine')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('57f064bcbb02e271552d00cd6cbc886ca4c997474124cd2bfa0047f2f8f13773fd7b474e86f48bc7e1561c151a69714734266726666a36b596149d9b66cd0d84')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE
}

package() {
  cd ${pkgname}-${pkgver}
  make DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:

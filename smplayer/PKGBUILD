# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>

pkgname=smplayer
pkgver=19.10.2
pkgrel=1
pkgdesc='Media player with built-in codecs that can play virtually all video and audio formats'
url='https://www.smplayer.info/'
arch=('x86_64')
license=('GPL')
depends=('qt5-script' 'mpv' 'hicolor-icon-theme' 'libx11' 'zlib' 'gcc-libs')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'youtube-dl: youtube videos and streaming'
            'mplayer: alternative multimedia engine')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('37ed9f9a051077b9bc1a68f06fd4ad37fdac2802f62c28fd177e91d97d094c44')
sha512sums=('46327a1f988cfe8fe4e04f456057c2395f5ad011513988c1ffecf5c91cf2fdedbe8095cfba15f0f61c3e1cabc7f34b78d676bce430a79fcf542d74e14a33ec62')

prepare() {
  cd ${pkgname}-${pkgver}
  # make build reproducible, we compress ourselves
  sed '/gzip -9/d' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  export CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}"
  make PREFIX=/usr \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE \
    CFLAGS_EXTRA="${CFLAGS} ${CPPFLAGS} ${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}
  make DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="${pkgdir}" PREFIX=/usr -j1 install
}

# vim: ts=2 sw=2 et:

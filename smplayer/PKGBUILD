# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>

pkgname=smplayer
pkgver=17.11.2
pkgrel=1
pkgdesc='Media player with built-in codecs that can play virtually all video and audio formats'
url='http://www.smplayer.info/'
arch=('x86_64')
license=('GPL')
depends=('qt5-script' 'mpv' 'hicolor-icon-theme' 'libx11' 'zlib' 'gcc-libs')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'youtube-dl: youtube videos and streaming'
            'mplayer: alternative multimedia engine')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('7eeb3b218c8e208711aaef01d27abee0e1eb289f2f129a45fb35a4256bf6089c')
sha512sums=('e0a6e6fc29063b5f82303febdc8243c10276feb4500985e96ce418298c8f10796df44363df3136452a6cb3f8156ebdb4fba3e865a37d433f3d258db493343f81')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE
}

package() {
  cd ${pkgname}-${pkgver}
  make DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:

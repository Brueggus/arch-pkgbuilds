# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-ptrace
pkgver=0.9.4
pkgrel=2
pkgdesc='Python binding of ptrace library to trace processes and syscalls'
url='https://github.com/haypo/python-ptrace'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-sphinx')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/haypo/python-ptrace/archive/${pkgver}.tar.gz)
sha512sums=('dd300fb3f2e3bbca67e585601788a0b83130c96697e8820b02f19934c45e294bc2da2e22f08ad325c34795184a956ae6a42cbe83c976fe5e0d65b9386da82b57')

build() {
  msg "Building python..."
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build
  make -j1 -C doc man text SPHINXBUILD=sphinx-build
}

check() {
  msg "Checking python..."
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python runtests.py
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 doc/build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-ptrace
pkgver=0.9.5
pkgrel=1
pkgdesc='Python binding of ptrace library to trace processes and syscalls'
url='https://github.com/haypo/python-ptrace'
arch=('any')
license=('GPL2')
depends=('python' 'python-six')
makedepends=('python-sphinx' 'python-setuptools')
source=(https://github.com/haypo/python-ptrace/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2ad84da6ba7e1191653a024be7cccad1a428caa4a9e9dd6076f71251daa69350306b415e8c22f59e645805ef1c39d6d4124560aafd19d236084a8f3072b4cd4d')
b2sums=('e99524b4c8811efc3d3f8442c650da3cefd1326300f33099cb5b100d6d0748506ccd667418b4355f4a5ebb9b78f51015ba0a6bb02db603542ee9b57fd272bc29')

build() {
  echo "Building python..."
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -j1 -C doc man text SPHINXBUILD=sphinx-build
}

check() {
  echo "Checking python..."
  cd ${pkgbase}-${pkgver}
  python runtests.py || echo "Warning: tests only run in an env with ptrace allowed"
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 doc/build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

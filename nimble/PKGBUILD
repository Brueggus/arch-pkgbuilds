# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.11.0
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
makedepends=('nim')
source=(https://github.com/nim-lang/nimble/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a31d06128f3d1014d68297aa5cd264023d202cae74746d78ba84e335af3399d2')
sha512sums=('b396599554bb51a40774cdb5c90beab21e9cfef2683fd479fc925de4ae4f5c0d90e821f176b40cda241a3ad8542504cd552f97f2dca88ca32a4edeedbd422fa4')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"

  # completions
  install -Dm 644 nimble.bash-completion "${pkgdir}/usr/share/bash-completion/completions/nimble"
  install -Dm 644 nimble.zsh-completion "${pkgdir}/usr/share/zsh/site-functions/_nimble"
}

# vim: ts=2 sw=2 et:

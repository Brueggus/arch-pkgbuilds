# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.11.4
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
makedepends=('nim')
source=(https://github.com/nim-lang/nimble/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d36e72c7e4c2ef57d51f086b91bf093c6d96e43989a26c30453566889325f209995abfe00449731eacd59ec3d6ab0e6c591413e5356fa8ad6c91d8aa7d330aaa')
b2sums=('31b816ec8cf975631653e069a2c1855a3175254f0aae7ebc5f74ab586f47ef0e0e00235ee47b0996bb43955be7a6d400d6decc6bc0707e094307ff482d665ee8')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"

  # completions
  install -Dm 644 nimble.bash-completion "${pkgdir}/usr/share/bash-completion/completions/nimble"
  install -Dm 644 nimble.zsh-completion "${pkgdir}/usr/share/zsh/site-functions/_nimble"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.8.4
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64' 'i686')
license=('BSD')
depends=('glibc')
makedepends=('nim')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nim-lang/nimble/archive/v${pkgver}.tar.gz)
sha256sums=('ba47cee4f74c56d14d6142e9324b3a8fd5a0e6cd8cbcf81e04ae58543b848e5e')
sha512sums=('93092b59926941b35d74cf678dc6c45e7c7d23654441fd59cf0794d0f9917fb1c13cf813e1109172981bb1da06f925116643b97141fdc039b43fbbcad7b583d7')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=kramdown
pkgname=ruby-kramdown
pkgver=2.0.0
pkgrel=1
pkgdesc='Fast, pure Ruby Markdown superset converter, using a strict syntax definition'
url='https://kramdown.gettalong.org/'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gettalong/kramdown/archive/REL_${pkgver//./_}.tar.gz)
sha256sums=('d91f8c8901f3dfda4503e8eeaf9e4c5403a1c86f99e18d942577da90f36dbbf5')
sha512sums=('8be5ab8fd9312084764eeba2cb5f16270e3b83575d1faea22760d2080daa4021144b038269bb0360283bd30fa11b7dda22ccab1ded5f8ce601304d8dc2606836')

prepare() {
  cd ${_gemname}-REL_${pkgver//./_}
  rake gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-REL_${pkgver//./_}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-REL_${pkgver//./_}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/man/man1"
  mv "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/man/man1/kramdown.1" "${pkgdir}/usr/share/man/man1"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/test"
}

# vim: ts=2 sw=2 et:

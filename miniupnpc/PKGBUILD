# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgname=miniupnpc
pkgver=2.0.20180406
pkgrel=1
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
depends=('sh')
provides=('libminiupnpc.so')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8c7226bd47a6e4a46ffa14b8cdef940a8a5c4f721ab33bc72a217371a4a72996')
sha512sums=('8aa96021757d1086d2cdb7017520eed00f4887f7b5c9edf1fd172e4ab1ce6b7627fe00cadd786e77dda84e7a0b4cc8d438f921630481ac44c2c1a578b0c8d791')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

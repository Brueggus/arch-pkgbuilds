# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgname=miniupnpc
pkgver=2.0.20180410
pkgrel=1
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
depends=('sh')
provides=('libminiupnpc.so')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0e4c6d6ebc8b75eb636df7d3f3370c98713545acc3694b5ae40daa2502487a23')
sha512sums=('08e19fa6f7c9015fefa2c5bf1c007f0c2c1b8c90c1901bd2be2ccbfd19f2fd6a13190a3b5f752bfdc9cefd8e9ca535b584be561a3aa17eec5967b9cebfb28879')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

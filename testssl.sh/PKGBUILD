# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=2.9.5-4
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url='https://github.com/drwetter/testssl.sh'
arch=('any')
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
makedepends=('perl' 'perl-data-dump' 'perl-json' 'perl-test-base')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/drwetter/testssl.sh/archive/v${_pkgver}.tar.gz
        set-install-dir.patch)
sha512sums=('e6c0e9cbe300e3f9e6dea2ebf9933cb30c9b8c4e1026e5010636fe6d0c91c2ac44e952b91813aeae7be4ab90654ff8b461e12692b0c26b786eebd90692c5de67'
            'e74b1efa9b23ff5f4f82a9c9ef8438a3927ea15e2eb6aa39f5ec9db02f28d3dd4c6697badad5a5a61d7c9bad6ec187917af89a172a56b39b661241ad9960abeb')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
}

check() {
  cd ${pkgname}-${_pkgver}
  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

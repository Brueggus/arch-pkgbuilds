# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=2.9.5-7
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url='https://github.com/drwetter/testssl.sh'
arch=('any')
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
makedepends=('perl' 'perl-data-dump' 'perl-json' 'perl-test-base')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/drwetter/testssl.sh/archive/v${_pkgver}.tar.gz
        set-install-dir.patch)
sha512sums=('35ab900b84db8a0a210cb9ce8bb794ff7e8cddd5c85a151083bc76eba834dce37597b29b6235fcf2ce37b004dd295b1756e01374a4943e1e2ed0d2a8cf8761f6'
            'a02a7f1cfe62ee13b07af6a5e4ff2fbd2c346ae47fb416de84e7448ba89e5be5fe66870f749a9cc0cbd614c06a1419fe56595da9ad710799a837be4be99bea40')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
}

check() {
  cd ${pkgname}-${_pkgver}
  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=3.0
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url='https://github.com/drwetter/testssl.sh'
arch=('any')
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
makedepends=('perl' 'perl-data-dump' 'perl-json' 'perl-test-base')
source=(https://github.com/drwetter/testssl.sh/archive/${_pkgver}/${pkgname}-${pkgver}.tar.gz
        set-install-dir.patch)
sha512sums=('a5e65d3145bc422cf03b49087cc24cadca367206a06dda61d114b0a6afd7c4921204e23cdc3ade2f5d7e9ce4adf17b54691b9d33d46b75b296111663e9cf063a'
            '82bb83a5553bdc8d02cb655c76163ac9c86b0fc1cff9c5627cb035eae016c5a8b76a3a9e278de4578779bf63a35548815a53911288872530d65c1100c065f621')
b2sums=('56f13cb58bc930e0c70ef1ff4734dd7e363f362397ebe354c446f1fd6e73a0774a6eb84326a405a5aab224b058754e03fcecda254617eee2fde4d9566ada4e15'
        '26f90e385085e0c50bd877b37e85f489177bcef0be90baad096c6f3e95732542e2adcaf2aa03ba75d264607d869ea479b768834ba476e258eea291c452f4f3b9')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
}

check() {
  cd ${pkgname}-${_pkgver}
  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=2.9.5-3
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url='https://github.com/drwetter/testssl.sh'
arch=('any')
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
makedepends=('perl' 'perl-data-dump' 'perl-json' 'perl-test-base')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/drwetter/testssl.sh/archive/v${_pkgver}.tar.gz
        set-install-dir.patch)
sha512sums=('fb704269f6a2762169591716cb413aba29db0dcc1d671d4b8000a7cf95ce847e77362cba1f56a62813f42b3e640c4796faecd4c265a057283853bc810379c188'
            '61423f2b40e5b30c65efc976f7f68d1e5580e668f6aa8bada0eb39fa1b51d791acc1e20852f02323fa3b0535bcedf08b97f9415c371e9dd89544197abf823089')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
}

check() {
  cd ${pkgname}-${_pkgver}
  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

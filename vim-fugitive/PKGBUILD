# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=3.0
pkgrel=1
pkgdesc='Git wrapper so awesome, it should be illegal'
url='https://github.com/tpope/vim-fugitive'
arch=('any')
license=('custom:vim')
depends=('vim' 'git')
groups=('vim-plugins')
source=(https://github.com/tpope/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        license.txt)
sha512sums=('c68aebb577530be2bb32bb79b261b4b235e14d59e73be462aa4a0f6358c484dbaa19c40f880e1b678780abb66d56960f58a4d5fa99c24c60d797db0e25077464'
            'a50e91b1896b0d952008ba2f641a87af2d1a01e4f280f6c914edcd51ae5d1586d5ade71c3609866b501569007bcb7f2494f08280afec170884b90fab36332fac')

package() {
  cd ${pkgname}-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin ftdetect syntax
  install -Dm 644 ../license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=5.0.1
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('i686' 'x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python2' 'xmlto' 'doxygen')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jtv/libpqxx/archive/${pkgver}.tar.gz
        ${pkgname}-fix-5.0.1-configure-1.patch::https://github.com/jtv/libpqxx/commit/80a9d5386641ac67d4ea1b602c786b45b40b252f.patch
        ${pkgname}-fix-5.0.1-configure-2.patch::https://github.com/jtv/libpqxx/commit/85e9336740475be25ed19924cca0961f7d844c4b.patch)
sha256sums=('21ba7167aeeb76142c0e865127514b4834cefde45eaab2d5eb79099188e21a06'
            'b1ab21997cecb5183f66834db7afec6c2493b087d0df0b034894e6133b36fea0'
            'c54349bb3cf18870051ee631fbef21a35003b80832e199a93394b18122eb3fe3')
sha512sums=('a097a419301d45e8078af08eb4667b7ae06c91d8bb62d57d26e0f6df138eeea350d4ba672a96271f724b3de86f3dd1d57da81aee093835c4b08b2084e98720ef'
            'f99bc6780e9e102414f1de0e50b05d1a52326e2e19f7a9f70729718a2d9129eacfc9510f0b74c3b4d8f83cdf39a6d9578050a37621d0e4199898fa3fe1a52775'
            'c5706c10e8c8f9b03859592a263d0cd004aafa917b4e98153f84081da05635d5c91c0b25af7fd59d28406e578701f1974bbd02dfd83bf0851569aa562518687d')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|python|python2|' -i tools/splitconfig
  patch -p1 < "${srcdir}/${pkgname}-fix-5.0.1-configure-1.patch"
  patch -p1 < "${srcdir}/${pkgname}-fix-5.0.1-configure-2.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

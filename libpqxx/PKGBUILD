# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=6.2.3
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python2' 'xmlto' 'doxygen')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jtv/libpqxx/archive/${pkgver}.tar.gz)
sha256sums=('382b88992c6162c9814388cc8575eb774ffad03d49743a5d9576aa3bffc91dfa')
sha512sums=('b3640ca7d95770017544d0195d78d704ba5745e7816017ed7a5be7dbaeb369887f268fedf30b2d9ab1010e72804bc9d779178c4232a252d28c1a7cc6cace103a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|python|python2|' -i tools/splitconfig
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

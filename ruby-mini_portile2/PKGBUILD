# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname=mini_portile2
pkgname=ruby-mini_portile2
pkgver=2.4.0
pkgrel=1
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/flavorjones/mini_portile/archive/v${pkgver}.tar.gz)
sha256sums=('80b935100a5decb9f5be5f1adbc01ebcb81063c6a9c640a08435d7e7e47c29ec')
sha512sums=('f282617ad5401be5250c9c978e798a2d1c63b749febd898eae05ebb529f5dffea02a7a2adbbe6df69263db7b909f4304dc3ae6942007d032eac276ce030866f1')

prepare() {
  cd mini_portile-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0|' -i ${_gemname}.gemspec
}

build() {
  cd mini_portile-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd mini_portile-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install -N --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

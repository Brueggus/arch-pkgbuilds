# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=owl-lisp
pkgver=0.1.16
pkgrel=1
pkgdesc='Simple purely functional lisp'
url='https://github.com/aoh/owl-lisp'
arch=('x86_64')
license=('MIT')
depends=('glibc')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/aoh/owl-lisp/archive/v${pkgver}.tar.gz)
sha256sums=('4ad5939a6822076c25975da4971c730a077d3e04a077f36923b40087cb25d0ab')
sha512sums=('1716bde1f9b42c5aaccc99b657e10c0625afbad96517e60e9a79ea1eef9dd77097179ffee389753a664a1bd6049fb05c1700743e4c138d10d33c2cb3c0de06d1')

prepare() {
  cd owl-${pkgver}
  sed -ri 's|( \$\(CFLAGS\))|\1 $(LDFLAGS)|g' Makefile
}

build() {
  cd owl-${pkgver}
  make
}

package() {
  cd owl-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 fasl/* -t "${pkgdir}/var/lib/${pkgname}/fasl"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=owl-lisp
pkgver=0.1.15
pkgrel=1
pkgdesc='Simple purely functional lisp'
url='https://github.com/aoh/owl-lisp'
arch=('x86_64')
license=('MIT')
depends=('glibc')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/aoh/owl-lisp/archive/v${pkgver}.tar.gz)
sha256sums=('f731a58e6c6e53f6bd0e1dbd985609030828a5ebc41515c9e0ac69d0ca127f84')
sha512sums=('d890c7207866827bc49275ad478b1e4774d24757e268354d25fb72af850ec2ed1b2d2b5fccee46f8b5021975b5fdb0f5d0b3609a50e4841917fa88d40f5e24a6')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -ri 's|( \$\(CFLAGS\))|\1 $(LDFLAGS)|g' Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 fasl/* -t "${pkgdir}/var/lib/${pkgname}/fasl"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

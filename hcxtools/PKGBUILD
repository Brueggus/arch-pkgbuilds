# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxtools
pkgver=5.2.2
pkgrel=1
pkgdesc='Portable solution for capturing wlan traffic and conversion to hashcat and John the Ripper formats'
url='https://github.com/ZerBea/hcxtools'
arch=('x86_64')
license=('MIT')
depends=('curl' 'libpcap' 'zlib' 'openssl')
source=(https://github.com/ZerBea/hcxtools/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxtools/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('704b8d693b163eb9a82b3c320fc5cbf1e703e8982cbe41a7e0c950e40b613a9a2e3b3e7bacb1f096c72eac4a71f161c76c8faaedb561feadb17945ed870d7a55'
            'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

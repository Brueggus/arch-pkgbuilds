# $Id: PKGBUILD 260246 2017-09-23 17:27:40Z felixonmars $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.13
pkgrel=6
pkgdesc="Add-ons for xmonad"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc-libs' 'xmonad' 'sh' 'haskell-x11' 'haskell-x11-xft' 'haskell-utf8-string' 'haskell-random'
         'haskell-old-time')
makedepends=('ghc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmonad/xmonad-contrib/archive/v${pkgver}.tar.gz")
sha512sums=('d8cc89184edd3fdc531f28de2f94a3b3c57522494dd5f1c1c899c81fbd5c0a8211777ee9b5618932f069fc025894e39e049113b7d271f13c3e3f8d21ffc566e6')

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
             --prefix=/usr -fuse_xft --libsubdir=\$compiler/site-local/\$pkgid \
             --docdir=/usr/share/doc/${pkgname}
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup.lhs copy --destdir=$pkgdir

  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

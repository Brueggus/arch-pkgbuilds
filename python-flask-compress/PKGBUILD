# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

_pyname=Flask-Compress
pkgname=python-flask-compress
pkgver=1.4.0
pkgrel=2
pkgdesc='Compress responses in your Flask app'
url='http://pypi.python.org/pypi/Flask-Compress/'
arch=('any')
license=('BSD')
depends=('python-flask')
makedepends=('python-setuptools')
source=(https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('468693f4ddd11ac6a41bca4eb5f94b071b763256d54136f77957cfee635badb3')
sha512sums=('f86efc13f574335ca530f091252f9ae248f66e3251a3dbcfb5888602b00e1e05d10335f88541fe4f9ab7f537fbf7bc390468a57cefe2d3a7f63a69f2cf0619db')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

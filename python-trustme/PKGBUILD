# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-trustme
pkgname=('python-trustme' 'python2-trustme')
_pyname=${pkgbase/python-/}
pkgver=0.4.0
pkgrel=1
pkgdesc='Library for fake certificate authority (CA) to generate fake TLS certs'
url='https://trustme.readthedocs.io'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-cryptography'
             'python2' 'python2-setuptools' 'python2-cryptography')
checkdepends=('python-pytest' 'python-pyopenssl' 'python-service_identity')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/python-trio/trustme/archive/v${pkgver}.tar.gz)
sha512sums=('bec8de0e93ecad1b322a370e9b8f9651f9a294b58216b1caf42d5cc69293c38fd62542358106a1d5a59469240e471c26617503f2e486b3432a22d7363e0925e7')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  python2 setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package_python-trustme() {
  depends=('python' 'python-cryptography')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-trustme() {
  depends=('python2' 'python2-cryptography')
  cd ${_pyname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

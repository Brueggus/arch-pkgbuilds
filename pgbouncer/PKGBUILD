# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
pkgver=1.9.0
pkgrel=1
pkgdesc='Lightweight connection pooler for PostgreSQL'
url='https://pgbouncer.github.io/'
arch=('x86_64')
license=('BSD')
depends=('libevent>=2.0' 'c-ares' 'systemd' 'pam')
makedepends=('asciidoc' 'xmlto')
checkdepends=('postgresql-libs')
backup=('etc/pgbouncer/pgbouncer.ini'
        'etc/logrotate.d/pgbouncer')
install=${pkgname}.install
source=(https://pgbouncer.github.io/downloads/files/${pkgver}/pgbouncer-${pkgver}.tar.gz
        pgbouncer.ini
        pgbouncer.logrotate
        pgbouncer.service
        pgbouncer.tmpfiles.conf)
sha512sums=('b127f4cb60ca5cdf339da76727918f0a3797a3e0a89f8ed6fdcaa80f3391d2c5bb51e19731915775210c0e5070dd611ee2e410c9a947624e45b5cf11aecdacf9'
            '39abc7b11c9d7a2593941b4d2a82db998ac1b1e3da131ae276da73c7afc4eda7b69bbfd0acd39f7bce20ecb911baf0adba341ff58dcab1a57e419708e7c8d26f'
            '1dc86704fce211b23afe7962c947c7de80a15bef219928acbf486b915d80d44a6590fbc509fe650c97b694a508bcf95d2152663863f0c372323286e644c6d60a'
            '2b4baaaa7129eda54b34405aa4e15a4d37b2173b529ecae070e766f1dcebd0af8f6898ee1b665a60d5f633036a52662e89967f33caacf2841dad441960230ef7'
            '4e5bcb7690335b43a017795b377857c241ce571465363e01e35829c2c210781fefd6880d085e5928db417b69458d1e79069b5494f6be6f0ae19e0894522e7971')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-debug \
    --with-pam
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -C test run_test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ../pgbouncer.ini "${pkgdir}/etc/pgbouncer/pgbouncer.ini"
  install -Dm 644 ../pgbouncer.logrotate "${pkgdir}/etc/logrotate.d/pgbouncer"
  install -Dm 644 ../pgbouncer.service "${pkgdir}/usr/lib/systemd/system/pgbouncer.service"
  install -Dm 644 ../pgbouncer.tmpfiles.conf "${pkgdir}/usr/lib/tmpfiles.d/pgbouncer.conf"
  install -d "${pkgdir}/var/log/pgbouncer"
}

# vim: ts=2 sw=2 et:

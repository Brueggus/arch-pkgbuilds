# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
pkgver=1.10.0
pkgrel=1
pkgdesc='Lightweight connection pooler for PostgreSQL'
url='https://pgbouncer.github.io/'
arch=('x86_64')
license=('BSD')
depends=('libevent>=2.0' 'c-ares' 'systemd' 'pam')
makedepends=('asciidoc' 'xmlto')
checkdepends=('postgresql-libs')
backup=('etc/pgbouncer/pgbouncer.ini'
        'etc/logrotate.d/pgbouncer')
source=(https://pgbouncer.github.io/downloads/files/${pkgver}/pgbouncer-${pkgver}.tar.gz
        pgbouncer.ini
        pgbouncer.logrotate
        pgbouncer.service
        pgbouncer.tmpfiles.conf
        pgbouncer.sysusers)
sha512sums=('8ec9f102b57ca5f92fb689588ec090056ac29f21825400ec67bca413fe076ad50e0b491e65c4cfebc488cc245eb3c88c36db2ee0ba1cb737f35404be0983dd1d'
            '39abc7b11c9d7a2593941b4d2a82db998ac1b1e3da131ae276da73c7afc4eda7b69bbfd0acd39f7bce20ecb911baf0adba341ff58dcab1a57e419708e7c8d26f'
            '1dc86704fce211b23afe7962c947c7de80a15bef219928acbf486b915d80d44a6590fbc509fe650c97b694a508bcf95d2152663863f0c372323286e644c6d60a'
            '2b4baaaa7129eda54b34405aa4e15a4d37b2173b529ecae070e766f1dcebd0af8f6898ee1b665a60d5f633036a52662e89967f33caacf2841dad441960230ef7'
            '421d10fd3b77fc4bec18122f39d237f5eed5130b059dddee5243b17a5a76894ae9262b6f584f5d872ead5028a431a30ce66c8c8e662607573352e739e578bede'
            '05551189022bd160ca0d64831d2559480ae2dcfe8a489aa4c22cc375c3def31d1169e48e17e6502ccd18237d79afef5bc4587f3cf14274c125eb71311d00503b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-debug \
    --with-pam
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -C test run_test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ../pgbouncer.ini "${pkgdir}/etc/pgbouncer/pgbouncer.ini"
  install -Dm 644 ../pgbouncer.logrotate "${pkgdir}/etc/logrotate.d/pgbouncer"
  install -Dm 644 ../pgbouncer.service "${pkgdir}/usr/lib/systemd/system/pgbouncer.service"
  install -Dm 644 ../pgbouncer.tmpfiles.conf "${pkgdir}/usr/lib/tmpfiles.d/pgbouncer.conf"
  install -Dm 644 ../pgbouncer.sysusers "${pkgdir}/usr/lib/sysusers.d/pgbouncer.conf"
}

# vim: ts=2 sw=2 et:

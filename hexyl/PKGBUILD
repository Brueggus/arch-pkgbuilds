# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Attenuation <ouyangjun1999@gmail.com>

pkgname=hexyl
pkgver=0.6.0
pkgrel=1
pkgdesc='Colored command-line hex viewer'
url='https://github.com/sharkdp/hexyl'
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/sharkdp/hexyl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b6f6a324d147fffb51bca7148cd3a7c41d926f609d0626ba4c3f26c7bc48b642613063bf6cb6d485b056113763b5a82b2d40710220ef57cfe8ebf48681cac380')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

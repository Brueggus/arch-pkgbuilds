# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Attenuation <ouyangjun1999@gmail.com>

pkgname=hexyl
pkgver=0.5.0
pkgrel=1
pkgdesc='Colored command-line hex viewer'
url='https://github.com/sharkdp/hexyl'
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/sharkdp/hexyl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('afdb01b1260c29936a129590e31e025ef432d5241229f72e205c8fca7a83fcfde2ea43c6b33713ffb7d50b878d61858a249b1d61be4fb447619b6c2ea2d3ac70')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

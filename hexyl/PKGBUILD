# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Attenuation <ouyangjun1999@gmail.com>

pkgname=hexyl
pkgver=0.7.0
pkgrel=1
pkgdesc='Colored command-line hex viewer'
url='https://github.com/sharkdp/hexyl'
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/sharkdp/hexyl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('74636e01e40a017079e2ffc2f31c4400ee21ec74203729ba858f314b4a793c9eda8e008a9430051b0f36807212b4f5415a1634709b2b0d167ecc31fade844b27')
b2sums=('3c09858fa0630f6e4b47434962e7c6f6bca7d77d12cf5837c4dd162838467946e54da32cc57fc7de7ccb11eb3aac2a1729b534eade12a688377c5b28d7534bb5')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

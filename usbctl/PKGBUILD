# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <git@rxv.cc>

pkgname=usbctl
pkgver=1.1
pkgrel=1
pkgdesc='Linux-hardened deny_new_usb control'
url='https://github.com/anthraxx/usbctl'
arch=('any')
license=('MIT')
depends=('linux-hardened' 'bash' 'usbutils' 'coreutils' 'diffutils' 'procps-ng' 'grep' 'util-linux')
checkdepends=('shellcheck')
optdepends=('sudo: elevate to root when needed')
source=(https://github.com/anthraxx/usbctl/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/anthraxx/usbctl/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
sha512sums=('11425f7fbe5ea5a4df5b15d886c933b30fe01975f00fcf77a607096fa4219a0be3e44a0fa6c1b88fb8f9531dbe45ce98a6b2f232bf8d6c4e8c04e4ce9e043fbf'
            'SKIP')
validpgpkeys=('E240B57E2C4630BA768E2F26FC1B547C8D8172C8') # Levente Polyak

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" MANDIR=/share/man/man1 install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

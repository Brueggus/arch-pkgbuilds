# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=libmypaint
pkgver=1.4.0
pkgrel=1
pkgdesc='Library for making brushstrokes which is used by MyPaint and other projects'
url='http://mypaint.org/'
arch=('x86_64')
license=('ISC')
depends=('glibc' 'json-c' 'gegl' 'glib2' 'json-glib' 'babl')
makedepends=('intltool' 'python' 'gobject-introspection' 'python')
source=(https://github.com/mypaint/libmypaint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        libmypaint-1.4-gegl-version-revert.patch::https://github.com/anthraxx/libmypaint/commit/5e0290c5fb8a175a9f0dd4c6897ff234361c321f.patch
        gegl-0.4.patch)
sha512sums=('d6c17381dbfc8ee346762f9b4d337ccc691e48b9004f0890c682be0911a2a8c9d35592461d54d55063298c16e81353b1f004f7a0c34bb96d7e511bf9b39cd7a7'
            'dd1b4cb146c52865f02857ce9eefff9fc2341cd722e754536dc65613be6d437e63f367208200ab7b2811c0a1cf0f9e782d761667bc6df48c9cdb5cfbc7649019'
            '0df26f4c8f6f6c31a4d5718d97b3338455fbdb9168167bcae36942f5dc1d5c61db1db2448cfa62cb4551356c7454a7ef91885042cb4529a410d6fc59597fa4b9')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Rp1 < ../libmypaint-1.4-gegl-version-revert.patch
  patch -Np1 < ../gegl-0.4.patch
  sed 's|python2|python|' -i autogen.sh
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-gegl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

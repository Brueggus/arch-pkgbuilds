# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=libmypaint
pkgver=1.3.0
pkgrel=4
pkgdesc='Library for making brushstrokes which is used by MyPaint and other projects, with GEGL support'
url='http://mypaint.org/'
arch=('x86_64')
license=('ISC')
depends=('glibc' 'json-c' 'gegl' 'glib2' 'json-glib' 'babl')
makedepends=('intltool' 'python' 'gobject-introspection')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mypaint/libmypaint/archive/v${pkgver}.tar.gz
        versioning.patch)
sha256sums=('8fbdce62f66a027d8b43fa8b061f0e6ff2a3da63cbe55a82d1642e5e39da0654'
            'd850f9b64bc037bd7ed6bbcec0a39115dd43ff896aef59daea79d8786fbb28c0')
sha512sums=('98ba7a48a611014e683d8d534c73af1ed0754e1d9974d62db7efb588abdb9c01d090daaaba3847ece7ca7e5a4e83b334f2a3473a2c9b7e4a9c0680231f650202'
            '20bba36076e90fcc2fbe85ae08f60b9c80ba84c4f1e24a1ca55b0f494d95a561cd899061bdee48bcdd493fe70f84dc07ada7343128849247ce6c2635b1f97fee')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -uNp2 < ../versioning.patch
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-gegl
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check || warning "Failed to execute tests (make problem)"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

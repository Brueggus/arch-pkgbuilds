# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.6.0
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python' 'gtk2')
checkdepends=('cmocka')
source=(http://www.profanity.im/${pkgname}-${pkgver}.tar.gz)
sha1sums=('9ed7dffb7b99dfff6010032602f3b08839b12c94')
sha512sums=('c7d8e74b764276f59fccfa029d45271032db315cdbf6e00deccbd611f202983a0930fed99021202333047acee9f3f0d7779cd9bb3628d2e5c4757e3de8c3629e')

prepare() {
  cp -a ${pkgname}-${pkgver}{,-gtk}
}

build() {
  msg "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
  msg "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc='Console based XMPP client (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python' 'gtk2')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

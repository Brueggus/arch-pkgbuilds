# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>

# TODO: enable omemo
pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.7.1
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
             'gpgme' 'python' 'gtk2' 'autoconf-archive')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha1sums=('01f1d7c290b3fc3aa5b094eb1e708bc3efdd3e27')
sha512sums=('2d7ea82c33a01605a016826590ee7d8b29f6fd3df93feb4211c414d841ee0c809b257d6df79d17003afacab278935d2e8ed96b472307740fdea7ba4bf92b383d')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  msg "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
  msg "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc='Console based XMPP client (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python' 'gtk2')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

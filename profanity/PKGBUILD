# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.9.4
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=(
  'curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme'
  'python' 'gtk2' 'autoconf-archive' 'libsignal-protocol-c' 'libgcrypt'
  'libx11' 'libxss')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('d21a1dbb29d84667dcc0469c7671dd0d21402ef2a6beaa480570c1c111760472')
sha512sums=('ff791c35535b1a4dc419fc68939b4e0ea69b835f7339934b25d557740ddebd5b51f7e21fb2ea9f371cc2c06ec6fe1e8406d331ad59c41cc155acc11cf4e36dd3')
b2sums=('96c6a02ca7e45c3c33e62e1f46688da7f159197cc3ab675ab861830d5c13909359de5ff481dd41a21b396c445e63996ebb6bc45c427679c88cd333b6c5d9071f')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons-and-clipboard \
      --without-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons-and-clipboard \
      --with-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
           'gpgme' 'python' 'libgcrypt' 'libsignal-protocol-c')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
           'gpgme' 'python' 'libgcrypt' 'libsignal-protocol-c' 'gtk2' 'libx11'
           'libxss')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

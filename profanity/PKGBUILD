# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.9.0
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
             'gpgme' 'python' 'gtk2' 'autoconf-archive' 'libsignal-protocol-c'
             'libgcrypt')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('e646f5fb7e3fe9c6c528e9570afbd946376975631fb41e3fd7b84d249b06c1da')
sha512sums=('dd545d6c615aefb98bf05ce3a84f4723b37aca6ef4a895b58396da0042cd4fded6ea32ecb4e7f3b0714d4ccae15a5b72bd1e3ad488189ee72d4066e22de55e44')
b2sums=('4db27e817d141c120e09414a682b48f510eeaa06363c0df6a0d8d9ac1286f3deee2c107d48029ade90fce4ccb726ab32fe99ac67e33d63dfa912249885de81d9')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
  'gpgme' 'python' 'libgcrypt' 'libsignal-protocol-c')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc='Console based XMPP client (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
  'gpgme' 'python' 'gtk2' 'libsignal-protocol-c' 'libgcrypt')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.9.2
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
             'gpgme' 'python' 'gtk2' 'autoconf-archive' 'libsignal-protocol-c'
             'libgcrypt')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('b27fb9a02e6f6cc9e1836c1fea06abb94b115e0b73033c1d3be261ce7c859fb3')
sha512sums=('b7b8fa8ef6980582d4490601e4144cd49c777fb254e0cdc44495b21b3bde7a0c035364c53dccc39b5b34bf768809e6515b2ee6f54e068fe71bd4a86be8ea1ee3')
b2sums=('f3915128d6595082e18a4d8be7b28b8a180fc2b4b61fa32c04accfcaf15ebea72c22b87d080178e2bdf2756ab03a65c39c635fc94527969990e83312571e6f67')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
  'gpgme' 'python' 'libgcrypt' 'libsignal-protocol-c')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc='Console based XMPP client (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify'
  'gpgme' 'python' 'gtk2' 'libsignal-protocol-c' 'libgcrypt')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

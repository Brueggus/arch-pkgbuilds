# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=alicloud-vault
pkgver=1.2.0
pkgrel=1
pkgdesc='Vault for securely storing and accessing Alibaba Cloud credentials in development environments'
url='https://github.com/arafato/alicloud-vault'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go-pie')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('865e7cb1e61d97bef8b0239e4528f4faceed005904805fa015c1a8952764dd4f43798a700f9cda3972a3a3b7fc4334527ddb80089f20a0312ac040ae1c08f440')
b2sums=('09e0c7febd26ddc8c453c489a017ebb77541f380dfd7f3742718e03710b08ea5ca9f0c6be710ea8023162f9d4ef2473f3308a1de80af9a36feaf0be8c0fed776')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -ldflags="-extldflags ${LDFLAGS} -X main.Version=${pkgver} -s -w" \
    -o "./${pkgname}" .
  go clean --modcache
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

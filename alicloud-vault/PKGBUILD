# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=alicloud-vault
pkgver=1.3.0
pkgrel=1
pkgdesc='Vault for securely storing and accessing Alibaba Cloud credentials in development environments'
url='https://github.com/arafato/alicloud-vault'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c9f3e3cfbeecd9cdcbd987d6d999fb6475bcab00d17ce5100104436f75fb08d58da093272e353e14aeb4104d11fd1cd716dc51b2fedaa7b46406632ad80b912c')
b2sums=('18f5d5a01f06bd06f9da404df892f5420c0843411f7a3760bef049c0333b17956d1028dbb10a4af4b98c475dbc0c4f693f13e7716225d8b49c67cebbe5c29ce1')

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build -ldflags="-X main.Version=${pkgver}"
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

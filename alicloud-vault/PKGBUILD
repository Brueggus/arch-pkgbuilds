# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=alicloud-vault
pkgver=1.2.1
pkgrel=1
pkgdesc='Vault for securely storing and accessing Alibaba Cloud credentials in development environments'
url='https://github.com/arafato/alicloud-vault'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1fe1d9e1abe22f91464c63434b94703cc3c901d1810face1331bf9d1d5baa0834029e94a7ea1d46239f3e80084a97a53a3ddfc2c89875850a267d00fb51032ea')
b2sums=('290be8e613c90dbb7f91b7bd6ea59df355ad05e68b2e0caed796576c898062b575eec6c26509d67cec868d06438879f72b37443812224ec530b1f6cdf650e86f')

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build -ldflags="-X main.Version=${pkgver}"
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

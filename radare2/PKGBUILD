# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=3.0.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('fb5db40c6ac143c7257c9f4044851c647eac8942d07421837335f9c520fed447')
sha512sums=('5ca698123ccad80c54c483f7e2cc678dabfc9c1c172670ff9e1d11512174c9ecc15e81dfdb5de846b42c951cafe23d29ceca6f3824504a2f3dac722c659bb2d4')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/zsh/* -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

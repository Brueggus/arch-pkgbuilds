# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=2.0.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('d8f7e1ab96028fc8bd62d4f92fbbe8bbf48c6cda8112e6eaec93bf5ffbbbd1dd')
sha512sums=('1a8db3c71895fd9ccf284e7d0747770a5490872c5ee7f8e6d1c3a2590d2b344873bfc11b5f686bd08caa441bb787b2ec86aae9e83bf61e296f6a233d3ba35207')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # remove shit
  rm "${pkgdir}"/usr/bin/r2-{indent,docker} \
    "${pkgdir}/usr/share/man/man1/r2-docker.1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.3.0
pkgrel=2
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz
        capstone3-1.3.0.patch::https://github.com/radare/radare2/commit/d9d5f79278c0413582e056850184cb5ee0767727.patch)
sha256sums=('37b479224b4acd0159bd8dd1e02da0b250c8c33c17a3b6d7f7bbeb9b975fa749'
            '52622d533eeb5416ce37c5e93e8af52f69e01c937f383da9e66bc75fa2a0c1f1')
sha512sums=('6d38ea213a335225f3b0e74874fd3b162c8ecb3145198c42f08d736d8d44dfd0d263592b5bd701b54715c1f96145201428babf0d75ca342b4fc36fd77a8800c3'
            '2ebe155d3a4c397b920ed00eec4e0f414d2b5e53d90538a20f5afb16f14d1a33cf73159ca52c92bc6a17bae826d6fd76e8bee4a6b362b3d5856317e09708f91c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/capstone3-1.3.0.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # remove shit
  rm "${pkgdir}"/usr/bin/r2-{indent,docker} \
    "${pkgdir}/usr/share/man/man1/r2-docker.1"
}

# vim: ts=2 sw=2 et:

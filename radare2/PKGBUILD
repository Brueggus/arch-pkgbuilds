# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=2.6.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz
       ${pkgname}-${pkgver}.fix-capstone.patch::https://github.com/radare/radare2/commit/3752d992f3140806ea1d513739b6f23addf52df1.patch
       ${pkgname}-${pkgver}.fix-zip.patch::https://github.com/radare/radare2/commit/a555204c3617438250cded94d084b4dec1aaf551.patch
       ${pkgname}-${pkgver}.fix-lib-dist.patch::https://github.com/radare/radare2/pull/10164/commits/bd21b8b0f5968849b19ec705f43460a104300356.patch)
sha256sums=('c4722519e53bbb03289e3b98e89e637e2d1f401d02cf78a198e42c2e92aeb820'
            '80c84b357f89b49c6a48f78c20647b968ebe2a97e039ef669989671e0b0e93d8'
            'ca8d31dabd4f6b3ef7ac0e618945bf5e543cbe7259f7971bab9584eda9cd8436'
            'c968b3f113982e2a4e092e84a8f8cd512b6212bb27d4a09bd6a7ae311bd966f0')
sha512sums=('44e7d0c10e17b40d4d71af851cf462c159b03b3242dfccd786bd54d3cf9f92441084f26694166dd1385e84c00626f45dbe09d1fe713595721723c468ac47b01b'
            '4412c99facdcf15bb6f3202c15e7c0a581df6eda67e5476b70f95e21540d253fdd2dd5518009c139cc7723d589f0d903a2a98f3d6ed33578b0bed2e5c2bf4242'
            'ec2aa6993afc004bc57a2e4f3feeff99f0d074d40a22d24e81c63635422208e756ca91edcb74957b89fa3aba707ed5602a8b0c8425a43e407872e1ca82d890cd'
            'aee102df6196e2ba0f2334828ecad27d468b845efa1f95e587ec26ef229bcef1f94d3d0daca44fc9731c11a5cfc5f1609bf7c90d56836b0f1679db2cf916c075')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../${pkgname}-${pkgver}.fix-capstone.patch
  patch -Np1 < ../${pkgname}-${pkgver}.fix-zip.patch
  patch -Np1 < ../${pkgname}-${pkgver}.fix-lib-dist.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

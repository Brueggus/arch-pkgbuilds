# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=3.0.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('2de7c54f723b8439f899a86cb9e5142e7b62664994cea60aefdd9c2a69a3f9fe')
sha512sums=('755cbbf968179d987b1bd11469ef356e9e5b9968983f45a3276e0001c877d13b3b12b71a29dd954e8111c5196284c0e49f26bd79f5b5e037dd4dd27b1f73e380')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/zsh/* -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=3.2.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('18dbec13588d7795e3ce0b3a442578227d941b70cb1421b52abe127191db6634')
sha512sums=('9773068568cf71797b0c86837188604130ef9d63e7b7fd115820d8979dde37d259aaea45002c9984e8c3599e9bade53720d3bb1b3635bdad5a2dc73aff54f048')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/zsh/* -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:

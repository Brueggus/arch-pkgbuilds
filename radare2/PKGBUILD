# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.5.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('c6b465cb2f36a206d5e9380c0bcbb4c05ed5cb7995e554703206e0bbdc9c74a1')
sha512sums=('d12f448e13dba4507704143216fead40bcee88b59c006f7bc46778a6f423abf4b2b856df4348a4ccaa422e72d9a21ae18add691ceaf5b06936aec214871f7d88')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # remove shit
  rm "${pkgdir}"/usr/bin/r2-{indent,docker} \
    "${pkgdir}/usr/share/man/man1/r2-docker.1"
}

# vim: ts=2 sw=2 et:

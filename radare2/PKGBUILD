# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.4.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('fb58417a6d35af4096da9130a041e1d36f900b17e2c2594b405a9b88ce33c7a8')
sha512sums=('618774dbe02eaf160e4b246501d3ff48257305bc795ed241c40ff5e71ee553259384a17786a914cebe563fe8d6a7be1856883875fcee15f8177c8c9a1192889b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # remove shit
  rm "${pkgdir}"/usr/bin/r2-{indent,docker} \
    "${pkgdir}/usr/share/man/man1/r2-docker.1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=pdfcrack
pkgver=0.17
pkgrel=1
pkgdesc='Password recovery tool for PDF-files'
url='http://pdfcrack.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/pdfcrack/pdfcrack-${pkgver}.tar.gz{,.asc})
sha256sums=('561bb1ee21005b6a9cf09771571836de6625ad6c52822b08eaf33b9f32ef0e96'
            'SKIP')
sha512sums=('a7d41383b53d57a8d6cce6e7da9ccc8d3cb30833d9451ce1554c8f04286dc64336313591b5b579a1a3402ccc317c96356e5124aa7f43022bbede0ee9b1d5c0c5'
            'SKIP')
validpgpkeys=(
  '987AEFC56E70D68440C0D45149241DE2BA57F699' # Henning Norén <henning.noren@gmail.com>
  'AC49CA5B95335D6DAF85BAC563EFAA4606037EB3' # Henning Norén <henning.noren@gmail.com>
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 pdfcrack -t "${pkgdir}/usr/bin"
  install -Dm 644 README changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pdfcrack.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

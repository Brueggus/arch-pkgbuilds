# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=pdfcrack
pkgver=0.18
pkgrel=1
pkgdesc='Password recovery tool for PDF-files'
url='http://pdfcrack.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/pdfcrack/pdfcrack-${pkgver}.tar.gz{,.asc})
sha256sums=('8223aec52a2ae36f9a10a731513461458874f1fd8d803e4a04910d9dbf1cba0c'
            'SKIP')
sha512sums=('7d1624d835bcefff131745c75dc2ecafe63afdbf2c56568880804d5b7062a8eef27eebfedf340ab9d7e5770dd1a553d5d30b7ea5bca63a317deb40e0deed35a2'
            'SKIP')
validpgpkeys=(
  '987AEFC56E70D68440C0D45149241DE2BA57F699' # Henning Norén <henning.noren@gmail.com>
  'AC49CA5B95335D6DAF85BAC563EFAA4606037EB3' # Henning Norén <henning.noren@gmail.com>
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 pdfcrack -t "${pkgdir}/usr/bin"
  install -Dm 644 README changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pdfcrack.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Changaco <changaco ατ changaco δοτ net>

pkgname=slowhttptest
pkgver=1.8
pkgrel=1
pkgdesc='Highly configurable tool that simulates some Application Layer Denial of Service (DoS) attacks'
url='https://github.com/shekyan/slowhttptest'
arch=('x86_64')
license=('Apache')
depends=('openssl' 'gcc-libs')
source=(https://github.com/shekyan/slowhttptest/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('31f7f1779c3d8e6f095ab19559ea515b5397b5c021573ade9cdba2ee31aaef11')
sha512sums=('527a902927302243b92beb9f9ab184d552c7092dc15fe5a3e95abdd444b00cfeaa2dfe1f93b60e3a350e7238fb8d4d5669cd3e03f84da9cc4dc7927468406ef4')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:

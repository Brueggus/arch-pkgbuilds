# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: William Grieshaber <me@zee.li>
# Contributor: Victor Aur√©lio <victoraur dot santos at gmail dot com>

pkgname=speedtest-cli
pkgver=1.0.2
pkgrel=1
pkgdesc='Command line interface for testing internet bandwidth using speedtest.net'
url='https://github.com/sivel/speedtest-cli'
arch=('any')
license=('Apache')
depends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sivel/speedtest-cli/archive/v${pkgver}.tar.gz)
sha512sums=('aa2dad2d9c1246af332c63437be171c0c0d138f16604d246f9f5b8b36b456fbb02376b166c75491211369d6b61220be7669f0b0487235f74dc2db95014e8d349')

package(){
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 ${pkgname}.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:

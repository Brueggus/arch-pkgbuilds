# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Andrews <christophersimonandrews [at] gmail [dot] com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Serge Victor <arch@random.re>
# Contributor: Krzysztof Wloch <wloszekk [at] gmail [dot] com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>

pkgname=fasm
pkgver=1.71.60
pkgrel=2
pkgdesc='Fast and efficient self-assembling x86 assembler for DOS, Windows and Linux operating systems'
url='https://flatassembler.net/'
arch=('i686' 'x86_64')
license=('custom')
depends_x86_64=('lib32-glibc')
source=(${pkgname}-${pkgver}.tgz::https://flatassembler.net/${pkgname}-${pkgver}.tgz)
sha512sums=('6c31fd044a972cb694b84adbb2b18940d9256f12c4ad1acd466b2f9c99c3bc01bfa6daba434e480c7694be5a604809d5f0da8dc41ff09919f0a5f0dda9ad74b3')

build() {
  cd ${pkgname}
  CFLAGS+=" -pie"

  # compile fasm with itself
  ./fasm source/libc/${pkgname}.asm
  gcc -m32 ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -L/usr/lib32 source/libc/${pkgname}.o -o source/libc/${pkgname}

  # tools
  local s
  for s in listing prepsrc symbols; do
    ./source/libc/${pkgname} tools/libc/${s}.asm ${s}.o
    gcc -m32 ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -o ${s} ${s}.o
  done
}

package() {
  cd ${pkgname}

  install -Dm 755 source/libc/${pkgname} -t "${pkgdir}/usr/bin"
  # tools
  local s
  for s in listing prepsrc symbols; do
    install -Dm 755 ${s} "${pkgdir}/usr/bin/${pkgname}-${s}"
  done

  # docs
  install -Dm 644 ${pkgname}.txt whatsnew.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/elfexe/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe"
  install -Dm 644 examples/elfexe/dynamic/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfexe/dynamic/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfexe/dynamic"
  install -Dm 644 examples/elfobj/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/elfobj"
  install -Dm 644 examples/libcdemo/*.asm -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 examples/libcdemo/*.inc -t "${pkgdir}/usr/share/doc/${pkgname}/examples/libcdemo"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

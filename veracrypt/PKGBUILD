# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
_pkgname=VeraCrypt
pkgver=1.21
pkgrel=1
pkgdesc='Disk encryption with strong security based on TrueCrypt'
url='http://veracrypt.codeplex.com/'
arch=('i686' 'x86_64')
license=('custom:TrueCrypt')
depends=('fuse2>=2.8.0' 'wxgtk3>=3.0' 'libsm' 'device-mapper')
optdepends=('sudo: mounting encrypted volumes as nonroot users')
makedepends=('yasm' 'unzip')
source=("${pkgname}_${pkgver}_Source.zip::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.zip/download"
        "${pkgname}_${pkgver}_Source.zip.sig::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.zip.sig/download"
        no-makeself.patch
        veracrypt.desktop)
sha512sums=('c71af0ed7b886b9a14421d71765c4324d7f10eaf12ba417213cb7775543eb035bc208b0a6ae2a4a5264a558a5c8431bc9db819131d708b4d9d036219e1d7ca7e'
            '9a444969a36a52cd6bd4043a125b82207cf2b9f4d2292d4a6f2cbc503d3b71d00b74f07c171c9461bbf49038174856132dc336c4d81d30732d49a22f4cfb6113'
            'e3972fd239fd83807c5ea9dd9268a0e962b0bd9cb9cf8f6251ac51a44a94f3c57f0b1e6857b42e4296cff531030c8278c543b386ebf463d4e19fe9a536acbcf9'
            'f689ca64bac7042030de7714aed8cc89f2c5f87b407444b1b121491c1d89c147babaaa454ddc2a93b70ae20d4da59f96ad64f01b04bea9017d658c377faeb75d')
validpgpkeys=('993B7D7E8E413809828F0F29EB559C7C54DDD393') # VeraCrypt Team <veracrypt@idrix.fr>

prepare() {
  cd src
  patch -p1 < "${srcdir}/no-makeself.patch"
}

build() {
  cd src
  make PKG_CONFIG_PATH=/usr/lib/pkgconfig \
    WX_CONFIG=/usr/bin/wx-config-gtk3 \
    TC_EXTRA_LFLAGS+="-ldl ${LDFLAGS}" \
    TC_EXTRA_CXXFLAGS="${CXXFLAGS}" \
    TC_EXTRA_CFLAGS="${CFLAGS}"
}

package() {
  cd src
  install -Dm 755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/veracrypt.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

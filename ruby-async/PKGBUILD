# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=1.4.1
pkgrel=1
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-nio4r' 'ruby-timers' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/socketry/async/archive/v${pkgver}.tar.gz)
sha256sums=('db7198b3122b5c4600ae1c14ddfc2761437ca5a63fae37a68d792385c27e9983')
sha512sums=('82ebcdac7cb77b7ae782e7d37daa9bcc35ad100ea165ce5edf0cb29c41c64d009c4bdd6201c064452417bebe0a2f0ff6780253c72640650d4226362347c47b20')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=1.8.0
pkgrel=1
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-nio4r' 'ruby-timers' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/socketry/async/archive/v${pkgver}.tar.gz)
sha256sums=('9a86357a8a46dd1b9dedf70465a8722c71e8a1734d0a83fc73a4f268ca1efd5d')
sha512sums=('71dbfd760fe667d9bcbe580fcc95f38bd37af57605b5f455083ed0ddc772e10cb2fd46805d22d2b66e838b0b1bb9987b28927794e1db40f14a1cf10a92cbc31e')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:

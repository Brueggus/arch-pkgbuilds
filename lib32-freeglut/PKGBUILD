# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_pkgname=freeglut
pkgname=lib32-${_pkgname}
pkgver=3.2.0
pkgrel=1
pkgdesc='Provides functionality for small OpenGL programs (32-bit)'
url='http://freeglut.sourceforge.net/'
arch=('x86_64')
license=('MIT')
depends=('lib32-libxi' 'lib32-libxrandr' 'lib32-libgl' 'freeglut')
makedepends=('lib32-mesa' 'lib32-glu' 'lib32-libxxf86vm' 'cmake')
replaces=('lib32-glut')
provides=('lib32-glut')
conflicts=('lib32-glut')
source=(https://downloads.sourceforge.net/freeglut/${_pkgname}-${pkgver}.tar.gz
        glut.diff)
sha512sums=('78da1eaa845607ddd97a6744f3a97ff26cdb562fdf990695c8ac97a60d9d692496e4bf3672acfc522add60823c2f3bc7435e19e8041fbbb5d95851c0b641a818'
            '6778538722707460a7c71eceee430f3945fcfcff351d0a05623417a183af4e8f85a497e24208b73756b2713bb5c11d39dbd4e6de7f06b8865fd81a1faa23b9a4')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i ../glut.diff
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake -H${_pkgname}-${pkgver} -Bbuild \
    -DFREEGLUT_BUILD_STATIC_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  rm -rf "${pkgdir}/usr/include"
  ln -sr "${pkgdir}"/usr/lib32/pkgconfig/{glut,freeglut}.pc
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ${_pkgname}-${pkgver}/COPYING
}

# vim: ts=2 sw=2 et:

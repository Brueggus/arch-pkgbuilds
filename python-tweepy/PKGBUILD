# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tweepy
pkgname=('python-tweepy' 'python2-tweepy')
_pypiname=tweepy
pkgver=3.7.0
pkgrel=1
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-pip' 'python-requests' 'python-requests-oauthlib'
             'python-six' 'python-pysocks'
             'python2-setuptools' 'python2-pip' 'python2-requests' 'python2-requests-oauthlib'
             'python2-six' 'python2-pysocks')
source=(https://github.com/tweepy/${_pypiname}/archive/${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha512sums=('d0aa9cb198544e6cd1f24094c672f113b425af6c9f8c470cd8721e7e423037a05aae7d5dba7ceb1d48d8a03e736921f67fd32a396f1f6a703cb9b194066a5248')

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
  python2 setup.py build
}

package_python-tweepy() {
  depends=('python' 'python-requests' 'python-requests-oauthlib' 'python-six' 'python-pysocks')
  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

package_python2-tweepy() {
  depends=('python2' 'python2-requests' 'python2-requests-oauthlib' 'python2-six' 'python2-pysocks')
  cd ${_pypiname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:

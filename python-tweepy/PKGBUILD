# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tweepy
pkgname=('python-tweepy' 'python2-tweepy')
_pypiname=tweepy
pkgver=3.8.0
pkgrel=1
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-pip' 'python-requests' 'python-requests-oauthlib'
             'python-six' 'python-pysocks'
             'python2-setuptools' 'python2-pip' 'python2-requests' 'python2-requests-oauthlib'
             'python2-six' 'python2-pysocks')
source=(https://github.com/tweepy/${_pypiname}/archive/v${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha256sums=('a71d2bb42ece75222fb34d8c1b7a23db70c276206ba5e38b467a02513da4d0c4')
sha512sums=('ac0dd20db9bfc2e5ae8f0fbac77052c3636bb3035ed3f0c144e15834765f4feff989f5431dbcd54e934223b2a3256d70446ef94d9274468a2e59e2fea71690c9')

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
  python2 setup.py build
}

package_python-tweepy() {
  depends=('python' 'python-requests' 'python-requests-oauthlib' 'python-six' 'python-pysocks')
  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

package_python2-tweepy() {
  depends=('python2' 'python2-requests' 'python2-requests-oauthlib' 'python2-six' 'python2-pysocks')
  cd ${_pypiname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:

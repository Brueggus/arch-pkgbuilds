# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-socketio
pkgname=('python-socketio' 'python2-socketio')
pkgver=1.8.3
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-engineio'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-engineio')
checkdepends=('python-pytest' 'python-mock'
              'python2-pytest' 'python2-mock')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}.tar.gz)
sha256sums=('c8a6e0576e50e9cc3db3d8cc0901d3f1f587e67643775c40b9d4a3c30f6a7448')
sha512sums=('e6b3c64c42c25490f9f5e45c32fad44cb5acb3c1ef09705d7539cf1ad828c3ab4cb75fbc4578ae85a4a98f6134827dbbfac02316c2afbb0508c0774aead35704')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  msg2 'Building python2...'
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  msg2 'Checking python...'
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  msg2 'Checking python2...'
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-socketio() {
  depends=('python' 'python-six' 'python-engineio')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-socketio() {
  depends=('python2' 'python2-six' 'python2-engineio')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

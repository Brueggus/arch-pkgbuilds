# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-socketio
pkgname=('python-socketio' 'python2-socketio')
pkgver=2.1.2
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-engineio' 'python-tornado'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-engineio' 'python2-tornado')
checkdepends=('python-pytest' 'python-mock'
              'python2-pytest' 'python2-mock')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}.tar.gz)
sha256sums=('410e56a46aa08a6f254b506dd6c3e438341a45679886f3d81684fcc8b743fc3d')
sha512sums=('a7247a367bc7f89ab02e5099d18250d1c5e36502126631dccfaab6b26fbc72d72e3464ff1f05ea12efe1f0ab5d0bfaae2c38e0c809590284313466058c480b0f')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  msg2 'Building python2...'
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  msg2 'Checking python...'
  (cd ${pkgbase}-${pkgver}
    py.test -k 'not test_logger'
  )
  msg2 'Checking python2...'
  (cd ${pkgbase}-${pkgver}-py2
    py.test2 -k 'not test_logger'
  )
}

package_python-socketio() {
  depends=('python' 'python-six' 'python-engineio')
  optdepends=('python-tornado: tornado asyncio webserver')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-socketio() {
  depends=('python2' 'python2-six' 'python2-engineio')
  optdepends=('python2-tornado: tornado asyncio webserver')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

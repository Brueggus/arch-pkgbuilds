# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=3.1.2
pkgrel=2
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio')
makedepends=('python-setuptools' 'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest' 'python-mock')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}.tar.gz)
sha256sums=('a44950c0535f92b3ad6f055536f4ffc35a4be8af8f88919ec25e55c2765f5fa8')
sha512sums=('026aa04ef3d3dd32a3617b8572e0d704ec03cb9a694059ef89325a8d766ff25f93341f1f2621b46729b9462d4927bdc7052fe051a73ea7b94046a0da0d5aaf21')

build() {
  msg2 'Building python...'
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  msg2 'Checking python...'
  cd ${pkgbase}-${pkgver}
  py.test -k 'not test_logger'
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

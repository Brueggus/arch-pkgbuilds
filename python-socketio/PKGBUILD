# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=4.4.0
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio')
makedepends=('python-setuptools' 'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest' 'python-mock')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1df422c78c5c117fe6a402c73441552264c3d04277b8ae35b8e9e654abb127fe6af45188434cb7203199216dc65e7b29aa328b324471cf74f66d8e6d518db0ba')
b2sums=('bdbf12289d3376e775caa72d5c4665937860ed0c1058c17ef1d0f477de9b7a53b38b6cae3d64c2b70faa13367ff8f681b06a9ea64ac5fc051ac3e1ee5a7a93a0')

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  py.test -k 'not test_logger'
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=scapy3k
_pkgname=scapy
pkgver=0.22
pkgrel=1
pkgdesc='Powerful interactive packet manipulation program written in Python 3'
url='https://github.com/phaethon/scapy'
depends=('tcpdump' 'python' )
optdepends=('python-cryptography: WEP decryption, IPsec and certificate support'
            'python-pyx: psdump() and pdfdump() functions'
            'python-matplotlib: plotting support'
            'python-netifaces: network interface information support'
            'graphviz: conversations() method support'
            'sox: for VOIP support')
checkdepends=('python-cryptography' 'python-pyx' 'python-matplotlib' 'python-netifaces' 'graphviz' 'sox')
arch=('any')
license=('GPL2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/phaethon/scapy/archive/v${pkgver}.tar.gz)
sha256sums=('257144ee72b6f40372d0fd7bf6dbe8743bdfa82805846ef88c25f4963286d153')
sha512sums=('e3c85254bf11cec890e26ec5a459b206771f34cd7a8e7a890c19578dbe6ac9854d6403e0f49d72cd0223e7244a82cb000b0ec512a0f6118b0bddc0fa6a24a0f2')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|((bin\|doc)/(UT)?scapy)|\13k|g' -i setup.py
  mv doc/scapy.1.gz doc/scapy3k.1.gz
  mv bin/scapy{,3k}
  mv bin/UTscapy{,3k}
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/test
  # skip tests as they hang
  # ./run_tests -t regression.uts -K netaccess
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

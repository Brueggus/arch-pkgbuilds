# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=4.1
pkgrel=1
pkgdesc='Utility to ping multiple hosts at once'
url='http://www.fping.org/'
arch=('x86_64')
license=('custom')
depends=('glibc')
install=${pkgname}.install
source=(https://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('cd31ea336985f8d727b0db3c70213b827f0282b0c382694e10c72a89c425f2f7383d07f8f2febdc23aa246ebbf65ac9751b2404be405088963a682945edc80ba'
            'SKIP')
validpgpkeys=(
  "8D1E89B8FBA920A5A609F2615A11A22BC5A1F734" # David Schweikert <david@schweikert.ch>
)

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --mandir=/usr/share/man \
              --enable-ipv4 \
              --enable-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Thurstenson <thurstylark@gmail.com>

pkgname=vim-ale
pkgver=1.9.0
pkgrel=1
pkgdesc='Asynchronous Lint Engine'
url='https://github.com/w0rp/ale'
arch=('any')
license=('BSD')
depends=('vim')
optdepends=('shellcheck: for sh/bash support'
            'flake8: for python support')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/w0rp/ale/archive/v${pkgver}.tar.gz)
sha256sums=('df7958311f447a373a5016fdce5ca1c6f1e6ed1ef13d9193a8220e3f0541226a')
sha512sums=('8ec81163c86d2b647451f2146a68715cba485d3318e01fbdd377528a882070be344cef345544386a400d46a4cae75e64d10caf1f7ad7cc951c73cca1a3fa30b4')

package() {
	cd ale-${pkgver}
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
	install -dm 755 "${pkgdir}/usr/share/vim/vimfiles"
	cp -dr --no-preserve=ownership {ale_linters,autoload,doc,ftplugin,plugin,syntax} \
		"${pkgdir}/usr/share/vim/vimfiles"
}

# vim: ts=2 sw=2 et:

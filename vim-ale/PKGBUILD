# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Thurstenson <thurstylark@gmail.com>

pkgname=vim-ale
pkgver=1.8.0
pkgrel=1
pkgdesc='Asynchronous Lint Engine'
url='https://github.com/w0rp/ale'
arch=('any')
license=('BSD')
depends=('vim')
optdepends=('shellcheck: for sh/bash support'
            'flake8: for python support')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/w0rp/ale/archive/v${pkgver}.tar.gz)
sha256sums=('44168b16de4ad80bafe3c06ba7ed3950d2c3c140a8719a318989c13c4b5e0065')
sha512sums=('d62bf7edc9e118902d9cc86dc4d36acd32cd3bdf77733dce7e0f217f4ab5dc96c02fa9b7f426681a346204ef52268bc0582de6bde45d9cae800f12fcff3ad1ed')

package() {
	cd ale-${pkgver}
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
	install -dm 755 "${pkgdir}/usr/share/vim/vimfiles"
	cp -dr --no-preserve=ownership {after,ale_linters,autoload,doc,plugin} \
		"${pkgdir}/usr/share/vim/vimfiles"
}

# vim: ts=2 sw=2 et:

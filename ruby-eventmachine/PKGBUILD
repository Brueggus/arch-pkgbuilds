# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

_gemname=eventmachine
pkgname=ruby-${_gemname}
pkgver=1.2.3
pkgrel=2
pkgdesc='EventMachine: fast, simple event-processing library'
url='http://rubyeventmachine.com'
arch=('i686' 'x86_64')
license=('Ruby' 'GPL2')
depends=('ruby' 'openssl')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/eventmachine/eventmachine/archive/v${pkgver}.tar.gz)
sha512sums=('7e6e81e2b7e93436467d47b7bbb53491739805144f5b78abf2d257338094a6500b292cea7e09b92a05078014d1926b529ec64f71d6a42594b8b7ac18e668f7bd')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files README.md CHANGELOG.md GNU LICENSE|find|' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}*.gem
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

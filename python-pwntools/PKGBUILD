# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ding Xiao <tinocodfcdsa10@mails.tsinghua.edu.cn>
# Contributor: Firmy <firmianay@gmail.com>

pkgname=python-pwntools
_pyname=pwntools
pkgver=4.0.1
pkgrel=2
pkgdesc='CTF framework and exploit development library'
url='https://github.com/Gallopsled/pwntools'
arch=('any')
license=('MIT' 'GPL2' 'BSD')
depends=('python'
         'python-mako'
         'python-pyelftools'
         'python-capstone'
         'ropgadget'
         'python-pyserial'
         'python-requests'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-dateutil'
         'python-packaging'
         'python-psutil'
         'python-intervaltree'
         'python-sortedcontainers'
         'python-unicorn'
         'python-tox'
         'python-paramiko'
         'ropgadget'
         'python-setuptools')
source=(https://github.com/Gallopsled/pwntools/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5598bf8ebb96264dab3663d296be3dfe783451cf3535ad611d094a7c550d467f')
b2sums=('9003e0ef0ceaa3e1a089de68f94c854e65c12ce4e21d3b3aadfa0c777f7aa168f02869b34ccdede04b17d53a5ad8ea8b0d91a20ea0d73496b814847a3ce4b423')

prepare() {
  cd ${_pyname}-${pkgver}
  sed 's|>=1.0.2rc1||' -i setup.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build --only-use-pwn-command
  install -Dm 644 LICENSE-pwntools.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.{txt,md}
}

# vim:set ts=2 sw=2 et:

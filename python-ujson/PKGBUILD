# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-ujson
pkgname=('python-ujson' 'python2-ujson')
_pkgname=ultrajson
pkgver=3.0.0
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
arch=('x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pip' 'python2-pip')
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('bd77fd9ba4c1f059216be46afced3db20b82b8637c5d41fe5ba04b9d63e636c422cae44a7fcee913dc40fc9d91367f2de97db757e4cb53b22ecb77b1c765e27d')
b2sums=('e15895072e44e0f54a7437fe5613aff3fa912f7d19f3058bb4977cf407d8b2a3d0c8c94526459bfbbccbcff56737c76be880bd3385776c8a771dba0194c8e0e7')

prepare() {
  sed '/setup_requires=\["setuptools_scm"\],/d' -i ${_pkgname}-${pkgver}/setup.py
  sed "s/{version}/$pkgver/" ${_pkgname}-${pkgver}/python/version_template.h > ${_pkgname}-${pkgver}/python/version.h
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build -b ../buildpy3
    ln -s ../buildpy3 build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build -b ../buildpy2
    ln -s ../buildpy2 build
  )
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" pytest
}

package_python-ujson() {
  depends=('python')
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-ujson() {
  depends=('python2')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-ujson
pkgname=('python-ujson' 'python2-ujson')
_pkgname=ultrajson
pkgver=2.0.3
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/esnme/ultrajson"
arch=('x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pip' 'python2-pip')
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/esnme/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('31ccee759847424de1d683bb8b55554898ea2b358a3c48524f4f455aa0c5d732d0edf84bd5fa0c203ca9c58b4fbc6d94130f447c1cbae564dc60e4863b17dad8')
b2sums=('063aa628c9d06f0dcebd73ee044ec3458d0613d2ff7e97b33d7b187ebc9557e9ab53d7db5ba987e2acf374ed3c2a9e7d0d5163a22e2d2bd691789711487ce52a')

prepare() {
  sed '/setup_requires=\["setuptools_scm"\],/d' -i ${_pkgname}-${pkgver}/setup.py
  sed "s/{version}/$pkgver/" ${_pkgname}-${pkgver}/python/version_template.h > ${_pkgname}-${pkgver}/python/version.h
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build -b ../buildpy3
    ln -s ../buildpy3 build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build -b ../buildpy2
    ln -s ../buildpy2 build
  )
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" pytest
}

package_python-ujson() {
  depends=('python')
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-ujson() {
  depends=('python2')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:

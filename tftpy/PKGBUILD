# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tftpy
pkgname=('python2-tftpy')
pkgver=0.6.2
pkgrel=2
pkgdesc='Pure Python TFTP library'
url='https://github.com/msoulier/tftpy'
arch=('any')
license=('MIT')
makedepends=('python2' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/msoulier/tftpy/archive/${pkgver}.tar.gz)
sha256sums=('e3e47a787f6694265dcf94fe84cb69f483ee8143ec8cd557c9092484bc380f08')
sha512sums=('1e5c31b2cf7df922fe0e5a266a80e867a6f59fbfe3aa8b66f9b2157ad6433bae81872c56faede0e9d8ed1320dd8ffadb3807d85924f2c1113ac9712534bfd48c')

build() {
  cd ${pkgbase}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${pkgbase}-${pkgver}
  make test PY=python2
}

package_python2-tftpy() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README ChangeLog  -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv "${pkgdir}/usr/bin/tftpy_client"{.py,}
  mv "${pkgdir}/usr/bin/tftpy_server"{.py,}
}

# vim: ts=2 sw=2 et:

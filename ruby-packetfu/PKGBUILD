# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=packetfu
pkgname=ruby-${_gemname}
pkgver=1.1.12
pkgrel=1
pkgdesc='Library for reading and writing packets to an interface or to a libpcap-formatted file'
url='https://github.com/packetfu/packetfu'
arch=('any')
license=('BSD')
depends=('ruby' 'ruby-network_interface' 'ruby-pcaprub')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/packetfu/packetfu/archive/version-${pkgver}.tar.gz)
sha512sums=('c50c12274a476eeb19588dc5e545487319946edc75e808105a511f6de1b91c9c2713348617ffe859c579e43ffb00e6d6b498df93e43f3cf921fc1412977dc394')

prepare() {
  cd ${_gemname}-version-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed "s|1.1.13-pre|${pkgver}|" -i lib/packetfu/version.rb
}

build() {
  cd ${_gemname}-version-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-version-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=badtouch
pkgver=0.5.2
pkgrel=1
pkgdesc='Scriptable network authentication cracker'
url='https://github.com/kpcyrd/badtouch'
depends=('openssl' 'libcap')
makedepends=('cargo')
arch=('x86_64')
license=('GPL3')
install=badtouch.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('616881b449de0ff645975247ed18ace8457de959cfd8e53d67c55508c48b2009')
sha512sums=('68343b91764691ee36d5ca5eb66e58ac3ed279400dc7f622e09a78fa76dd0ad9fba0abc51122dcc042885b8c85c69364dda7d89eb01d3036178d549a9b954a4c')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 docs/badtouch.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 scripts/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: bitwave <aur [aT] oomlu [d0T] de>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=yara
pkgver=3.9.0
pkgrel=1
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara'
arch=('x86_64')
license=('BSD')
depends=('openssl' 'file')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/VirusTotal/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('ebe7fab0abadb90449a62afbd24e196e18b177efe71ffd8bf22df95c5386f64d')
sha512sums=('c9aa17d5e25f21f935e9938d8248366f928889a6fb8f70bbd22b6e2d7f74427d47259ca410dfcaa8de3e8ad7c82715fde0366e24209a672555e54d8d459086be')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-crypto \
    --enable-magic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

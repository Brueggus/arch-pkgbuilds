# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=4.0.0
pkgrel=1
epoch=1
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('i686' 'x86_64')
depends=('opencl-icd-loader')
optdepends=('libxnvctrl: NVIDIA X driver configuration support')
makedepends=('opencl-headers')
license=('MIT')
source=(https://hashcat.net/files/${pkgname}-${pkgver}.tar.gz{,.asc}
        hashcat-fix-version.patch)
sha512sums=('ef5117ec34a0fb06a8be1058caeb88016421bff9de5acced7f738b0dca82b52fc2495758242389315c29a66d46360211528d79ce8207af6f6a451eef6e0d8806'
            'SKIP'
            'c11e4af083f7212fcd04f12c9e75e19e535962def8f9553395b571d82b1e049c4d8763709494ffda6a23d9aa0f7ac9fd7cf844c948f153771c6da79106570c2d')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/hashcat-fix-version.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr SHARED=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr SHARED=1 install
  install -Dm 644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=5.0.0
pkgrel=1
epoch=1
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('x86_64')
depends=('ocl-icd' 'xxhash')
makedepends=('opencl-headers')
provides=('libhashcat.so')
license=('MIT')
source=(https://hashcat.net/files/${pkgname}-${pkgver}.tar.gz{,.asc}
        0001-add-optional-support-for-system-xxhash.patch)
sha512sums=('090f07a011961a2c9a9bdf31e8a5efae4b443b6b205e964a6cd74bed6efe8fae2377f22ce7728ad7e732e9f943cf30da88066fe0990d085547f1e2ba000a3d95'
            'SKIP'
            '3c1311e07d624b929e35273e282f1bfa0e86d5d1c46bb04a6576278a5455c440621f673e3c22846dda21cec4b328ec1276e15f4b286094fe7b40a7506cfa7a46')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../0001-add-optional-support-for-system-xxhash.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make \
    PREFIX=/usr \
    SHARED=1 \
    SYSTEM_XXHASH=1
}

package() {
  cd ${pkgname}-${pkgver}
  make \
    DESTDIR="${pkgdir}" \
    PREFIX=/usr \
    SHARED=1 \
    SYSTEM_XXHASH=1 \
    install
  install -Dm 644 docs/license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

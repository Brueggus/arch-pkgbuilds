# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.10
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='https://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=(https://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha256sums=('f1dcb1ec3e35685e46a8512137b8062daa1d0327900177998a405feab608adeb')
sha512sums=('4bf24f4b84fc17d1cecda7686259efdd4d36afc267a315be147545518ba965929f3a58d13f534aec4c1816e55cfb854ec647d09e170bb5f5eccfb4dc3469acd0')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
  sed -E 's|(diff_file = open\(diffhitlist_filename)\)|\1, "rb")|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

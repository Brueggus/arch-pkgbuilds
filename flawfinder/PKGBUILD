# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.9
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='https://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=(https://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha256sums=('d7aefd002cb63e125ff20bc2960388cf63e202b7e7f971e47129f2214faca90a')
sha512sums=('d359bbb63d8eba5bb1eec354e8843295100c1e3fe228c6326bc5c623e4b9ddebb33ce1ba68d2be6a9dd75adaa0f8b5f410fc26c2bdb56ff5900ab9b87dad543d')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
  sed -E 's|(diff_file = open\(diffhitlist_filename)\)|\1, "rb")|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

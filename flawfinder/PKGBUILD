# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.7
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='http://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python2')
source=(http://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha256sums=('8a2db4a890453c789af85d1e9147736d9e6f31853f632fbdfa6df63ce8252489')
sha512sums=('ecfe98c58e253b1d90b833747d69c41597ab7b4c82286e5f45dff45de90056216c25d2c9626540aebfc1e475882a04c3cfa3c0cb3ee242f498f5cdfe188d761f')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgbase=distorm
pkgname=('distorm' 'python-distorm' 'python2-distorm')
pkgver=3.3.8
pkgrel=1
pkgdesc='Powerful disassembler library for x86/AMD64'
url='https://github.com/gdabah/distorm'
arch=('x86_64')
license=('BSD')
makedepends=('python' 'python2' 'glibc')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gdabah/distorm/archive/v${pkgver}.tar.gz)
sha512sums=('bf65fe500089b4aa38d6430eb985f1dc3494b43c829379c56f91b5b4b25d19504fa3781a2fb2b03946f541962f6e564e46d6ec464c469ee21ecb773303b8494e')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
  sed -e '1i#!/usr/bin/env python' -i ${pkgbase}-${pkgver}/examples/python/sample.py
  sed -e '1i#!/usr/bin/env python2' -i ${pkgbase}-${pkgver}-py2/examples/python/sample.py
}

build() {
  (cd ${pkgbase}-${pkgver}
    make -C make/linux CFLAGS="${CFLAGS} ${CPPFLAGS} -fPIC"
    python setup.py build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

package_distorm() {
  depends=('glibc')
  provides=('libdistorm3.so')
  cd ${pkgbase}-${pkgver}
  make -C make/linux PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/lib/libdistorm3.so "${pkgdir}/usr/lib/libdistorm3.so".$(grep 'LIB_S_VERSION =' make/linux/Makefile|cut -d' ' -f3)
}

package_python-distorm() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 examples/python/sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-distorm() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 examples/python/sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

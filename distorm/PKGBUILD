# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgbase=distorm
pkgname=('distorm' 'python-distorm' 'python2-distorm')
pkgver=3.4.1
pkgrel=2
pkgdesc='Powerful disassembler library for x86/AMD64'
url='https://github.com/gdabah/distorm'
arch=('x86_64')
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools' 'glibc')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gdabah/distorm/archive/v${pkgver}.tar.gz)
sha512sums=('0e9f8b62bc190ef7d516f1902b6003adef9c7d5d4a5f985fb0bdfc5d4838b2805e2b8836b02d5eccdb3401e814417de615dec675aed9e606c93122ca8a0d2083')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
  sed -e '1i#!/usr/bin/env python' -i ${pkgbase}-${pkgver}/examples/python/sample.py
  sed -e '1i#!/usr/bin/env python2' -i ${pkgbase}-${pkgver}-py2/examples/python/sample.py
}

build() {
  (cd ${pkgbase}-${pkgver}
    make -C make/linux CFLAGS="${CFLAGS} ${CPPFLAGS} -fPIC"
    python setup.py build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

package_distorm() {
  depends=('glibc')
  provides=('libdistorm3.so')
  cd ${pkgbase}-${pkgver}
  make -C make/linux PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf "/usr/lib/libdistorm3.so.$(grep 'LIB_R_VERSION =' make/linux/Makefile|cut -d' ' -f3)" "${pkgdir}/usr/lib/libdistorm3.so"
}

package_python-distorm() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 examples/python/sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-distorm() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 examples/python/sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=xwallpaper
pkgver=0.6.4
pkgrel=1
pkgdesc='Wallpaper setting utility for X'
url='https://github.com/stoeckmann/xwallpaper'
arch=('x86_64')
license=('ISC')
depends=('pixman' 'libpng' 'libxpm' 'xcb-util' 'xcb-util-image' 'libjpeg-turbo' 'libseccomp' 'libseccomp.so')
source=(https://github.com/stoeckmann/xwallpaper/releases/download/v${pkgver}/xwallpaper-${pkgver}.tar.xz{,.sig})
sha512sums=('531910957c44f7e5ce7357fa46737967f673aeca53868012211eb91507f78bd028f8d84fcbee6e1237107816a09f8247cd0445c0907b60792ff5d07416630d56'
            'SKIP')
b2sums=('e81a1a9ec56ffe0a1953f1186c945c9ebb143ff683b8c0c01b819506b79520738935d04725420a24b38119221cbb71d9a5562bffd03e03736c65ba7e3c6a1713'
        'SKIP')
validpgpkeys=('CDB24BD3BDDCBCBBAE5CB6207DB470F35B0B8B18') # Tobias Stoeckmann <tobias@stoeckmann.org>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-zshcompletiondir=/usr/share/zsh/site-functions
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

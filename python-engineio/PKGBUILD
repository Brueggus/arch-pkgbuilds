# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-engineio
pkgver=3.11.2
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
depends=('python-six' 'python-urllib3' 'python-websocket-client' 'python-websockets')
optdepends=('python-eventlet: eventlet driver'
            'python-aiohttp: aiohttp driver'
            'python-tornado: tornado driver'
            'python-gevent: gevent driver'
            'python-gevent-websocket: gevent driver')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-eventlet' 'python-aiohttp' 'python-tornado'
             'python-urllib3' 'python-websocket-client' 'python-gevent' 'python-gevent-websocket' 'python-websockets')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-runner')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5e0e7a95c92438899831a7528291440fde0b0069714adae8419967c7bc4a79cc8c423a18226d1894477a43d6b4526226f6df8f96e4ef35238d9cc219d21f936e')
b2sums=('d4cc07eab9f38396252e6687ff96baa3a5983e1f3f623eb9f29069bbd444935a49d9c6cc461fc937f3424f1fc38d0761672879ffa867588811d6309375e218df')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

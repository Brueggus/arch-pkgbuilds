# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-engineio
pkgver=3.13.2
pkgrel=3
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-urllib3' 'python-websocket-client' 'python-websockets')
optdepends=('python-eventlet: eventlet driver'
            'python-aiohttp: aiohttp driver'
            'python-tornado: tornado driver'
            'python-gevent: gevent driver'
            'python-gevent-websocket: gevent driver')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-eventlet' 'python-aiohttp' 'python-tornado'
             'python-urllib3' 'python-websocket-client' 'python-gevent' 'python-gevent-websocket' 'python-websockets')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-runner')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        py39.patch)
sha512sums=('712b7f9646d0a8aa470d0f373ccfa4baf4e37eba6583def4325037a33e066819f3dbcb8cc6544ba2878a784e8ca22f67f490a7aa18f6c8f45c660900a1946561'
            'bdaf8f8f76c527a56000dac427c062b2be484381b3e1e05ec16a7c8f7b7fad8f07c16552c07dc08c002fb5067defa03de7dd8420e82289ea3ce836c9d88765dc')
b2sums=('5b63d660ba82f94dd784f5aea7e651a05cf914049ab99f9aef36972efbe4978832f7225e7d77098d5319f437ac77216c44d11efa5f353745b8d4c285c24560e8'
        '561bd0711b97c872b7f410a05f0bd5f65085eec27ee996b1491c9750d1bdc410ac38629a8717f1c5eb67897dd48da5664562d98e7a664271b1f65c4f6f4faac5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../py39.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

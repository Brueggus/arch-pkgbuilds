# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-engineio
pkgname=('python-engineio' 'python2-engineio')
pkgver=3.2.2
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-eventlet' 'python-aiohttp' 'python-tornado'
             'python-urllib3' 'python-websocket-client' 'python-gevent' 'python-gevent-websocket' 'python-websockets'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-eventlet' 'python2-tornado'
             'python2-urllib3' 'python2-websocket-client' 'python2-gevent' 'python2-gevent-websocket' 'python2-enum34')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-runner'
              'python2-mock' 'python2-pytest' 'python2-pytest-runner')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}.tar.gz
        ${pkgbase}-3.2.2-test-fixup.patch::https://github.com/miguelgrinberg/python-engineio/commit/61b04ea89cf2cc358a40f7854c31859aea8e30d6.patch)
sha256sums=('ec18a3499120d70e4a87826ccddd748b43aa479d6fcf43ef3905bd13a8943124'
            '0dd2ada7a6160ac59b6ae66a8f12e1bfe67da57262de96238caa8e2ded0b2ebe')
sha512sums=('1b12b820a25c0d59c030d2165f5f39928101464d27bc562b1b530ef404ec4513d494d83b3bd122542825ac0cc92009ed6043577bc9ebd6f29fc5c495e0096759'
            'b29801084e9bd5ff601ae1b8fa2c5233b8b0c024c260bb1a61558eb85f3b445a5ffdb0bbbb489867340d6465b62ddacfd1fc72b3d8d210291e524b27125dbc83')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -Np1 < "${srcdir}/${pkgbase}-3.2.2-test-fixup.patch"
}

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  python2 setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  python setup.py test
}

package_python-engineio() {
  depends=('python-six' 'python-urllib3' 'python-websocket-client' 'python-websockets')
  optdepends=('python-eventlet: eventlet driver'
              'python-aiohttp: aiohttp driver'
              'python-tornado: tornado driver'
              'python-gevent: gevent driver'
              'python-gevent-websocket: gevent driver')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/tests
}

package_python2-engineio() {
  depends=('python2-six' 'python2-urllib3' 'python2-enum34' 'python2-websocket-client')
  optdepends=('python2-eventlet: eventlet driver'
              'python2-tornado: tornado driver'
              'python2-gevent: gevent driver'
              'python2-gevent-websocket: gevent driver')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/tests
}

# vim: ts=2 sw=2 et:

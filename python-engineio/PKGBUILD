# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-engineio
pkgname=('python-engineio' 'python2-engineio')
pkgver=2.1.0
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six'
             'python2-setuptools' 'python2-sphinx' 'python2-six')
checkdepends=('python-mock' 'python-eventlet' 'python-pytest' 'python-pytest-runner' 'python-aiohttp'
              'python2-mock' 'python2-eventlet' 'python2-pytest' 'python2-pytest-runner')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}.tar.gz)
sha256sums=('e8f8c1e3d59208f402916a7887487c7d906330f907f1cfb754753e56bfd82b36')
sha512sums=('a4ecd8f55ec4278ce6e247f79e5f8c71e92c3af1bc0c9e19827f4b5a1aaf5b046ad2d5e75016e5028ea4165112cffc4ddc1c5311d2df433071acffb17ccd1f98')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${pkgbase}-${pkgver}
    python setup.py test
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-engineio() {
  depends=('python-six')
  optdepends=(
    'python-eventlet: eventlet support'
    'python-aiohttp: aiohttp support'
  )

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-engineio() {
  depends=('python2-six')
  optdepends=(
    'python2-eventlet: eventlet support'
  )

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

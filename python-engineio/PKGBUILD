# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-engineio
pkgname=('python-engineio' 'python2-engineio')
pkgver=3.0.0
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six'
             'python2-setuptools' 'python2-sphinx' 'python2-six')
checkdepends=('python-mock' 'python-eventlet' 'python-pytest' 'python-pytest-runner' 'python-aiohttp' 'python-tornado'
              'python2-mock' 'python2-eventlet' 'python2-pytest' 'python2-pytest-runner' 'python2-tornado')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}.tar.gz)
sha256sums=('25c483e85a468cbdfe77ff500e50698c3c2cc6f96baa18e1c72fa40fc8593a9b')
sha512sums=('6c5d30eddb08f67cae6e0ffb17476c6021811a47f0188114caa2747c55c68fd8bf1336556417485f564d771935e6ffcbb31e33419c029a64533915a2ef9d9b3d')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${pkgbase}-${pkgver}
    python setup.py test
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-engineio() {
  depends=('python-six')
  optdepends=(
    'python-eventlet: eventlet support'
    'python-aiohttp: aiohttp support'
    'python-tornado: tornado asyncio support'
  )

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-engineio() {
  depends=('python2-six')
  optdepends=(
    'python2-eventlet: eventlet support'
    'python-tornado: tornado asyncio support'
  )

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

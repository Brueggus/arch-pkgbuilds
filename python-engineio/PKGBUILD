# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-engineio
pkgname=('python-engineio' 'python2-engineio')
pkgver=3.1.0
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six' 'python-eventlet' 'python-aiohttp' 'python-tornado'
             'python-urllib3' 'python-websocket-client' 'python-gevent' 'python-websockets' 'python-gevent-websocket'
             'python2-setuptools' 'python2-sphinx' 'python2-six' 'python2-eventlet' 'python2-tornado'
             'python2-urllib3' 'python-websocket-client' 'python2-gevent' 'python2-gevent-websocket' 'python2-enum34')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-runner'
              'python2-mock' 'python2-pytest' 'python2-pytest-runner')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}.tar.gz)
sha256sums=('78e203bf72fcbf7fd48a2f8d3bf4c6ef0374c03aaa6da30c194ae4e39ec6d310')
sha512sums=('87d863cc69593d90a44e09bf85809a794c29d1f47d364e435b9f27d01625c14bf49dbc5de89a964d4988db06397b6eac0c8e3fa0b58ee56bc5bc671af719c1c9')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed '/gevent.queue/d' -i tests/test_server.py
}

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  python2 setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  python setup.py test
  # todo: python2 tests are wonky
  # python2 setup.py test
}

package_python-engineio() {
  depends=('python-six' 'python-eventlet' 'python-aiohttp' 'python-tornado' 'python-gevent'
           'python-websockets' 'python-gevent-websocket')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/tests
}

package_python2-engineio() {
  depends=('python2-six' 'python2-eventlet' 'python2-tornado' 'python2-gevent'
           'python2-gevent-websocket' 'python2-enum34')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/python-engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/tests
}

# vim: ts=2 sw=2 et:

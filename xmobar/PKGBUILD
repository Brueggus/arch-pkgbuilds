# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.31
pkgrel=13
pkgdesc='Minimalistic Text Based Status Bar'
url='https://hackage.haskell.org/package/xmobar'
license=('BSD')
arch=('x86_64')
depends=('libxft' 'libxinerama' 'libxrandr' 'libxpm' 'ghc-libs' 'haskell-x11'
         'haskell-x11-xft' 'haskell-utf8-string' 'haskell-network-uri'
         'haskell-hinotify' 'haskell-stm' 'haskell-parsec' 'haskell-parsec-numbers'
         'haskell-mtl' 'haskell-regex-base' 'haskell-regex-compat'
         'haskell-http' 'haskell-dbus' 'haskell-libmpd' 'haskell-iwlib'
         'wireless_tools' 'haskell-text' 'haskell-async' 'haskell-extensible-exceptions')
makedepends=('ghc')
source=(https://github.com/jaor/xmobar/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b0afe21f398a16a2e9d6e94ecb643fc5a259e1f60504ce50acd90ae31ae2e14d0925afb66f9593ff2a9bb016e39ab612027e836ece8ede665c66badac3386d01')

prepare() {
  cd xmobar-$pkgver
  sed -i -e 's/==.*0.3/== 0.4/' -e 's/< *4.12/<5/' -e 's/< *2.5/<3/' xmobar.cabal
}

build() {
  cd xmobar-${pkgver}
  runhaskell setup configure -O \
    --enable-shared \
    --prefix=/usr \
    --enable-executable-dynamic --disable-library-vanilla \
    --disable-library-vanilla \
    --flags="with_utf8 with_xft with_iwlib with_xpm with_inotify with_mpd with_dbus with_mpris"
  runhaskell setup build
}

package() {
  cd xmobar-${pkgver}
  runhaskell setup copy --destdir="${pkgdir}"
  install -Dm 644 license "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

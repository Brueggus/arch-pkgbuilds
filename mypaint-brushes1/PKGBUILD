# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: vimacs <https://vimacs.lcpu.club>

pkgname=mypaint-brushes1
pkgver=1.3.0
pkgrel=1
pkgdesc='Brushes used by MyPaint and other software using libmypaint (v1.x)'
url='https://github.com/mypaint/mypaint-brushes'
arch=('any')
license=('custom:CC0')
depends=('libmypaint<2')
source=(https://github.com/mypaint/mypaint-brushes/archive/v${pkgver}/${pkgname/1/}-${pkgver}.tar.gz)
sha256sums=('704bb6420e65085acfd7a61d6050e96b0395c5eab078433f11406c355f16b214')
sha512sums=('f8e0050cd700358d75cd25e40acd73905e2a53f21c6177cf67e8012aa4fbba8dc445109f07601c3846f95532a40feff4441081c0aa4e958049fc0ed8008fb414')

prepare() {
  cd ${pkgname/1/}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname/1/}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname/1/}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

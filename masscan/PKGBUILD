# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=masscan
pkgver=1.3.0
pkgrel=1
pkgdesc='TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes'
url='https://github.com/robertdavidgraham/masscan'
arch=('x86_64')
license=('AGPL3')
depends=('glibc' 'libpcap')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('dbf489d1fb1c882873071073ef8e65723bf3b805c794a5d13dfdcea539558f049845b5bc02ada1e313729954fe7ba29be727a777937b8e4d41778c9175d13824')
b2sums=('1d64a80345aa18cc0fdd00b5fca6708bcabded62571887e7171f6ac9e9f77f6d9f5bcca9c0ee04f67fcdf3ead1d6b242dc5129c6c9a94e560ac5380904a5a02a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|(CFLAGS )=|\1+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS+=" ${CPPFLAGS}"
  make CC=gcc
}

check() {
  cd ${pkgname}-${pkgver}
  make regress
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/masscan.8 -t "${pkgdir}/usr/share/man/man8"
  install -Dm 644 README.md doc/masscan.8.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

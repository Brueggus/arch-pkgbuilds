# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: vimacs <https://vimacs.lcpu.club>

pkgname=mypaint-brushes
pkgver=2.0.1
pkgrel=1
pkgdesc='Brushes used by MyPaint and other software using libmypaint'
url='https://github.com/mypaint/mypaint-brushes'
arch=('any')
license=('custom:CC0')
depends=('libmypaint')
source=(https://github.com/mypaint/mypaint-brushes/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5572c849c3cf01c33bceddb97f9ced55a219265738a46e406ddb5220ac59e22a')
sha512sums=('9dfaea56f6f1383cb3405afd05e339c00bad5e5bf383a9176cf24135899930bc719ff62787f1c6882c6b295440214dfc17815f81c043154cf9389756b8e85b65')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

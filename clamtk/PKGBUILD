# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>

pkgname=clamtk
pkgver=5.26
pkgrel=1
pkgdesc='Easy to use, light-weight, on-demand virus scanner for Linux systems'
url='https://dave-theunsub.github.io/clamtk/'
arch=('any')
license=('GPL')
depends=('clamav' 'perl' 'gtk2-perl' 'perl-locale-gettext' 'perl-libwww' 'perl-http-message'
         'perl-lwp-protocol-https' 'perl-text-csv' 'perl-json' 'python' 'zenity' 'desktop-file-utils'
         'gnome-icon-theme' 'cron')
source=(https://bitbucket.org/davem_/clamtk/downloads/clamtk-${pkgver}.tar.xz{,.asc})
sha512sums=('791228a678a0b3bd57ef6effc959bec0c78151f56715d85ca8fafc139ab649d417762db1f567e9fc28d10475138ba90012a9fe9b49840a7658039f9d42e7bcf5'
            'SKIP')
validpgpkeys=('5DD47B3B121EE5C354A20305F51D19546ADA59DE') # Dave Mauroni <dave.nerd@gmail.com>

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 clamtk -t "${pkgdir}/usr/bin"
  for f in lib/* ; do
    install -Dm 644 ${f} "${pkgdir}/usr/share/perl5/vendor_perl/ClamTk/`basename ${f}`"
  done

  for f in po/*.mo ; do
    install -Dm 644 "${f}" "${pkgdir}/usr/share/locale/`basename ${f} .mo`/LC_MESSAGES/clamtk.mo"
  done

  install -Dm 644 clamtk.1.gz -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 CHANGES DISCLAIMER README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm 644 images/clamtk.{xpm,png} -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 clamtk.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:

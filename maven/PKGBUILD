# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=maven
pkgver=3.6.1
pkgrel=2
pkgdesc='Java project management and project comprehension tool'
url='https://maven.apache.org'
arch=('any')
license=('APACHE')
depends=('java-environment>=7' 'bash' 'procps-ng')
makedepends=('maven' 'java-environment=8')
backup=('opt/maven/conf/settings.xml')
source=(https://www-eu.apache.org/dist/maven/maven-3/${pkgver}/source/apache-maven-${pkgver}-src.tar.gz{,.asc}
        0001-MNG-6642.patch
        maven.sh)
# https://www.apache.org/dist/maven/KEYS
validpgpkeys=(
  '042B29E928995B9DB963C636C7CA19B7B620D787' # StephenConnolly<stephenc@apache.org>
  'AE9E53FC28FF2AB1012273D0BF1518E0160788A2' # Karl Heinz Marbaise (ASF Key) <khmarbaise@apache.org>
)
sha256sums=('025921fff6ba827a25413ffc08fb1933565eb1f07ee2d3f228911913ee4f3c3f'
            'SKIP'
            '70858a5052fdcc93823f34fd6716200c433eec3990cef2058c186ef52fb9882d'
            '17d9b8efa6a922907110026273443bcc8d83e061e14ac82cb5052b483acc9b76')

prepare() {
  cd apache-maven-${pkgver}

  patch -Np1 -i ../0001-MNG-6642.patch
}

build() {
  cd apache-maven-${pkgver}

  export PATH="/usr/lib/jvm/java-8-openjdk/bin:${PATH}"
  mvn package \
    -Dmaven.repo.local="${srcdir}/repo" \
    -Dproject.build.sourceEncoding=UTF-8 -e
}

package() {
  cd apache-maven-${pkgver}

  install -Dm 755 ../maven.sh -t "${pkgdir}/etc/profile.d"
  install -d "${pkgdir}/opt/maven"

  # skip tests as they were already executed
  export PATH="/usr/lib/jvm/java-8-openjdk/bin:${PATH}"
  mvn install \
    -Dmaven.repo.local="${srcdir}/repo" \
    -Dproject.build.sourceEncoding=UTF-8 -e \
    -DdistributionTargetDir="${pkgdir}/opt/maven" \
    -DskipTests -Dmaven.test.skip=true

  install -d "${pkgdir}/usr/bin"
  ln -s /opt/maven/bin/mvn "${pkgdir}/usr/bin/mvn"
  ln -s /opt/maven/bin/mvnDebug "${pkgdir}/usr/bin/mvnDebug"
}

# vim: ts=2 sw=2 et:

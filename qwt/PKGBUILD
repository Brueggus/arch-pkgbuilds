# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt
pkgver=6.1.4
pkgrel=1
pkgdesc='Qt Widgets for Technical Applications'
url='https://qwt.sourceforge.net/'
arch=('x86_64')
depends=('qt5-base' 'qt5-svg')
optdepends=('qt5-tools: For Designer plugin')
makedepends=('qt5-tools')
license=("custom:${pkgname}")
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('6135ce47fd4f9ff5b705193fc1f7410cfd5a0b84931bb17db750f6a5486ae810c261dae32431bd52838c36eadee02487148e1efa71c465c63aa43062eec160da')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -e '/^\s*QWT_INSTALL_PREFIX/ s|=.*|= /usr|' \
      -e '/^QWT_INSTALL_DOCS/ s|/doc|/share/doc/qwt|' \
      -e '/^QWT_INSTALL_HEADERS/ s|include|&/qwt|' \
      -e '/^QWT_INSTALL_PLUGINS/ s|plugins/designer|lib/qt/&|' \
      -e '/^QWT_INSTALL_FEATURES/ s|features|lib/qt/mkspecs/&|' \
      -i qwtconfig.pri
}

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5 qwt.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install

  mv "${pkgdir}/usr/share/doc/qwt/man" "${pkgdir}/usr/share"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

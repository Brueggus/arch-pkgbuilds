# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Abel Stern <abel.stern AT gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=byacc
pkgver=20170201
pkgrel=1
pkgdesc='Berkeley reimplementation of the Unix parser generator Yacc'
url='http://invisible-island.net/byacc/'
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc')
replaces=('byacc-noconflict')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz{,.asc})
sha512sums=('f45e6f5eefe4a4bbcd233fbe06cf25fecec3ecabebea9064db0f52d900ae90783dd180f90d70901f82e2b632ce0432d28a913d312b15225e6256344c176a3638'
            'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # Thomas Dickey <dickey@invisible-island.net>

build() {
  cd ${pkgname}-${pkgver}
  sed -n '/is distributed/,/distributed freely/p' README > LICENSE
  ./configure --prefix=/usr --mandir=/usr/share/man --program-transform=s,^,b,
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Abel Stern <abel.stern AT gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=byacc
pkgver=20180510
pkgrel=1
pkgdesc='Berkeley reimplementation of the Unix parser generator Yacc'
url='https://invisible-island.net/byacc/'
arch=('x86_64')
license=('custom')
depends=('glibc')
replaces=('byacc-noconflict')
source=(ftp://ftp.invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz{,.asc})
sha512sums=('89ac9d4bd1edfdcfa304b0efacc478dbdd84cc2a1cd4ef75515863b1ea2731b106202c758b469c39a4379bb155c0a2365baa290797da0a6bb3904295431556d6'
            'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # Thomas Dickey <dickey@invisible-island.net>

build() {
  cd ${pkgname}-${pkgver}
  sed -n '/is distributed/,/distributed freely/p' README > LICENSE
  ./configure --prefix=/usr --mandir=/usr/share/man --program-transform=s,^,b,
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

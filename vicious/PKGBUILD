# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.3.0
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url='https://github.com/Mic92/vicious'
arch=('any')
license=('GPL2')
depends=('lua')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Mic92/vicious/archive/v${pkgver}.tar.gz)
sha256sums=('12dc9a4e35db8e314ee936b45e5ad5fa740fc8d0936c3787e0854870267c208a')
sha512sums=('a48fa87dfdcc94da878beac0d859859a4f3e77b58b65cfbf5f66fa05ecc677a5609ee05c0ff3cbd86c406c47eaa7a46dc87655b5f6fac900742a8f6a7a55e697')

package() {
  cd ${pkgname}-$pkgver
  install -Dm 644 *.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}"
  install -Dm 644 widgets/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/widgets"
  install -Dm 644 contrib/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/contrib"
  install -Dm 644 README.md Changes.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:

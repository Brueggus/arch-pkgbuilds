# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.3.2
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url='https://github.com/Mic92/vicious'
arch=('any')
license=('GPL2')
depends=('lua')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=(https://github.com/Mic92/vicious/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('696d5d114bbda7864662447b5abcadc4112890975937365282bfe0749e1bd673')
sha512sums=('6977df7fee3ff1754459edc5be70b6f23590f69a0e46aaeec70aefcac3e4db48bdc89880557555a542584681ac2ab4982c7abd6f0b40f0ccd66d9925ff129755')

package() {
  cd ${pkgname}-$pkgver
  install -Dm 644 *.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}"
  install -Dm 644 widgets/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/widgets"
  install -Dm 644 contrib/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/contrib"
  install -Dm 644 README.md Changes.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:

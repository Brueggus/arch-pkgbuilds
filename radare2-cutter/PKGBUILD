# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=radare2-cutter
_gitcommit=1aad797722f9fc60b80792a4cc02380c9241714a
pkgver=1.10.3
epoch=1
pkgrel=2
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python' 'jupyter'
         'pyside2' 'python-shiboken2' 'graphviz' 'gcc-libs')
makedepends=('git' 'cmake' 'shiboken2')
optdepends=('r2ghidra-dec: ghidra decompiler plugin')
source=(${pkgname}::"git+https://github.com/radareorg/cutter#commit=${_gitcommit}"
        radare2-cutter-qt5.15.patch)
sha512sums=('SKIP'
            '966bf673904d3c633a09a7c6755d32de1205c07849def03ac35433eb6587ad5fd4860e33c11d54e6b4cff5470ed91cf35eacb0a15ca3bb4e1d095fdfa0e3e87b')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  patch -d $pkgname -p1 -i ../radare2-cutter-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd ${pkgname}/src
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCUTTER_ENABLE_PYTHON=ON \
    -DCUTTER_ENABLE_PYTHON_BINDINGS=ON \
    -DCUTTER_USE_BUNDLED_RADARE2=OFF \
    -DCUTTER_USE_ADDITIONAL_RADARE2_PATHS=OFF \
    -DCUTTER_ENABLE_CRASH_REPORTS=OFF \
    -DCUTTER_ENABLE_GRAPHVIZ=ON
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  install -Dm 755 src/build/Cutter -t "${pkgdir}/usr/bin"
  install -Dm 644 src/org.radare.Cutter.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 src/img/cutter.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=radare2-cutter
pkgver=1.8.1
pkgrel=1
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python' 'jupyter')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('SKIP')

build() {
  mkdir -p cutter-$pkgver/build
  cd cutter-$pkgver/build

  qmake ../src/Cutter.pro

  make
}

package() {
  cd cutter-$pkgver

  install -Dm 755 build/Cutter -t "$pkgdir/usr/bin"
  install -Dm 644 src/org.radare.Cutter.desktop -t "$pkgdir/usr/share/applications"
  install -Dm 644 src/img/cutter.svg -t "$pkgdir/usr/share/icons/hicolor/scalable/apps"
  install -d "$pkgdir/usr/share/doc/$pkgname"

  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=rack
pkgname=ruby-rack
pkgver=2.2.2
pkgrel=1
pkgdesc='Minimal, modular, and adaptable interface for developing web applications'
url='https://rack.github.io/'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-minitest'
             'ruby-minitest-global_expectations' 'ruby-minitest-sprint')
options=('!emptydirs')
source=(https://github.com/rack/rack/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('73bddac03ac2702b803cfc09b7e56ac9cb86775d4d697ca6f98bb4302fe6ec9e')
sha512sums=('d0d27d091584b6cabafaa1d1053e013bc28ac0dc7da7b5bf28e528f5479091db1c896052a4ffe75be22011d3bc35c41ce73514a47e66b28ff8d3b723c46fd42c')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec Rakefile
}

build() {
  cd ${_gemname}-${pkgver}
  rake build
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"

  cd "${pkgdir}/${_gemdir}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=0.8.17
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='http://scrot.sourcearchive.com/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('giblib')
source=("http://scrot.sourcearchive.com/downloads/${pkgver%.*}-${pkgver##*.}/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "http://scrot.sourcearchive.com/downloads/${pkgver%.*}-${pkgver##*.}/${pkgname}_${pkgver%.*}-${pkgver##*.}.debian.tar.gz")
sha256sums=('613d1cf524c2b62ce3c65f1232ea4f05c7daf248d5e82ff2a6892c98093994f2'
            'c0929e9ce56e2e6965d43745f0b07787a12fb7d9d98a61027a37a1151a0ccd87')
sha512sums=('cba8f589e45758ddbfe4e276399a1ecb0dbe29569be5d85d97733e7f64de2911bd2d03e62700ad0c718a1fc886c2e3def9dee1de5cac884f9e65e772cebe838c'
            'ba7372cb4cb6a54863f15ed50c5d67c1f4e6109a51f8612a95c10b99e282249323369fef295d0e283af6d357ff09368c4da2b94390a6b651f24ae9d62cc14554')

prepare(){
    for patch in $(<"${srcdir}/debian/patches/series"); do
        patch -Np1 -d "${srcdir}/${pkgname}-${pkgver%.*}" -i "${srcdir}/debian/patches/${patch}"
    done
}
build() {
  cd ${pkgname}-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver%.*}
  make DESTDIR=${pkgdir} docsdir=/usr/share/doc/scrot install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=1.1.1
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://github.com/resurrecting-open-source-projects/scrot'
arch=('x86_64')
license=('MIT')
depends=('giblib' 'libxfixes' 'libxcursor')
makedepends=('autoconf-archive')
source=(https://github.com/resurrecting-open-source-projects/scrot/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('63bc645d5dfff51e441541dc999379b34722c6428a4cd642e20005e6901383ba')
sha512sums=('6444fec6d793053b120a6f81bb1696b2c3d1a48f2b7d3949f63ab7e97cbd60d87962b87e52bf43acfae12c3381d59f7924c0f7c71d5372876d14a698f9b9f138')

prepare(){
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm 644 README ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=1.3
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://github.com/resurrecting-open-source-projects/scrot'
arch=('x86_64')
license=('MIT')
depends=('giblib' 'libxfixes' 'libxcursor')
makedepends=('autoconf-archive')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('45828824701c7574676d0746d01f3dc8313ea91a272fa169b616765442e8842b67d8aa73f47009a66d34dd6b89a230fd9fb42bc598e88baae70fc45345c13a4c')
b2sums=('1972edfa3b61c95fd786d0b4db13633778293b2c671c395939ea0b77d49714e644abd60cfae5656f03fd1c701185d2c8961c45d62989e882f2cd8f4a824dee57')

prepare(){
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

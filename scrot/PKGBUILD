# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=0.9.1
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://tracker.debian.org/pkg/scrot'
arch=('x86_64')
license=('MIT')
depends=('giblib')
source=(http://http.debian.net/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver%.*}.orig.tar.gz
        http://http.debian.net/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver%.*}-${pkgver##*.}.debian.tar.xz)
sha256sums=('d9b6141c652f4c4c1a9d412d6d57d2045b515e70cd726e4787f5f066c837269e'
            'e3313362fae895d393f7b7d26e087c45f61ccaee4e31251af616604e77258547')
sha512sums=('e449b951fcbe2124fe874e5516d397a2d7ebb99ec8fceeb30c55cbee2596dec39ef5dff9db9acf1ff2930bc3752d4c1c1fbe0a28bc284481676370f330046f7b'
            'fbd33ae9b6c500b000baf01564d912ea05185651d9eac8e052c945aa7c03b11b550fbaafa0a655d499028e653b6517220f16a514613853e37cda8009b4b79db4')

prepare(){
    for patch in $(<"${srcdir}/debian/patches/series"); do
        msg2 "Applying ${patch}..."
        patch -Np1 -d "${srcdir}/${pkgname}-${pkgver%.*}" -i "${srcdir}/debian/patches/${patch}"
    done
  cd ${pkgname}-${pkgver%.*}
    autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver%.*}
  make DESTDIR=${pkgdir} docsdir=/usr/share/doc/scrot install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:

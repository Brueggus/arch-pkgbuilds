# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=smplayer-themes
pkgver=17.2.0
pkgrel=1
epoch=1
pkgdesc='Themes for SMPlayer'
url='http://smplayer.sourceforge.net/'
arch=('any')
license=('GPL2')
depends=('smplayer')
makedepends=('optipng')
source=(https://downloads.sourceforge.net/smplayer/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('c2ca64aed6feda30de2be62f1ced99122cc92506c086dbb2a8f9b8a5a6b0f9d6bb3c85dfa43be0dea848f8e2d094115de1acf75da3c23ecf8d46d3685d22e4ba')

prepare() {
  cd ${pkgname}-${pkgver}
  # Fix invalid PNG icons to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:

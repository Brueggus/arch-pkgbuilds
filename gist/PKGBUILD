# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris van Dijk (quigybo) <quigybo at hotmail dot com>
# Contributor: Scott Olson <scott at scott-olson dot org>

pkgname=gist
pkgver=5.1.0
pkgrel=1
pkgdesc='Potentially the best command line gister'
url='https://github.com/defunkt/gist'
arch=('any')
license=('custom:MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-ronn')
optdepends=("xclip: put pasted gist's url into X selection automagically"
            "xsel: put pasted gist's url into X selection automagically")
source=(https://github.com/defunkt/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7a63dd3f9c0d532890061f35f71cc7bc38d99958e1a34f1d3c6b596eaf6a2dcbb7a51ed9e8f3df961589038d5ee0391c348e636f277f7314eef26a5fafd90eac')
b2sums=('2a8aa2ee5db34510040b5dca75e81b09c57e1a2e3e4db52ad34f82859407abf8c02670a566ac26d8b5e4176abddf1201af06e0cee7d6a4d783e4ebd9043d9734')

build() {
  cd ${pkgname}-${pkgver}
  rake build
}

package() {
  cd ${pkgname}-${pkgver}
  PREFIX="${pkgdir}/usr" rake install
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

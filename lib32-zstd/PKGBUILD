# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

_pkgname=zstd
pkgname=lib32-zstd
pkgver=1.4.4
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm (32-bit)'
url='https://www.zstd.net/'
arch=('x86_64')
license=('BSD' 'GPL2')
depends=('zstd' 'lib32-zlib' 'lib32-xz' 'lib32-lz4')
makedepends=('gtest')
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)
sha256sums=('59ef70ebb757ffe74a7b3fe9c305e2ba3350021a918d168a046c6300aeea9315')
sha512sums=('f12b6f38bd322a0538179ef90e4c83d2cba29e1fef391a8e8e964785e5761e7ba685f62e9a5a1d6b66171fd64e4fd6374e607569531e2f4c05c470cff2294454')
b2sums=('789d1968938d5b30d0c29f7819f7a4ed20bd70d8098dd9d37aa67605fe7af82d33db7a677295313260f1bf019afbf4a0e9b976d3452c47532cbbc8aa77c24d99')

build() {
  cd ${_pkgname}-${pkgver}
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${_pkgname}-${pkgver}
  make \
    PREFIX=/usr \
    LIBDIR=/usr/lib32 \
    PKGCONFIGDIR=/usr/lib32/pkgconfig \
    DESTDIR="${pkgdir}" \
    install
  rm -rf "${pkgdir}"/usr/{share,include,bin}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

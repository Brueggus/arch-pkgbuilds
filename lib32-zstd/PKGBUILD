# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

_pkgname=zstd
pkgname=lib32-zstd
pkgver=1.4.4
pkgrel=2
pkgdesc='Zstandard - Fast real-time compression algorithm (32-bit)'
url='https://www.zstd.net/'
arch=('x86_64')
license=('BSD' 'GPL2')
depends=('zstd' 'lib32-zlib' 'lib32-xz' 'lib32-lz4')
makedepends=('gtest')
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz
        fix-pkgconfig-file.patch)
sha256sums=('59ef70ebb757ffe74a7b3fe9c305e2ba3350021a918d168a046c6300aeea9315'
            'fe09744976e49ca7c441d1b00dcbb60e911831b267ebf64c0c81e0b6b0cbb598')
sha512sums=('f12b6f38bd322a0538179ef90e4c83d2cba29e1fef391a8e8e964785e5761e7ba685f62e9a5a1d6b66171fd64e4fd6374e607569531e2f4c05c470cff2294454'
            'bf21e66654459bf1f7d00d845e674ba57d80a9e468c9c67f908d1599b9150f20c1f207f928a7c124bd2b3a6003dbcf61e46ab634d7b11ff8fd40143eb605a2b7')
b2sums=('789d1968938d5b30d0c29f7819f7a4ed20bd70d8098dd9d37aa67605fe7af82d33db7a677295313260f1bf019afbf4a0e9b976d3452c47532cbbc8aa77c24d99'
        '30b24315c8c86a7f873449b50dab6375c79b6e774fbdfb3de86f40e0a968fce33b655eb56552ca391bcf3c5501b07e624c4c8e4dddffe47f3d3852dd15920107')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i ../fix-pkgconfig-file.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${_pkgname}-${pkgver}
  make \
    PREFIX=/usr \
    LIBDIR=/usr/lib32 \
    PKGCONFIGDIR=/usr/lib32/pkgconfig \
    DESTDIR="${pkgdir}" \
    install
  rm -rf "${pkgdir}"/usr/{share,include,bin}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:

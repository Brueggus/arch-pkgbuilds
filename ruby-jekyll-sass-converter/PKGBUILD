# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=jekyll-sass-converter
pkgname=ruby-${_gemname}
pkgver=2.0.1
pkgrel=3
pkgdesc='Sass converter for Jekyll'
url='https://github.com/jekyll/jekyll-sass-converter'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc' 'ruby-sassc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jekyll/jekyll-sass-converter/archive/v${pkgver}.tar.gz)
sha256sums=('c748e3813ef577d504bbeb582bb2710e3303bcbd46a58089d52c063afb0f0441')
sha512sums=('5bb39d78ae9c2b0edfa0682dd6e1c6c5636a76a207925c91a0ac471a31037351415b9e265ecbac0fb1dbc73a18635ad334478f38042d6bdb8c57b6c65598db93')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

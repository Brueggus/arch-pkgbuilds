# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=jekyll-sass-converter
pkgname=ruby-${_gemname}
pkgver=2.0.0
pkgrel=1
pkgdesc='Sass converter for Jekyll'
url='https://github.com/jekyll/jekyll-sass-converter'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc' 'ruby-sass')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jekyll/jekyll-sass-converter/archive/v${pkgver}.tar.gz)
sha256sums=('0adec9b46c093d0e4385a71ad784647a3b748b16e47a948d3f0202865132960f')
sha512sums=('f3372fadd414c416f4e47175460b78ae4a08cc0659da46fecefc5e7703062ed4e96676d10f68eb474cb86e5e33d66e02f259484aab7a24fd5be080256a0b82cf')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

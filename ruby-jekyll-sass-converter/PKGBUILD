# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=jekyll-sass-converter
pkgname=ruby-${_gemname}
pkgver=1.5.0
pkgrel=1
pkgdesc='Sass converter for Jekyll'
url='https://github.com/jekyll/jekyll-sass-converter'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-sass')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jekyll/jekyll-sass-converter/archive/v${pkgver}.tar.gz)
sha256sums=('b6e42e91c38e3340d8b10577297e9c1b9d9ee38e613246a48f6db4a046bb6a98')
sha512sums=('ece83d8c13e93a90c66ed899accc77746d56067f6841ca0d29050168c52d9eea6de3d78760b0f468be2dbc9b0b9df5ac02d5066a26809a33cdadd591fbbc9b52')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a example "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:

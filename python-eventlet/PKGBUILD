# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-eventlet
pkgname=('python-eventlet' 'python2-eventlet')
_pyname=eventlet
pkgver=0.20.1
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='http://eventlet.net'
arch=('any')
license=('MIT')
makedepends=('python2-pip' 'python-greenlet' 'python-setuptools' 'python-sphinx' 'python2-greenlet' 'python2-setuptools' 'python2-sphinx' 'python2-subprocess32')
#TODO: add 'python-dnspython' if test is fixed
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq'
              'python2-psycopg2' 'python2-nose' 'python2-pyopenssl' 'python2-httplib2' 'python2-mock' 'python2-pyzmq' 'python2-dnspython')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/eventlet/eventlet/archive/v${pkgver}.tar.gz
        python36-1.patch::https://github.com/eventlet/eventlet/commit/461348f9a8459724be87c281eea4e0408138a82c.patch
        python36-2.patch::https://github.com/eventlet/eventlet/commit/ed79125c652d7ea02cb79a84c1292fdc19932a94.patch
        python36-3.patch::https://github.com/eventlet/eventlet/commit/7595a5a4b789ce9492f88c96a412cdf1e47e60cc.patch)
sha512sums=('fbd6073c0046f58eb9375a358010c56727cc9674c118558244dc7f4c10e2763923c0a0a12623c921b261667bbfe5c6b1eaf5ed0835338ffbe927596dd2ee67f1'
            'ffd4e3d5c8ee3eb0235f075708a183186788e780b1a6ad020fa258a880f2afdcdea0cb1da4f60c11e025520c9dbfeea5be438ae365a00e826206682ed35c6b59'
            '12d94cc282e4e70d0c0682e6f90d695339451629eb7b72792c13fc7bc1747598df134d1819ea248fdc499e6522e689bd29a099478badeb6f44380137d40b7cf7'
            'e96ad673894b9238af2fe7d4afcce179c66b5fb83c3464e949399f0d19b9355d7975b57f706ea18b65414ec42fe28f44156e348da34ade0a139c6d488354f9f0')

prepare() {
  (cd ${_pyname}-${pkgver}
    sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
    patch -p1 < "${srcdir}/python36-1.patch"
    patch -p1 < "${srcdir}/python36-2.patch"
    patch -p1 < "${srcdir}/python36-3.patch"
  )
  cp -a ${_pyname}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${_pyname}-${pkgver}-py2/examples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C doc text
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C doc text
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests -sv tests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2 -sv tests
  )
}

package_python-eventlet() {
  depends=('python' 'python-greenlet')
  optdepends=('python-psycopg2: non-blocking PostgreSQL support'
              'python-pyopenssl: non-blocking SSL support'
              'python-httplib2: non-blocking HTTP support'
              'python-pyzmq: non-blocking ZeroMQ support'
              'python-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-eventlet() {
  depends=('python2' 'python2-greenlet' 'python2-subprocess32')
  optdepends=('python2-psycopg2: non-blocking PostgreSQL support'
              'python2-pyopenssl: non-blocking SSL support'
              'python2-httplib2: non-blocking HTTP support'
              'python2-pyzmq: non-blocking ZeroMQ support'
              'python2-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:

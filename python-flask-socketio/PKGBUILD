# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-flask-socketio
pkgname=('python-flask-socketio' 'python2-flask-socketio')
_pkgname=Flask-SocketIO
pkgver=2.9.4
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-flask' 'python-eventlet' 'python-socketio'
             'python2-setuptools' 'python2-sphinx' 'python2-flask' 'python2-eventlet' 'python2-socketio')
checkdepends=('python-coverage' 'python-pytest'
              'python2-coverage' 'python2-pytest')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha256sums=('bce1490faa1765707e5aaa2f8ccd0e623d4b1c94ab2d39fa52dafa1257a871bc')
sha512sums=('805fd917d25ad0b47e58f620bc63f4b821b9400f3bf437f534e65bc19f49012ee176a176c5d73fe3189bc99506f9028ccf13c3c5e01867ecae5f8614976b7e03')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    py.test test_socketio.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    py.test2 test_socketio.py
  )
}

package_python-flask-socketio() {
  depends=('python-flask' 'python-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-socketio() {
  depends=('python2-flask' 'python2-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

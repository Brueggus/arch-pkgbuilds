# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-flask-socketio
pkgname=('python-flask-socketio' 'python2-flask-socketio')
_pkgname=Flask-SocketIO
pkgver=2.9.1
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-flask' 'python-eventlet' 'python-socketio'
             'python2-setuptools' 'python2-sphinx' 'python2-flask' 'python2-eventlet' 'python2-socketio')
checkdepends=('python-coverage' 'python-pytest' 'python2-coverage' 'python2-pytest')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha256sums=('c649d1e826b91c740e304b91c0777f44f91ee8ee083bc07ec4f3caea404826d4')
sha512sums=('772b304ef39cb68840b7ad9842696c5fb381533aa81decd72e920df15a3f042e9bd7dd82d460d28ba41d11a06f6010e3ad6f1bfb018a9caab96d15c233489e45')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    py.test test_socketio.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    py.test2 test_socketio.py
  )
}

package_python-flask-socketio() {
  depends=('python-flask' 'python-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-socketio() {
  depends=('python2-flask' 'python2-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

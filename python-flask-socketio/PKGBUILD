# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-flask-socketio
pkgname=('python-flask-socketio' 'python2-flask-socketio')
_pkgname=Flask-SocketIO
pkgver=3.0.2
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-flask' 'python-eventlet' 'python-socketio'
             'python2-setuptools' 'python2-sphinx' 'python2-flask' 'python2-eventlet' 'python2-socketio')
checkdepends=('python-coverage' 'python-pytest'
              'python2-coverage' 'python2-pytest')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha256sums=('64b8ab4503290884921aacd3b399a5f1e82266e20c98d87a0b339284bca92d05')
sha512sums=('5c5dab420cc08e01fc7ea6f9ae90e97330a79bbf1477f1f6f5909a8c2ac98a614aaa4c8c2264fda101b776271cb0e532e82749c3a54d1400eeca875bea885ebb')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    py.test test_socketio.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    py.test2 test_socketio.py
  )
}

package_python-flask-socketio() {
  depends=('python-flask' 'python-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-socketio() {
  depends=('python2-flask' 'python2-eventlet' 'python-socketio')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:

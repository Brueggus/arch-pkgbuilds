# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: fauno <fauno@parabola.nu>

pkgname=miniupnpd
pkgver=2.1.20180706
pkgrel=1
pkgdesc='Lightweight UPnP IGD daemon'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
backup=('etc/miniupnpd/miniupnpd.conf')
depends=('iptables' 'net-tools' 'util-linux')
makedepends=('lsb-release' 'procps-ng')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz{,.sig}
        miniupnpd.service)
sha256sums=('fc2d2fd044d8c3f8d02b63d70489bb35ece836a4fc1b6386865ac8fbe8d8b006'
            'SKIP'
            'a224a6e12d47809fe0a051ed5731084245e34a33f42ffe3127bcceff75d0e215')
sha512sums=('90881e08754f9fe3d1ef01da1fe437626d1fef09bea5eab9c1bcf3347e1197038ab87de8c7c454083ba90a86760230a323a6c293307b0fcecefbd5cfb658d227'
            'SKIP'
            '6387f646d91257e50159f66c5168830c5c845e1de739e52cc86647baafaa96ed3a5d00b6c7e46080c3dd998a486d81183c3c11ce9bf32e2f6d9fdd40b0ea8bef')
validpgpkeys=('BEB7EA42900FF505B7907AE50FF11B67A5C0863C') # miniupnp (miniupnp !) <miniupnp@free.fr>

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/gzip/d' -i Makefile.linux
  sed 's|/sbin/sysctl|sysctl|g' -i genconfig.sh
}


build() {
  cd ${pkgname}-${pkgver}
  CONFIG_OPTIONS="--ipv6 --leasefile" make -f Makefile.linux config.h
  make -f Makefile.linux
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" PREFIX=/ INSTALLPREFIX=/usr SBININSTALLDIR=/usr/bin -f Makefile.linux install

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ../miniupnpd.service -t "${pkgdir}/usr/lib/systemd/system"

  sed -i 's:/s\?bin/iptables:/usr/bin/iptables:
          s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
  sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
             s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"

  rm -r "${pkgdir}/etc/init.d"
}

# vim: ts=2 sw=2 et:

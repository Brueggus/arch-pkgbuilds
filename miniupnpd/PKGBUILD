# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: fauno <fauno@parabola.nu>

pkgname=miniupnpd
pkgver=2.0.20180503
pkgrel=1
pkgdesc='Lightweight UPnP IGD daemon'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
backup=('etc/miniupnpd/miniupnpd.conf')
depends=('iptables' 'net-tools' 'util-linux')
makedepends=('lsb-release')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz
        miniupnpd.service)
sha256sums=('b61bed16865f8692803a9f155523ca7c6e01c65db20362b6173226b08ce12a0c'
            '66cd1ed0ae55c683910d46c2e486a8b52e48088b70248323cf894bc962ede48f')
sha512sums=('7615f28d8e41bbe5b7343159a526ba33e643d14e2be7c2382461fda36ecfb3420ef2903e0cc9fa92ffe376f8609feb9403a316244eec0459e78a6b5ad96462ee'
            '5075892c664c4d935da66436e3f45ee077ddf77e80c7553930929fe2b9d587aaeb5df5a4febdbac8fa6d5b22dae35662012ae22eabb87fe3472e25b91afd3466')

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/gzip/d' -i Makefile.linux
}


build() {
  cd ${pkgname}-${pkgver}
  CONFIG_OPTIONS="--ipv6 --leasefile" make -f Makefile.linux config.h
  make -f Makefile.linux
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" PREFIX=/ INSTALLPREFIX=/usr SBININSTALLDIR=/usr/bin -f Makefile.linux install

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ../miniupnpd.service -t "${pkgdir}/usr/lib/systemd/system"

  sed -i 's:/s\?bin/iptables:/usr/bin/iptables:
          s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
  sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
             s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"

  rm -r "${pkgdir}/etc/init.d"
}

# vim: ts=2 sw=2 et:

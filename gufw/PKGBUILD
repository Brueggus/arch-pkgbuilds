# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
_pkgver=17.04
pkgver=${_pkgver}.1
pkgrel=3
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python-gobject' 'ufw' 'webkit2gtk' 'net-tools' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://launchpad.net/gui-ufw/${_pkgver}/${_pkgver}/+download/gui-ufw-${pkgver}.tar.gz)
sha512sums=('f8168652d6bfc0b50de3a74e1db08539642347b652af1d18310bc1fcaa774da5c72fef57778562f042bf19eb329fd0d576c15ce6200ee87ce48700fa02a7104c')

prepare() {
  cd gui-ufw-${pkgver}
  sed 's|python3.5|python3.6|g' -i bin/gufw-pkexec
}

package(){
  cd gui-ufw-${pkgver}
  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:

# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
_pkgver=18.10
pkgver=${_pkgver}.0
pkgrel=1
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python' 'python-gobject' 'ufw' 'webkit2gtk' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://launchpad.net/gui-ufw/trunk/${_pkgver}/+download/gui-ufw-${pkgver}.tar.gz)
sha256sums=('c44468550cbcb35ebee0dd8339341447118811ccd0714e79350136117421b93b')
sha512sums=('a2f733d9622e4f22d230243cc25dca0933eae163933d08a98358a933056cf29630c1fa34d7cc449fb45890c55770023aef1e6a3d72702dc1fd0357f38e09a7e7')

prepare() {
  cd gui-ufw
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  sed -E "s|python3\.[0-9]|python${PYTHONVERSION}|g" -i bin/gufw-pkexec
}

package() {
  cd gui-ufw
  # required so desktop, icon and translation files get installed
  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
